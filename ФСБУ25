Учёт аренды по ФСБУ 25: архитектура и реализация

1. Архитектура данных

Регистр сведений «_УчетДолгосрочнойАренды»
Хранит все движения по договорам аренды.
Измерения: Договор, ОбъектППА, Показатель, ПериодГрафика, ПериодДисконтирования и др.
Ресурсы: Обязательство, СтоимостьППА, НДС, АвансОбязательство, АвансНДС, АвансАктив.
Период — дата попадания в учёт.
Каждый документ добавляет слой данных. Старые записи не меняются.
Итог по договору = сумма всех слоёв с учётом периода.

Регистр сведений «_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик»
Детализация по объектам и периодам.
Связан с табличной частью документа через УИД строки.

2. Документ «График договора долгосрочной аренды»

Реквизиты шапки:
Договор, ДатаНачала, ДатаОкончания
СтавкаДисконтирования, Аванс
ВидГрафика (Расчет, Расторжение, ЗаменаСтороны, ИзменениеСтавкиНДС, Сторно)

Табличные части:
Изменения арендной платы — периоды действия ставок, суммы, НДС
Состав ППА — объекты аренды, базы распределения, счета отнесения процентов

3. Движок расчёта графика

Находится в форме документа, процедура РассчитатьИндивидуальныеГрафикиНаСервере().

Что делает:
Принимает готовый помесячный график
График арендных платежей уже сформирован в табличной части документа «ГрафикПоДоговору» — это вводные данные, а не результат расчёта.

Для каждого объекта из «Состав ППА»
создаёт копию общего графика, где арендная плата и НДС распределены пропорционально базе распределения объекта.
Получается индивидуальный график объекта (ТЗИГ).

Для каждого объекта выполняет расчёт:
приведенная стоимость (дисконтирование по дням)
проценты, изменение обязательства
долгосрочная и краткосрочная части
амортизация ППА (линейно, исходя из срока)
авансы и их амортизация
корректировка ОПС (если объект не новый)

Записывает результат:
индивидуальные графики сохраняются в регистр «_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик»
затем они сворачиваются (суммируются по периодам) в общий график
свернутые данные записываются обратно в табличную часть «ГрафикПоДоговору» — теперь уже с расчётными показателями

Вход:
таблица «ГрафикПоДоговору» (вводная, только платежи)
таблица «Состав ППА» (объекты с базами распределения)
реквизиты документа (ставка, даты, аванс)

Выход:
регистр детального графика (индивидуальные графики объектов)
заполненная ТЧ «ГрафикПоДоговору» (с процентами, амортизацией и т.д.)

4. Основные кейсы

Новый договор
Вид графика = Расчет
Движок строит график с нуля

В регистр записываются движения с видом «Поступление» (для новых объектов) или «Начисление»
Изменение условий
Вид графика = Расчет (или ИзменениеСтавкиНДС)

Движок пересчитывает график по новым условиям
В регистре появляется новый слой
Корректировка ОПС выравнивает обязательство и стоимость ППА

Расторжение
Вид графика = Расторжение
Формируются движения со знаком минус, списывающие остатки

Расчёт не выполняется
Замена стороны
Вид графика = ЗаменаСтороныДоговора

Остатки списываются со старого договора и начисляются на новый
Суммы переносятся без изменений

Сторно
Вид графика = Сторно
Копия движений указанного документа с противоположным знаком
Используется для отмены ошибочных документов в закрытых периодах

5. Трансляция в бухгалтерский и налоговый учёт
После записи движений в регистр сведений формируются проводки:

Хозрасчетный — для БУ
Налоговый — для НУ (с разделением НУ и ВР)

Запрос собирает остатки из регистра и превращает их в проводки по счетам:
01.03 / 02.03 / 76.07 и т.д.
Корректировка ОПС даёт проводку Дт 01.03 Кт 76.07.1 на сумму отклонения. 

Ежемесячное начисление арендной платы и процентов осуществляется с помощью внешней обработки "ФСБУ25: Формирование документов начиселния	000000730"
Она создает документы поступление товаров и услуг на основе дданных графиков.
Амортизация начисляется штатным документом "Амортизация ОС", но данные берутся из графиков.

6. Контроль целостности
При проведении (после записи) выполняется проверка сбалансированности:
Группировка по объектам ППА
Суммы обязательств, стоимости ППА, авансов должны быть = 0 в пределах допуска
НДС не проверяется (он учитывается отдельно)

При обнаружении разбаланса выводится сообщение:
сначала итоги по договору (только ненулевые показатели)
затем детализация по объектам
Проведение не блокируется, но ошибка фиксируется.

7. Отчётность

«Сверка остатков» — главный контрольный отчёт
Назначение
Единая точка проверки соответствия между:
плановыми данными графиков аренды
фактическими остатками по счетам бухгалтерского учёта
Что показывает
по каждому контрагенту, договору и объекту ППА:
обязательства (76.07.1)
проценты (76.07.5)
НДС (76.07.9)
стоимость ППА (01.03)
амортизацию (02.03)
авансы и их НДС
разбивку по документам, сформировавшим текущее состояние
отклонения между графиком и учётом

Как устроен
трёхуровневая группировка: контрагент → договор → объект ППА
внутри объекта — расшифровка по документам (слоям)
колонки разделены на два блока:
плановые показатели графика
фактические остатки по счетам

Зачем нужен
аудитору — чтобы видеть, что остатки подтверждены
бухгалтеру — чтобы находить расхождения
разработчику — чтобы понимать, как легли данные

Когда использовать
после каждого проведения
перед закрытием периода
при любых сомнениях в корректности учёта

Главное свойство
Если в отчёте нет отклонений — система сбалансирована.
Если есть — отчёт показывает, где искать причину.

«Анализ графика» — детальный расчётный отчёт

Назначение
Показать внутреннюю структуру графика аренды по каждому объекту ППА: как считались проценты, амортизация, обязательства, и как они распределены по периодам.

Что показывает
по каждому объекту ППА и периоду:
арендные платежи и НДС
приведенную стоимость
проценты
изменение обязательства
долгосрочную и краткосрочную части
стоимость ППА
амортизацию (начисленную и накопленную)
балансовую стоимость
итоги по договору в целом

Как устроен
два уровня группировки: объект ППА → период графика
каждая строка — один месяц (или иной период) аренды
все показатели рассчитаны по формулам дисконтирования и амортизации
видны «слои» изменений, если их несколько

Зачем нужен
бухгалтеру — чтобы понимать, из чего сложились цифры в балансе
аудитору — чтобы видеть корректность расчётов
разработчику — чтобы проверять работу движка

Главное свойство
Этот отчёт делает видимой математику ФСБУ 25:
как распределяется обязательство
как считаются проценты
как амортизация «бежит» по периодам
как меняется балансовая стоимость

Если «Сверка остатков» отвечает на вопрос «что есть», то «Анализ графика» — на вопрос «как получилось».

8. Что нельзя делать
Менять записи старых документов (только сторно)
Править регистры в обход документов

Аудиторы получают отчеты и больше не задают вопросов - полет 3 года.


// см. ОписаниеКонцепции в макете
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтотОбъект.Ссылка.Пустая() Тогда
		Ответственный = ПараметрыСеанса.ТекущийПользователь;
		ВидГрафика = Перечисления._ВидыГрафиковДолгосрочнойАренды.Расчет;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.ГрафикПоДоговору.Очистить();
	ЭтотОбъект.ДатаНачала = Дата(1,1,1);
	ЭтотОбъект.ДатаПрекращения = Дата(1,1,1); 
	ЭтотОбъект.ОПС = 0;
	ЭтотОбъект.СтоимостьППА = 0; 
	ЭтотОбъект.СПИ = 0;
	ЭтотОбъект.Примечание = "";
	ЭтотОбъект.АмППАНаНачало = 0;
	ЭтотОбъект.ПредОПСНаДатуРедакции = 0;
	ЭтотОбъект.Корректировка = 0;  
	ЭтотОбъект.ПредыдущаяРедакция = ОбъектКопирования.Ссылка;
	ЭтотОбъект.ОтражатьВБухгалтерскомУчете = Истина;
	ЭтотОбъект.ОтражатьВНалоговомУчете = Истина;
	ЭтотОбъект.ОтражатьКорректировкуСПИ = Истина;
	ЭтотОбъект.РетроКорректировки.Очистить();
	ЭтотОбъект.ОтразитьРетроКорректировку  = Ложь;
	ЭтотОбъект.Аванс = 0; 
	ЭтотОбъект.ВидГрафика = Перечисления._ВидыГрафиковДолгосрочнойАренды.Расчет;
	
	ЭтотОбъект.РасторжениеБУ.Очистить();
	ЭтотОбъект.РасторжениеНУ.Очистить();
	ЭтотОбъект.ФинРезультатРасторжениеБУ = 0;	
	
	ТекстПримечания = СтрШаблон("Создан на основе: %1", ОбъектКопирования.Ссылка);
	ЭтотОбъект.Примечание = ТекстПримечания; 
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	
	Для каждого строка Из СоставППА Цикл
		Строка.НакопленнаяАмортизация = 0;
		Строка.АмортизацияВПервомМесяце = Истина;
		Строка.СтоимостьППА = 0; 
		Строка.Аванс = 0;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Движения._УчетДолгосрочнойАренды.Записывать = Ложь;
	Движения._УчетДолгосрочнойАренды.Очистить();  
	Движения._УчетДолгосрочнойАренды.Записать();
	
	ДатаСторно = ?(НачалоМесяца(ДатаНачала) = ДатаНачала, ДатаНачала, ДобавитьМесяц(НачалоМесяца(ДатаНачала), 1));
	
	Если ВидГрафика = Перечисления._ВидыГрафиковДолгосрочнойАренды.ИзменениеСтавкиНДС Тогда
		
		ПересчитатьТолькоАренднуюПлатуВГрафиках2026();
		// Нет возврата - идем в общие процедуры
		
	ИначеЕсли ВидГрафика = Перечисления._ВидыГрафиковДолгосрочнойАренды.Расторжение Тогда
		
		РасторжениеДоговора(Отказ); 
		Движения._УчетДолгосрочнойАренды.Записать();
		РасторжениеБУиНУ();
		Возврат;
		
	ИначеЕсли ВидГрафика = Перечисления._ВидыГрафиковДолгосрочнойАренды.ЗаменаСтороныДоговора Тогда
		
		ПровестиЗаменуСтороныДоговора(Отказ); 
		Движения._УчетДолгосрочнойАренды.Записать();
		Возврат;
		
	ИначеЕсли ВидГрафика = Перечисления._ВидыГрафиковДолгосрочнойАренды.Расчет Тогда
		
		СторноОстатковНаДатуНачала(ДатаСторно);	
		НачислениеПервоначальнойСтоимостиППА();
		КапитализацияАванса();
		ПризнаниеОбязательстваПоАвансу();
		СоздатьЗаписиПоНовомуГрафику();
		ПогашениеАвансаИЕгоАмортизацияВСоставеППА();
		Движения._УчетДолгосрочнойАренды.Записать(); 
		КорректировкаОПС();
		// Нет возврата - идем в общие процедуры
		
	ИначеЕсли ВидГрафика = Перечисления._ВидыГрафиковДолгосрочнойАренды.Сторно Тогда
		
		СторноПредыдущегоДокумента();
		Движения._УчетДолгосрочнойАренды.Записать();
		СторноПроводокБУиНУ();
		Возврат;
		
	КонецЕсли;
	
	Движения._УчетДолгосрочнойАренды.Записать(); 
	ПроведениеПоБухгалтерскомуИНалоговомуУчету();	
	КорректировкаСПИ(); 

	РезультатПроверки = ПроверитьСбалансированностьГрафика(Договор, Дата);
	Если НЕ РезультатПроверки.БалансОк Тогда
	    Сообщить(РезультатПроверки.ТекстСообщения);
	КонецЕсли;	

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	КаскадноеУдалениеСтрокДетализации();
	
КонецПроцедуры  

Процедура КаскадноеУдалениеСтрокДетализации()
	
	//Каскадное удаление детализации в случае удаление строки графика
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ различные
	|	ГР.УИДСтрокиГрафика
	|ИЗ
	|	РегистрСведений._ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик КАК ГР
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ._ГрафикДоговораДолгосрочнойАренды.ГрафикПоДоговору КАК ДокГр
	|		ПО (ДокГр.Ссылка = ГР.График)
	|			И (ДокГр.УИДСтроки = ГР.УИДСтрокиГрафика)
	|ГДЕ
	|	ГР.График = &Ссылка
	|	И ДокГр.УИДСтроки ЕСТЬ NULL
	|";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка); 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборДвижений = РегистрыСведений._ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.СоздатьНаборЗаписей(); 
		НаборДвижений.Отбор.График.Установить(Ссылка); 
		НаборДвижений.Отбор.УИДСтрокиГрафика.Установить(Выборка.УИДСтрокиГрафика); 
		НаборДвижений.Прочитать();
		НаборДвижений.Очистить();
		НаборДвижений.Записать();
	КонецЦикла;  
	
	//Каскадное удаление детализации в случае удаление объектаППА из договора
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ различные
	|	ГР.ОбъектППА
	|ИЗ
	|	РегистрСведений._ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик КАК ГР
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ._ГрафикДоговораДолгосрочнойАренды.СоставППА КАК ДокСостав
	|		ПО (ДокСостав.Ссылка = ГР.График)
	|			И (ДокСостав.ОбъектППА = ГР.ОбъектППА)
	|ГДЕ
	|	ГР.График = &Ссылка
	|	И ДокСостав.ОбъектППА ЕСТЬ NULL
	|";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка); 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборДвижений = РегистрыСведений._ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.СоздатьНаборЗаписей(); 
		НаборДвижений.Отбор.График.Установить(Ссылка); 
		НаборДвижений.Отбор.ОбъектППА.Установить(Выборка.ОбъектППА); 
		НаборДвижений.Прочитать();
		НаборДвижений.Очистить();
		НаборДвижений.Записать();
	КонецЦикла; 	
	
КонецПроцедуры

Процедура РасторжениеДоговора(Отказ)
	
	Если ДатаРасторжения = Дата(1,1,1) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыОстатки.График,
	|	_УчетДолгосрочнойАрендыОстатки.Договор,
	|	_УчетДолгосрочнойАрендыОстатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыОстатки.ВидДвиженияГрафика,
	|	_УчетДолгосрочнойАрендыОстатки.Показатель,
	|	_УчетДолгосрочнойАрендыОстатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыОстатки.ПериодДисконтирования,
	|	_УчетДолгосрочнойАрендыОстатки.ВидОС,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.Обязательство) КАК ОбязательствоОстаток,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.СтоимостьППА) КАК СтоимостьППАОстаток,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.НДС) КАК НДСОстаток
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыОстатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыОстатки.Период < &Дата
	|	И _УчетДолгосрочнойАрендыОстатки.Договор = &Договор
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыОстатки.График,
	|	_УчетДолгосрочнойАрендыОстатки.Договор,
	|	_УчетДолгосрочнойАрендыОстатки.Показатель,
	|	_УчетДолгосрочнойАрендыОстатки.ВидДвиженияГрафика,
	|	_УчетДолгосрочнойАрендыОстатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыОстатки.ПериодДисконтирования,
	|	_УчетДолгосрочнойАрендыОстатки.ВидОС,
	|	_УчетДолгосрочнойАрендыОстатки.ПериодГрафика
	|имеющие СУММА(_УчетДолгосрочнойАрендыОстатки.Обязательство) <> 0 или СУММА(_УчетДолгосрочнойАрендыОстатки.СтоимостьППА) <> 0";
	Запрос.Параметры.Вставить("Дата", Дата); 
	Запрос.Параметры.Вставить("Договор", Договор); 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);//,,"ВидДвиженияГрафика"); 
		Движение.Обязательство = -ВыборкаДетальныеЗаписи.ОбязательствоОстаток;
		Движение.СтоимостьППА = -ВыборкаДетальныеЗаписи.СтоимостьППАОстаток; 
		Движение.НДС = -ВыборкаДетальныеЗаписи.НДСОстаток; 
		Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Выбытие;
		Движение.Регистратор = ЭтотОбъект.Ссылка;  
		Движение.ИДСтроки = новый УникальныйИдентификатор;
		Движение.Период = Дата; 
		Движение.ДатаСобытия = ДатаРасторжения;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПровестиЗаменуСтороныДоговора(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыОстатки.График,
	|	_УчетДолгосрочнойАрендыОстатки.Договор,
	|	_УчетДолгосрочнойАрендыОстатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыОстатки.ВидДвиженияГрафика,                                                                     
	|	_УчетДолгосрочнойАрендыОстатки.Показатель,
	|	_УчетДолгосрочнойАрендыОстатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыОстатки.ПериодДисконтирования,
	|	_УчетДолгосрочнойАрендыОстатки.ВидОС,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.Обязательство) КАК ОбязательствоОстаток,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.СтоимостьППА) КАК СтоимостьППАОстаток,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.НДС) КАК НДСОстаток
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыОстатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыОстатки.Период < &Дата
	|	И _УчетДолгосрочнойАрендыОстатки.Договор = &Договор
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыОстатки.График,
	|	_УчетДолгосрочнойАрендыОстатки.Договор,
	|	_УчетДолгосрочнойАрендыОстатки.Показатель,
	|	_УчетДолгосрочнойАрендыОстатки.ВидДвиженияГрафика,
	|	_УчетДолгосрочнойАрендыОстатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыОстатки.ПериодДисконтирования,
	|	_УчетДолгосрочнойАрендыОстатки.ВидОС,
	|	_УчетДолгосрочнойАрендыОстатки.ПериодГрафика 
	| имеющие СУММА(_УчетДолгосрочнойАрендыОстатки.Обязательство) <> 0 или СУММА(_УчетДолгосрочнойАрендыОстатки.СтоимостьППА) <> 0
	|";
	Запрос.Параметры.Вставить("Дата", Дата); 
	Запрос.Параметры.Вставить("Договор", ПредыдущийДоговор); 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		//Списание предыдущего договора
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи); 
		Движение.Обязательство = -ВыборкаДетальныеЗаписи.ОбязательствоОстаток;
		Движение.СтоимостьППА = -ВыборкаДетальныеЗаписи.СтоимостьППАОстаток;
		Движение.НДС = -ВыборкаДетальныеЗаписи.НДСОстаток; 
		Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Сторно;
		Движение.Регистратор = ЭтотОбъект.Ссылка;  
		Движение.ИДСтроки = новый УникальныйИдентификатор;
		Движение.Период = Дата;
		Движение.ДатаСобытия = ДатаЗаменыСтороны;
		
		//Начисление по новому договору
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи); 
		Движение.Обязательство = ВыборкаДетальныеЗаписи.ОбязательствоОстаток;
		Движение.СтоимостьППА = ВыборкаДетальныеЗаписи.СтоимостьППАОстаток; 
		Движение.НДС = ВыборкаДетальныеЗаписи.НДСОстаток; 
		Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Начисление;
		Движение.Договор = Договор;
		Движение.Регистратор = ЭтотОбъект.Ссылка;  
		Движение.ИДСтроки = новый УникальныйИдентификатор;
		Движение.Период = Дата; 
		Движение.ДатаСобытия = ДатаЗаменыСтороны;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СторноОстатковНаДатуНачала(ДатаСторно)
	
	ТЗСоставППА = СоставППА.Выгрузить();
	
	//Сторно стоимости ППА (< Даты начала)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	т.ОбъектППА,
	|	т.ПоказательБазыРаспределения,
	|	т.СтоимостьППА
	|ПОМЕСТИТЬ ВТ_СписокППА
	|ИЗ
	|	&ТЗСоставППА КАК т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Показатель,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) КАК Обязательство,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) КАК СтоимостьППА,
	|	ВЫБОР
	|		КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|				ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|				ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Выбытие
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокППА КАК ВТ_СписокППА
	|		ПО (ВТ_СписокППА.ОбъектППА = _УчетДолгосрочнойАрендыостатки.ОбъектППА)
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период < &ДатаДокумента
	|	И _УчетДолгосрочнойАрендыостатки.Договор = &Договор
	|	И _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.СтоимостьППА)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|				ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|				ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	_УчетДолгосрочнойАрендыостатки.Показатель,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА 
	| имеющие СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) <> 0 или  СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) <> 0
	|";
	
	Запрос.Параметры.Вставить("ДатаНачала", ДатаНачала); 
	Запрос.Параметры.Вставить("Договор", Договор); 
	Запрос.Параметры.Вставить("ДатаДокумента", Дата);
	Запрос.Параметры.Вставить("ТЗСоставППА", ТЗСоставППА);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.Период = Дата;
		Движение.ДатаСобытия = ДатаНачала;
		Движение.Договор = Выборка.Договор; 
		Движение.График = Ссылка;
		Движение.ОбъектППА = Выборка.ОбъектППА;
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		
		Если Выборка.Выбытие Тогда
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Выбытие;
		Иначе			
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Корректировка;
		КонецЕсли;
		
		Движение.Показатель = Выборка.Показатель;
		Движение.ПериодГрафика =  Дата(1,1,1);
		Движение.ПериодДисконтирования = ДатаНачала; 
		Движение.Обязательство = -Выборка.Обязательство;
		Движение.СтоимостьППА = -Выборка.СтоимостьППА;
	КонецЦикла;	
	
	//Сторно амортизации, процентов, арендных платежей - это будущая часть графика (т.к. >= датаНачала)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	т.ОбъектППА,
	|	т.ПоказательБазыРаспределения,
	|	т.СтоимостьППА
	|ПОМЕСТИТЬ ВТ_СписокППА
	|ИЗ
	|	&ТЗСоставППА КАК т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Показатель,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) КАК Обязательство,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.НДС) КАК НДС,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) КАК СтоимостьППА,
	|	ВЫБОР
	|		КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|				ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|				ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Выбытие
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокППА КАК ВТ_СписокППА
	|		ПО (ВТ_СписокППА.ОбъектППА = _УчетДолгосрочнойАрендыостатки.ОбъектППА)
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период < &ДатаДокумента
	|	И _УчетДолгосрочнойАрендыостатки.Договор = &Договор
	|	И _УчетДолгосрочнойАрендыостатки.ПериодГрафика >= &ДатаНачала
	|	И НЕ _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.ПриведеннаяСтоимость)
	|	И НЕ _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.СтоимостьППА)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	ВЫБОР
	|		КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|				ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|				ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.Показатель
	|
	|ИМЕЮЩИЕ
	|	(СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) <> 0
	|		ИЛИ СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) <> 0)";
	
	Запрос.Параметры.Вставить("ДатаНачала", ДатаСторно); 
	Запрос.Параметры.Вставить("Договор", Договор); 
	Запрос.Параметры.Вставить("ДатаДокумента", Дата); 
	Запрос.Параметры.Вставить("ТЗСоставППА", ТЗСоставППА);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.График = Ссылка;
		Движение.Период = Дата;
		Движение.Договор = Выборка.Договор;
		Движение.ОбъектППА = Выборка.ОбъектППА; 
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		
		Если Выборка.Выбытие тогда
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Выбытие;
		Иначе
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Сторно;
		КонецЕсли;
		
		Движение.Показатель = Выборка.Показатель;
		Движение.ПериодГрафика = Выборка.ПериодГрафика;
		Движение.ПериодДисконтирования = Выборка.ПериодДисконтирования;
		Движение.Обязательство = -Выборка.Обязательство;
		Движение.НДС = -Выборка.НДС; 
		Движение.СтоимостьППА = -Выборка.СтоимостьППА;
		Движение.ДатаСобытия = ДатаНачала;
		
	КонецЦикла;  
	
	//СторноПриведеннойСтоимости
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	т.ОбъектППА,
	|	т.ПоказательБазыРаспределения,
	|	т.СтоимостьППА
	|ПОМЕСТИТЬ ВТ_СписокППА
	|ИЗ
	|	&ТЗСоставППА КАК т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Показатель,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) КАК Обязательство,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) КАК СтоимостьППА,
	|	ВЫБОР
	|		КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|				ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|				ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Выбытие
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокППА КАК ВТ_СписокППА
	|		ПО (ВТ_СписокППА.ОбъектППА = _УчетДолгосрочнойАрендыостатки.ОбъектППА)
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период < &ДатаДокумента
	|	И _УчетДолгосрочнойАрендыостатки.Договор = &Договор
	|	И _УчетДолгосрочнойАрендыостатки.ПериодДисконтирования >= &ДатаНачала
	|	И _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.ПриведеннаяСтоимость)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	ВЫБОР
	|		КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|				ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|				ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	_УчетДолгосрочнойАрендыостатки.Показатель
	|
	|ИМЕЮЩИЕ
	|	СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) <> 0";
	
	Запрос.Параметры.Вставить("ТЗСоставППА", ТЗСоставППА);
	Запрос.Параметры.Вставить("ДатаНачала", ДатаСторно); 
	Запрос.Параметры.Вставить("Договор", Договор); 
	Запрос.Параметры.Вставить("ДатаДокумента", Дата); 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.График = Ссылка;
		Движение.Период = Дата;
		Движение.Договор = Выборка.Договор;
		Движение.ОбъектППА = Выборка.ОбъектППА; 
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		Если Выборка.Выбытие Тогда
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Выбытие;
		Иначе			
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Корректировка;
		КонецЕсли;
		Движение.Показатель = Выборка.Показатель;
		Движение.ПериодГрафика = Дата(1,1,1);
		Движение.ПериодДисконтирования = Выборка.ПериодДисконтирования;
		Движение.Обязательство = -Выборка.Обязательство;  
		Движение.ДатаСобытия = ДатаНачала;
		
	КонецЦикла;
	
	СписатьОстаткиПриВыбытииОбъектаППА(ТЗСоставППА, ДатаСторно, Дата, Договор);
	
КонецПроцедуры

Процедура КорректировкаОПС()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.График,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.ВидДвиженияГрафика,
	|	_УчетДолгосрочнойАрендыостатки.Показатель,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) КАК ОбязательствоОстаток,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) КАК СтоимостьППАОстаток,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА - _УчетДолгосрочнойАрендыостатки.Обязательство) КАК Отклонение
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.График = &График
	|	И _УчетДолгосрочнойАрендыостатки.ВидДвиженияГрафика = ЗНАЧЕНИЕ(Перечисление._ВидыДвиженийГрафиковДолгосрочнойАренды.Корректировка)
	|	И (_УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.ПриведеннаяСтоимость)
	|			ИЛИ _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.СтоимостьППА))
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.ВидДвиженияГрафика,
	|	_УчетДолгосрочнойАрендыостатки.График,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	_УчетДолгосрочнойАрендыостатки.Показатель,
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Договор
	|
	|ИМЕЮЩИЕ
	|	(СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) <> 0
	|		ИЛИ СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) <> 0)";
	
	Запрос.Параметры.Вставить("График", Ссылка);    
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.Период = Дата;
		Движение.Договор = Выборка.Договор;
		Движение.График = Ссылка;
		Движение.ОбъектППА = Выборка.ОбъектППА;
		Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Корректировка;
		Движение.Показатель = Перечисления._ПоказателиГрафиковДолгосрочнойАренды.ПриведеннаяСтоимость;
		Движение.ПериодГрафика = Дата(1,1,1);//ДатаНачала;//Дата(1,1,2);//ДатаНачала;
		Движение.ПериодДисконтирования = ДатаНачала;
		Движение.Обязательство = Выборка.Отклонение;
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		Движение.ДатаСобытия = ДатаНачала;
	КонецЦикла;	
	
КонецПроцедуры

Процедура СоздатьЗаписиПоНовомуГрафику()
	
	//Новые строки графика
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.ОбъектППА,
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка.Договор
	|ПОМЕСТИТЬ ВТ_СписокППА
	|ИЗ
	|	Документ._ГрафикДоговораДолгосрочнойАренды.СоставППА КАК ДокументГрафикДоговораДолгосрочнойАрендыСоставППА
	|ГДЕ
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	МАКСИМУМ(_УчетДолгосрочнойАрендыостатки.График) КАК График,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) КАК ПервоначальнаяСтоимость
	|ПОМЕСТИТЬ ВТ_СтарыеОбъекты
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период < &ДатаИтогов
	|	И _УчетДолгосрочнойАрендыостатки.ОбъектППА В
	|			(ВЫБРАТЬ
	|				ВТ_СписокППА.ОбъектППА
	|			ИЗ
	|				ВТ_СписокППА)
	|	И _УчетДолгосрочнойАрендыостатки.Договор В
	|			(ВЫБРАТЬ
	|				ВТ_СписокППА.Договор
	|			ИЗ
	|				ВТ_СписокППА)
	|	И _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.СтоимостьППА)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Договор
	|
	|ИМЕЮЩИЕ
	|	ОКР(СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА), 2) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.УИДСтрокиГрафика,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.АренднаяПлата,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.НДС,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ПриведеннаяСтоимость,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ПроцентныеРасходы,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбязательствоНаНачало,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ИзменениеОбязательства,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбязательствоНаКонец,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ДолгосрочнаяЧасть,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.КраткосрочнаяЧасть,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.СтоимостьППА,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.Амортизация,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.АмортизацияНакопленная,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.БалансоваяСтоимость,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.АктивностьСтроки,
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.ДатаС,
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.ДатаПо КАК ДатаПо,
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Дней,
	|	ВЫБОР
	|		КОГДА ВТ_СтарыеОбъекты.ОбъектППА ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНовыйОбъект
	|ИЗ
	|	Документ._ГрафикДоговораДолгосрочнойАренды.ГрафикПоДоговору КАК ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений._ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик КАК _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ._ГрафикДоговораДолгосрочнойАренды.СоставППА КАК Состав
	|			ПО (Состав.Ссылка = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График)
	|				И (Состав.ОбъектППА = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА)
	|		ПО ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Ссылка = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График
	|			И ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.УИДСтроки = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.УИДСтрокиГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтарыеОбъекты КАК ВТ_СтарыеОбъекты
	|		ПО (ВТ_СтарыеОбъекты.ОбъектППА = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА)
	|ГДЕ
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Ссылка = &Ссылка"; 
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка); 	
	Запрос.Параметры.Вставить("ДатаИтогов", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		//Приведенная стоимость Начисление
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.Период = Дата;
		Движение.Договор = Договор;
		Движение.График = Ссылка;
		Движение.ОбъектППА = Выборка.ОбъектППА;
		Если Выборка.ЭтоНовыйОбъект = Ложь Тогда
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Корректировка;
		Иначе	
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Поступление;
		КонецЕсли;
		Движение.Показатель = перечисления._ПоказателиГрафиковДолгосрочнойАренды.ПриведеннаяСтоимость;
		Движение.ПериодГрафика =  Дата(1,1,1);
		Движение.ПериодДисконтирования = выборка.ДатаПо;
		Движение.Обязательство = Выборка.ПриведеннаяСтоимость;
		Движение.СтоимостьППА = 0;
		Движение.ИДСтроки = Новый УникальныйИдентификатор; 
		Движение.ДатаСобытия = ДатаНачала;
		
		//АрендныеПлатежи Начисление
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.Период = Дата;
		Движение.Договор = Договор;
		Движение.График = Ссылка;
		Движение.ОбъектППА = Выборка.ОбъектППА;
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		Если Выборка.ЭтоНовыйОбъект = Ложь Тогда
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Начисление;
		Иначе	
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Поступление;
		КонецЕсли;
		
		Движение.Показатель = перечисления._ПоказателиГрафиковДолгосрочнойАренды.АрендныеПлатежи;
		Движение.ПериодГрафика = Выборка.ДатаПо;
		Движение.ПериодДисконтирования = Дата(1,1,1);
		Движение.Обязательство = -Выборка.АренднаяПлата;  
		Движение.НДС = -Выборка.НДС;  
		Движение.СтоимостьППА = 0; 
		Движение.ДатаСобытия = ДатаНачала;
		
		//Проценты Начисление
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.Период = Дата;
		Движение.Договор = Договор;
		Движение.График = Ссылка;
		Движение.ОбъектППА = Выборка.ОбъектППА;  
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		Если Выборка.ЭтоНовыйОбъект = Ложь Тогда
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Начисление;
		Иначе	
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Поступление;
		КонецЕсли;
		Движение.Показатель = перечисления._ПоказателиГрафиковДолгосрочнойАренды.Проценты;
		Движение.ПериодГрафика = Выборка.ДатаПо;
		Движение.ПериодДисконтирования = Дата(1,1,1);
		Движение.Обязательство = Выборка.ПроцентныеРасходы;
		Движение.СтоимостьППА = 0;  
		Движение.ДатаСобытия = ДатаНачала;
		
		//АмортизацияППА Начисление
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.Период = Дата;
		Движение.Договор = Договор; 
		Движение.График = Ссылка;
		Движение.ОбъектППА = Выборка.ОбъектППА;
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		Если Выборка.ЭтоНовыйОбъект = Ложь Тогда
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Начисление;
		Иначе	
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Поступление;
		КонецЕсли;
		Движение.Показатель = перечисления._ПоказателиГрафиковДолгосрочнойАренды.АмортизацияППА;
		Движение.ПериодГрафика = Выборка.ДатаПо;
		Движение.ПериодДисконтирования = Дата(1,1,1);
		Движение.Обязательство = 0;
		Движение.СтоимостьППА = -Выборка.Амортизация; 
		Движение.ДатаСобытия = ДатаНачала;
	КонецЦикла;
	
КонецПроцедуры

Процедура НачислениеПервоначальнойСтоимостиППА()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.ОбъектППА,
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка.Договор
	|ПОМЕСТИТЬ ВТ_СписокППА
	|ИЗ
	|	Документ._ГрафикДоговораДолгосрочнойАренды.СоставППА КАК ДокументГрафикДоговораДолгосрочнойАрендыСоставППА
	|ГДЕ
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	МАКСИМУМ(_УчетДолгосрочнойАрендыостатки.График) КАК График,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) КАК ПервоначальнаяСтоимость
	|ПОМЕСТИТЬ ВТ_СтарыеОбъекты
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период < &ДатаИтогов
	|	И _УчетДолгосрочнойАрендыостатки.ОбъектППА В
	|			(ВЫБРАТЬ
	|				ВТ_СписокППА.ОбъектППА
	|			ИЗ
	|				ВТ_СписокППА)
	|	И _УчетДолгосрочнойАрендыостатки.Договор В
	|			(ВЫБРАТЬ
	|				ВТ_СписокППА.Договор
	|			ИЗ
	|				ВТ_СписокППА)
	|	И _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.СтоимостьППА)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Договор
	|
	|ИМЕЮЩИЕ
	|	ОКР(СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА), 2) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.СтоимостьППА,
	|	ВЫБОР
	|		КОГДА ВТ_СтарыеОбъекты.ОбъектППА ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНовыйОбъект
	|ИЗ
	|	Документ._ГрафикДоговораДолгосрочнойАренды.ГрафикПоДоговору КАК ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений._ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик КАК _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ._ГрафикДоговораДолгосрочнойАренды.СоставППА КАК Состав
	|			ПО (Состав.Ссылка = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График)
	|				И (Состав.ОбъектППА = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА)
	|		ПО ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Ссылка = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График
	|			И ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.УИДСтроки = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.УИДСтрокиГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтарыеОбъекты КАК ВТ_СтарыеОбъекты
	|		ПО (ВТ_СтарыеОбъекты.ОбъектППА = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА)
	|ГДЕ
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.СтоимостьППА,
	|	ВЫБОР
	|		КОГДА ВТ_СтарыеОбъекты.ОбъектППА ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка); 
	
	Запрос.Параметры.Вставить("ДатаИтогов", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл    
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		Движение.Период = Дата;
		Движение.Договор = Договор;  
		Движение.График = Ссылка;
		Движение.ОбъектППА = Выборка.ОбъектППА; 
		Если Выборка.ЭтоНовыйОбъект  Тогда
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Поступление;
		Иначе	
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Корректировка;
		КонецЕсли;
		Движение.Показатель = Перечисления._ПоказателиГрафиковДолгосрочнойАренды.СтоимостьППА;
		Движение.ПериодГрафика = Дата(1,1,1);
		Движение.ПериодДисконтирования = ДатаНачала;
		Движение.СтоимостьППА =  Выборка.СтоимостьППА; 
		Движение.ДатаСобытия = ДатаНачала;
	КонецЦикла;
	
КонецПроцедуры  

Процедура КапитализацияАванса()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.ОбъектППА,
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка.Договор,
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Аванс
	|ПОМЕСТИТЬ ВТ_СписокППА
	|ИЗ
	|	Документ._ГрафикДоговораДолгосрочнойАренды.СоставППА КАК ДокументГрафикДоговораДолгосрочнойАрендыСоставППА
	|ГДЕ
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	МАКСИМУМ(_УчетДолгосрочнойАрендыостатки.График) КАК График
	|ПОМЕСТИТЬ ВТ_СтарыеОбъекты
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период < &ДатаИтогов
	|	И _УчетДолгосрочнойАрендыостатки.ОбъектППА В
	|			(ВЫБРАТЬ
	|				ВТ_СписокППА.ОбъектППА
	|			ИЗ
	|				ВТ_СписокППА)
	|	И _УчетДолгосрочнойАрендыостатки.Договор В
	|			(ВЫБРАТЬ
	|				ВТ_СписокППА.Договор
	|			ИЗ
	|				ВТ_СписокППА)
	|	И _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.СтоимостьППА)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Договор
	|
	|ИМЕЮЩИЕ
	|	ОКР(СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА), 2) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СписокППА.ОбъектППА,
	|	ВТ_СписокППА.Аванс,
	|	ВЫБОР
	|		КОГДА ВТ_СтарыеОбъекты.ОбъектППА ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНовыйОбъект
	|ИЗ
	|	ВТ_СписокППА КАК ВТ_СписокППА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтарыеОбъекты КАК ВТ_СтарыеОбъекты
	|		ПО (ВТ_СтарыеОбъекты.ОбъектППА = ВТ_СписокППА.ОбъектППА)
	|			И (ВТ_СтарыеОбъекты.Договор = ВТ_СписокППА.Договор)";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка); 
	
	Запрос.Параметры.Вставить("ДатаИтогов", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл    
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		Движение.Период = Дата;
		Движение.Договор = Договор;  
		Движение.График = Ссылка;
		Движение.ОбъектППА = Выборка.ОбъектППА; 
		Если Выборка.ЭтоНовыйОбъект  Тогда
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Поступление;
		Иначе	
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Корректировка;
		КонецЕсли;
		Движение.Показатель = Перечисления._ПоказателиГрафиковДолгосрочнойАренды.КапитализацияАванса;
		Движение.ПериодГрафика = Дата(1,1,1);
		Движение.ПериодДисконтирования = ДатаНачала;
		Движение.АвансАктив =  Выборка.Аванс; 
		Движение.ДатаСобытия = ДатаНачала;
	КонецЦикла;
	
КонецПроцедуры 

Процедура ПризнаниеОбязательстваПоАвансу()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.ОбъектППА,
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка.Договор,
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Аванс
	|ПОМЕСТИТЬ ВТ_СписокППА
	|ИЗ
	|	Документ._ГрафикДоговораДолгосрочнойАренды.СоставППА КАК ДокументГрафикДоговораДолгосрочнойАрендыСоставППА
	|ГДЕ
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	МАКСИМУМ(_УчетДолгосрочнойАрендыостатки.График) КАК График
	|ПОМЕСТИТЬ ВТ_СтарыеОбъекты
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период < &ДатаИтогов
	|	И _УчетДолгосрочнойАрендыостатки.ОбъектППА В
	|			(ВЫБРАТЬ
	|				ВТ_СписокППА.ОбъектППА
	|			ИЗ
	|				ВТ_СписокППА)
	|	И _УчетДолгосрочнойАрендыостатки.Договор В
	|			(ВЫБРАТЬ
	|				ВТ_СписокППА.Договор
	|			ИЗ
	|				ВТ_СписокППА)
	|	И _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.СтоимостьППА)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Договор
	|
	|ИМЕЮЩИЕ
	|	ОКР(СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА), 2) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА,
	|	ВЫБОР
	|		КОГДА ВТ_СтарыеОбъекты.ОбъектППА ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНовыйОбъект,
	|	СУММА(_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ЗачетАванса) КАК Аванс,
	|	СУММА(_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.НДСсАванса) КАК НДСсАванса
	|ИЗ
	|	Документ._ГрафикДоговораДолгосрочнойАренды.ГрафикПоДоговору КАК ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений._ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик КАК _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ._ГрафикДоговораДолгосрочнойАренды.СоставППА КАК Состав
	|			ПО (Состав.Ссылка = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График)
	|				И (Состав.ОбъектППА = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА)
	|		ПО ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Ссылка = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График
	|			И ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.УИДСтроки = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.УИДСтрокиГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтарыеОбъекты КАК ВТ_СтарыеОбъекты
	|		ПО (ВТ_СтарыеОбъекты.ОбъектППА = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА)
	|ГДЕ
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА,
	|	ВЫБОР
	|		КОГДА ВТ_СтарыеОбъекты.ОбъектППА ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка); 
	
	Запрос.Параметры.Вставить("ДатаИтогов", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл 
		
		//АвансОбязательство Начисление
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.Период = Дата;
		Движение.Договор = Договор;
		Движение.График = Ссылка;
		Движение.ОбъектППА = Выборка.ОбъектППА;
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		Если Выборка.ЭтоНовыйОбъект = Ложь Тогда
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Начисление;
		Иначе	
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Поступление;
		КонецЕсли;
		Движение.Показатель = перечисления._ПоказателиГрафиковДолгосрочнойАренды.ВыдачаАванса;
		Движение.ПериодГрафика = ДатаНачала;
		Движение.ПериодДисконтирования = Дата(1,1,1);
		Движение.АвансОбязательство = Выборка.Аванс;  
		Движение.АвансНДС = Выборка.НДСсАванса;  
		Движение.ДатаСобытия = ДатаНачала;
		
	КонецЦикла;
	
КонецПроцедуры 

Процедура ПогашениеАвансаИЕгоАмортизацияВСоставеППА()
	
	//Новые строки графика
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.ОбъектППА,
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка.Договор
	|ПОМЕСТИТЬ ВТ_СписокППА
	|ИЗ
	|	Документ._ГрафикДоговораДолгосрочнойАренды.СоставППА КАК ДокументГрафикДоговораДолгосрочнойАрендыСоставППА
	|ГДЕ
	|	ДокументГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	МАКСИМУМ(_УчетДолгосрочнойАрендыостатки.График) КАК График,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) КАК ПервоначальнаяСтоимость
	|ПОМЕСТИТЬ ВТ_СтарыеОбъекты
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период < &ДатаИтогов
	|	И _УчетДолгосрочнойАрендыостатки.ОбъектППА В
	|			(ВЫБРАТЬ
	|				ВТ_СписокППА.ОбъектППА
	|			ИЗ
	|				ВТ_СписокППА)
	|	И _УчетДолгосрочнойАрендыостатки.Договор В
	|			(ВЫБРАТЬ
	|				ВТ_СписокППА.Договор
	|			ИЗ
	|				ВТ_СписокППА)
	|	И _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.СтоимостьППА)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Договор
	|
	|ИМЕЮЩИЕ
	|	ОКР(СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА), 2) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.УИДСтрокиГрафика,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.АктивностьСтроки,
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.ДатаС,
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.ДатаПо КАК ДатаПо,
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Дней,
	|	ВЫБОР
	|		КОГДА ВТ_СтарыеОбъекты.ОбъектППА ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНовыйОбъект,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ЗачетАванса,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.НДСсАванса,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.АмортизацияАванса
	|ИЗ
	|	Документ._ГрафикДоговораДолгосрочнойАренды.ГрафикПоДоговору КАК ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений._ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик КАК _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ._ГрафикДоговораДолгосрочнойАренды.СоставППА КАК Состав
	|			ПО (Состав.Ссылка = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График)
	|				И (Состав.ОбъектППА = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА)
	|		ПО ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Ссылка = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График
	|			И ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.УИДСтроки = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.УИДСтрокиГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтарыеОбъекты КАК ВТ_СтарыеОбъекты
	|		ПО (ВТ_СтарыеОбъекты.ОбъектППА = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА)
	|ГДЕ
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Ссылка = &Ссылка
	|	И (_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ЗачетАванса <> 0
	|	ИЛИ _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.АмортизацияАванса <> 0)"; 
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка); 	
	Запрос.Параметры.Вставить("ДатаИтогов", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ЗачетАванса <> 0 Тогда
			//АрендныеПлатежи Начисление
			Движение = Движения._УчетДолгосрочнойАренды.Добавить();
			Движение.Период = Дата;
			Движение.Договор = Договор;
			Движение.График = Ссылка;
			Движение.ОбъектППА = Выборка.ОбъектППА;
			Движение.ИДСтроки = Новый УникальныйИдентификатор;
			Если Выборка.ЭтоНовыйОбъект = Ложь Тогда
				Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Начисление;
			Иначе	
				Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Поступление;
			КонецЕсли;
			
			Движение.Показатель = перечисления._ПоказателиГрафиковДолгосрочнойАренды.ЗачетАванса;
			Движение.ПериодГрафика = Выборка.ДатаПо;
			Движение.ПериодДисконтирования = Дата(1,1,1);
			Движение.АвансОбязательство = -Выборка.ЗачетАванса;  
			Движение.АвансНДС = -Выборка.НДСсАванса;  
			Движение.ДатаСобытия = ДатаНачала;
		КонецЕсли;                            
		
		//АмортизацияППА Начисление
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.Период = Дата;
		Движение.Договор = Договор; 
		Движение.График = Ссылка;
		Движение.ОбъектППА = Выборка.ОбъектППА;
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		Если Выборка.ЭтоНовыйОбъект = Ложь Тогда
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Начисление;
		Иначе	
			Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Поступление;
		КонецЕсли;
		Движение.Показатель = перечисления._ПоказателиГрафиковДолгосрочнойАренды.АмортизацияАванса;
		Движение.ПериодГрафика = Выборка.ДатаПо;
		Движение.ПериодДисконтирования = Дата(1,1,1);
		
		Движение.АвансАктив = -Выборка.АмортизацияАванса; 
		Движение.ДатаСобытия = ДатаНачала;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроведениеПоБухгалтерскомуИНалоговомуУчету()
	
	Движения.Хозрасчетный.Записывать = Ложь;
	Движения.Хозрасчетный.Очистить();
	Движения.Налоговый.Записывать = Ложь;
	Движения.Налоговый.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыОстатки.Договор,
	|	_УчетДолгосрочнойАрендыОстатки.ОбъектППА,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.Обязательство) КАК ОбязательствоОстаток,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.СтоимостьППА) КАК СтоимостьППАОстаток,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.Обязательство - _УчетДолгосрочнойАрендыОстатки.СтоимостьППА) КАК Отклонение
	|ПОМЕСТИТЬ ВТ_КорректировкиПриведеннойСтоимости_И_ППА
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыОстатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыОстатки.Период <= &ДатаДокумента
	|	И _УчетДолгосрочнойАрендыОстатки.График = &График
	|	И _УчетДолгосрочнойАрендыОстатки.ВидДвиженияГрафика = ЗНАЧЕНИЕ(Перечисление._ВидыДвиженийГрафиковДолгосрочнойАренды.Корректировка)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыОстатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыОстатки.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыОстатки.Договор,
	|	_УчетДолгосрочнойАрендыОстатки.ОбъектППА,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.Обязательство) КАК АрендныеПлатежи,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.НДС) КАК НДС,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.Обязательство + _УчетДолгосрочнойАрендыОстатки.НДС) КАК АрендныеПлатежи_С_НДС
	|ПОМЕСТИТЬ ВТ_КорректировкиАрендныхПлатежей
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыОстатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыОстатки.Период <= &ДатаДокумента
	|	И _УчетДолгосрочнойАрендыОстатки.График = &График
	|	И (_УчетДолгосрочнойАрендыОстатки.ВидДвиженияГрафика = ЗНАЧЕНИЕ(Перечисление._ВидыДвиженийГрафиковДолгосрочнойАренды.Начисление)
	|			ИЛИ _УчетДолгосрочнойАрендыОстатки.ВидДвиженияГрафика = ЗНАЧЕНИЕ(Перечисление._ВидыДвиженийГрафиковДолгосрочнойАренды.Сторно))
	|	И _УчетДолгосрочнойАрендыОстатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.АрендныеПлатежи)
	|	И НЕ _УчетДолгосрочнойАрендыОстатки.График.ПредыдущаяРедакция = ЗНАЧЕНИЕ(Документ._ГрафикДоговораДолгосрочнойАренды.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыОстатки.Договор,
	|	_УчетДолгосрочнойАрендыОстатки.ОбъектППА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыОстатки.Договор,
	|	_УчетДолгосрочнойАрендыОстатки.ОбъектППА,
	|	СУММА(_УчетДолгосрочнойАрендыОстатки.Обязательство) КАК Проценты
	|ПОМЕСТИТЬ ВТ_КорректировкиПроцентов
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыОстатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыОстатки.Период <= &ДатаДокумента
	|	И _УчетДолгосрочнойАрендыОстатки.График = &График
	|	И (_УчетДолгосрочнойАрендыОстатки.ВидДвиженияГрафика = ЗНАЧЕНИЕ(Перечисление._ВидыДвиженийГрафиковДолгосрочнойАренды.Начисление)
	|			ИЛИ _УчетДолгосрочнойАрендыОстатки.ВидДвиженияГрафика = ЗНАЧЕНИЕ(Перечисление._ВидыДвиженийГрафиковДолгосрочнойАренды.Сторно))
	|	И _УчетДолгосрочнойАрендыОстатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.Проценты)
	|	И НЕ _УчетДолгосрочнойАрендыОстатки.График.ПредыдущаяРедакция = ЗНАЧЕНИЕ(Документ._ГрафикДоговораДолгосрочнойАренды.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыОстатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыОстатки.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КорректировкиПриведеннойСтоимости_И_ППА.Договор,
	|	ВТ_КорректировкиПриведеннойСтоимости_И_ППА.ОбъектППА,
	|	ВТ_КорректировкиПриведеннойСтоимости_И_ППА.ОбязательствоОстаток,
	|	ВТ_КорректировкиПриведеннойСтоимости_И_ППА.СтоимостьППАОстаток,
	|	ВТ_КорректировкиПриведеннойСтоимости_И_ППА.Отклонение,
	|	0 КАК АрендныеПлатежи,
	|	0 КАК НДС,
	|	0 КАК АрендныеПлатежи_С_НДС,
	|	0 КАК Проценты
	|ПОМЕСТИТЬ ВТ_Общая
	|ИЗ
	|	ВТ_КорректировкиПриведеннойСтоимости_И_ППА КАК ВТ_КорректировкиПриведеннойСтоимости_И_ППА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_КорректировкиАрендныхПлатежей.Договор,
	|	ВТ_КорректировкиАрендныхПлатежей.ОбъектППА,
	|	0,
	|	0,
	|	0,
	|	ВТ_КорректировкиАрендныхПлатежей.АрендныеПлатежи,
	|	ВТ_КорректировкиАрендныхПлатежей.НДС,
	|	ВТ_КорректировкиАрендныхПлатежей.АрендныеПлатежи_С_НДС,
	|	0
	|ИЗ
	|	ВТ_КорректировкиАрендныхПлатежей КАК ВТ_КорректировкиАрендныхПлатежей
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_КорректировкиПроцентов.Договор,
	|	ВТ_КорректировкиПроцентов.ОбъектППА,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ВТ_КорректировкиПроцентов.Проценты
	|ИЗ
	|	ВТ_КорректировкиПроцентов КАК ВТ_КорректировкиПроцентов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Общая.Договор,
	|	ВТ_Общая.ОбъектППА,
	|	ВТ_Общая.ОбязательствоОстаток,
	|	ВТ_Общая.СтоимостьППАОстаток,
	|	ВТ_Общая.Отклонение,
	|	-ВТ_Общая.АрендныеПлатежи КАК АрендныеПлатежи,
	|	-ВТ_Общая.АрендныеПлатежи_С_НДС КАК АрендныеПлатежи_С_НДС,
	|	-ВТ_Общая.НДС КАК НДС,
	|	ВТ_Общая.Проценты,
	|	ВТ_Общая.ОбязательствоОстаток - ВТ_Общая.НДС + ВТ_Общая.Проценты КАК Счет76071,
	|	ВТ_Общая.ОбязательствоОстаток + ВТ_Общая.Проценты КАК Счет01К
	|ПОМЕСТИТЬ ВТ_Общая1
	|ИЗ
	|	ВТ_Общая КАК ВТ_Общая
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Общая1.Договор,
	|	ВТ_Общая1.ОбъектППА,
	|	СУММА(ВТ_Общая1.ОбязательствоОстаток) КАК ОбязательствоОборот,
	|	СУММА(ВТ_Общая1.СтоимостьППАОстаток) КАК СтоимостьППАОборот,
	|	СУММА(ВТ_Общая1.Отклонение) КАК Отклонение,
	|	СУММА(ВТ_Общая1.АрендныеПлатежи) КАК АрендныеПлатежи,
	|	СУММА(ВТ_Общая1.АрендныеПлатежи_С_НДС) КАК АрендныеПлатежи_С_НДС,
	|	СУММА(ВТ_Общая1.НДС) КАК НДС,
	|	СУММА(ВТ_Общая1.Проценты) КАК Проценты,
	|	СУММА(ВТ_Общая1.Счет76071) КАК Счет76071,
	|	СУММА(ВТ_Общая1.Счет01К) КАК Счет01К
	|ПОМЕСТИТЬ ВТ_Общая2
	|ИЗ
	|	ВТ_Общая1 КАК ВТ_Общая1
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Общая1.Договор,
	|	ВТ_Общая1.ОбъектППА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Общая2.Договор,
	|	ВТ_Общая2.ОбъектППА,
	|	ВТ_Общая2.ОбязательствоОборот,
	|	ВТ_Общая2.СтоимостьППАОборот,
	|	ВТ_Общая2.Отклонение,
	|	ВТ_Общая2.АрендныеПлатежи,
	|	ВТ_Общая2.АрендныеПлатежи_С_НДС,
	|	ВТ_Общая2.НДС,
	|	ВТ_Общая2.Проценты,
	|	ВТ_Общая2.Счет76071,
	|	ВТ_Общая2.Счет01К,
	|	ВЫБОР
	|		КОГДА ВТ_Общая2.Отклонение МЕЖДУ -1 И 1
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьПогрешность
	|ПОМЕСТИТЬ ВТ_Общая3
	|ИЗ
	|	ВТ_Общая2 КАК ВТ_Общая2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Общая3.Договор КАК ДоговорКонтрагента,
	|	ВТ_Общая3.Договор.Владелец КАК Контрагент,
	|	ВТ_Общая3.ОбъектППА КАК ОсновноеСредство,
	|	ВТ_Общая3.СтоимостьППАОборот,
	|	ВТ_Общая3.ОбязательствоОборот,
	|	ВТ_Общая3.Отклонение,
	|	ВТ_Общая3.АрендныеПлатежи,
	|	ВТ_Общая3.АрендныеПлатежи_С_НДС,
	|	ВТ_Общая3.НДС,
	|	ВТ_Общая3.Проценты,
	|	ВТ_Общая3.Счет76071,
	|	ВТ_Общая3.Счет01К,
	|	ВТ_Общая3.ЕстьПогрешность
	|ИЗ
	|	ВТ_Общая3 КАК ВТ_Общая3
	|";
	
	Запрос.Параметры.Вставить("ДатаДокумента", Дата+1); 
	Запрос.Параметры.Вставить("График", Ссылка); 
	
	Организация = Справочники.Организации.НайтиПоКоду("000000001");
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать(); 
	
	Пока Выборка.Следующий() Цикл 
		
		Если Окр(Выборка.ОбязательствоОборот,2) = 0 и Окр(Выборка.НДС,2) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОтражатьВБухгалтерскомУчете Тогда
			
			//01 76.07.1
			Если Окр(Выборка.СтоимостьППАОборот,2) <> 0 тогда
				ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01.03");
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.1");
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Корректировка ППА и ОПС";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = Выборка.СтоимостьППАОборот;
				
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Выборка.ОсновноеСредство); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Выборка.Контрагент);
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Выборка.ДоговорКонтрагента);   
			КонецЕсли;
			
			//76.07.5 76.07.1
			Если окр(Выборка.Проценты,2) <> 0 тогда
				ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.5");
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.1");
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Корректировка ППА и ОПС";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = Выборка.Проценты;
				
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Выборка.Контрагент); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, Выборка.ДоговорКонтрагента); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Выборка.Контрагент);
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Выборка.ДоговорКонтрагента); 
			КонецЕсли;
			
			//76.07.9 76.07.1
			Если Окр(Выборка.НДС) <> 0 тогда
				ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.9");
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.1");
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Корректировка ППА и ОПС";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = Выборка.НДС;
				
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Выборка.Контрагент); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, Выборка.ДоговорКонтрагента); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Выборка.Контрагент);
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Выборка.ДоговорКонтрагента);   
			КонецЕсли;	
		КонецЕсли;	
		Если ОтражатьВНалоговомУчете Тогда
			
			//ВР
			//01.03 
			Если Окр(Выборка.СтоимостьППАОборот,2) <> 0 тогда			
				ЗаписьНЗХ = Движения.Налоговый.Добавить();
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("01.03");
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("ПВ");
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Корректировка ППА и ОПС";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = Выборка.СтоимостьППАОборот;
				
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Выборка.ОсновноеСредство);
				
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Перечисления.УсловияПоступленияИВыбытияИмущества.ЗаПлату);
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Выборка.Контрагент);
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 3, Выборка.ДоговорКонтрагента);
				
				ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.Вр;
				ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.Вр;
			КонецЕсли;
			
			//01.К
			//НУ
			Если Окр(Выборка.Счет01К,2) <> 0 тогда			
				ЗаписьНЗХ = Движения.Налоговый.Добавить();
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("01.К");
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("ПВ");
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Корректировка ППА и ОПС";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = Выборка.Счет01К;
				
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Выборка.ОсновноеСредство);
				
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Перечисления.УсловияПоступленияИВыбытияИмущества.Другие);
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Выборка.Контрагент);
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 3, Выборка.ДоговорКонтрагента);
				
				ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.НУ;
				ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.НУ;
			
			
				//01.К
				//ВР  
				
				ЗаписьНЗХ = Движения.Налоговый.Добавить();
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("01.К");
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("ПВ");
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Корректировка ППА и ОПС";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = -Выборка.Счет01К;
				
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Выборка.ОсновноеСредство);
				
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Перечисления.УсловияПоступленияИВыбытияИмущества.Другие);
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Выборка.Контрагент);
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 3, Выборка.ДоговорКонтрагента);
				
				ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ВР;
				ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.ВР;
			
			КонецЕсли;
		КонецЕсли;			
		
	КонецЦикла; 
	
	СформироватьЗаписиРетроКорректировок();  
	
	Движения.Хозрасчетный.Записать();
	Движения.Налоговый.Записать();
	
КонецПроцедуры     

Процедура КорректировкаСПИ()  
	
	Если Не ОтражатьКорректировкуСПИ Тогда
		Возврат;
	КонецЕсли;
	
	Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Записывать = Ложь;
	Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Очистить(); 
	Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ГрафикДоговораДолгосрочнойАрендыСоставППА.ОбъектППА КАК ОсновноеСредство,
	|	ГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка КАК График
	|ПОМЕСТИТЬ ВТ_СписокОС
	|ИЗ
	|	Документ._ГрафикДоговораДолгосрочнойАренды.СоставППА КАК ГрафикДоговораДолгосрочнойАрендыСоставППА
	|ГДЕ
	|	ГрафикДоговораДолгосрочнойАрендыСоставППА.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.Период,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.Регистратор,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.НомерСтроки,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.Активность,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.ОсновноеСредство,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.Организация,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРабот,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.СрокИспользованияДляВычисленияАмортизации,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.СтоимостьДляВычисленияАмортизации,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.КоэффициентАмортизации,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.КоэффициентУскорения,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних.СпособНачисленияАмортизации
	|ПОМЕСТИТЬ ВТ_ПараметрыАмортизацииБухгалтерскийУчет
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	|			КОНЕЦПЕРИОДА(&ДатаРасчета, МЕСЯЦ),
	|			Организация = &Организация
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						ВТ_СписокОС.ОсновноеСредство
	|					ИЗ
	|						ВТ_СписокОС)) КАК ПараметрыАмортизацииБухгалтерскийУчетСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияОСОрганизаций.ОсновноеСредство,
	|	МАКСИМУМ(СостоянияОСОрганизаций.ДатаСостояния) КАК ДатаВводаВЭксплуатацию
	|ПОМЕСТИТЬ ВТ_ДатыВвода
	|ИЗ
	|	РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
	|ГДЕ
	|	СостоянияОСОрганизаций.Организация = &Организация
	|	И СостоянияОСОрганизаций.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				ВТ_СписокОС.ОсновноеСредство
	|			ИЗ
	|				ВТ_СписокОС)
	|	И СостоянияОСОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|
	|СГРУППИРОВАТЬ ПО
	|	СостоянияОСОрганизаций.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА КАК ОсновноеСредство,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) КАК ПервоначальнаяСтоимостьППАПоГрафикуНаОтчетнуюДату
	|ПОМЕСТИТЬ ВТ_ПервоначальнаяСтоимостьППАПоГрафикуНаОтчетнуюДату
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период <= КОНЕЦПЕРИОДА(&ДатаРасчета, ДЕНЬ)
	|	И _УчетДолгосрочнойАрендыостатки.ПериодГрафика < КОНЕЦПЕРИОДА(&ДатаРасчета, ДЕНЬ)
	|	И _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.СтоимостьППА)
	|	И _УчетДолгосрочнойАрендыостатки.ОбъектППА В
	|			(ВЫБРАТЬ
	|				ВТ_СписокОС.ОсновноеСредство
	|			ИЗ
	|				ВТ_СписокОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА КАК ОсновноеСредство,
	|	МАКСИМУМ(_УчетДолгосрочнойАрендыостатки.ПериодГрафика) КАК ПоследнийМесяцАмортизацииПоГрафику
	|ПОМЕСТИТЬ ВТ_ПоследнийМесяцАмортизацииПоГрафику
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период <= КОНЕЦПЕРИОДА(&ДатаРасчета, ДЕНЬ)
	|	И _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.АмортизацияППА)
	|	И _УчетДолгосрочнойАрендыостатки.ОбъектППА В
	|			(ВЫБРАТЬ
	|				ВТ_СписокОС.ОсновноеСредство
	|			ИЗ
	|				ВТ_СписокОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СписокОС.ОсновноеСредство,
	|	ВТ_ПоследнийМесяцАмортизацииПоГрафику.ПоследнийМесяцАмортизацииПоГрафику,
	|	ВТ_ДатыВвода.ДатаВводаВЭксплуатацию,
	|	ВТ_ПервоначальнаяСтоимостьППАПоГрафикуНаОтчетнуюДату.ПервоначальнаяСтоимостьППАПоГрафикуНаОтчетнуюДату
	|ПОМЕСТИТЬ ВТ_ОстаткиИПараметры
	|ИЗ
	|	ВТ_СписокОС КАК ВТ_СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатыВвода КАК ВТ_ДатыВвода
	|		ПО (ВТ_ДатыВвода.ОсновноеСредство = ВТ_СписокОС.ОсновноеСредство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальнаяСтоимостьППАПоГрафикуНаОтчетнуюДату КАК ВТ_ПервоначальнаяСтоимостьППАПоГрафикуНаОтчетнуюДату
	|		ПО (ВТ_ПервоначальнаяСтоимостьППАПоГрафикуНаОтчетнуюДату.ОсновноеСредство = ВТ_СписокОС.ОсновноеСредство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПоследнийМесяцАмортизацииПоГрафику КАК ВТ_ПоследнийМесяцАмортизацииПоГрафику
	|		ПО (ВТ_ПоследнийМесяцАмортизацииПоГрафику.ОсновноеСредство = ВТ_СписокОС.ОсновноеСредство)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОстаткиИПараметры.ОсновноеСредство,
	|	ВТ_ОстаткиИПараметры.ДатаВводаВЭксплуатацию,
	|	РАЗНОСТЬДАТ(ВТ_ОстаткиИПараметры.ДатаВводаВЭксплуатацию, ВТ_ОстаткиИПараметры.ПоследнийМесяцАмортизацииПоГрафику, МЕСЯЦ) КАК РасчетныйСрокПолезногоИспользования,
	|	ВТ_ОстаткиИПараметры.ПервоначальнаяСтоимостьППАПоГрафикуНаОтчетнуюДату
	|ИЗ
	|	ВТ_ОстаткиИПараметры КАК ВТ_ОстаткиИПараметры"; 
	
	мОрганизация = Справочники.Организации.НайтиПоКоду("000000001");
	Запрос.Параметры.Вставить("Организация", мОрганизация );
	Запрос.Параметры.Вставить("ДатаРасчета", Дата); 
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Запись = Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Добавить();
		Запись.Период = Дата;
		Запись.ОсновноеСредство = Выборка.ОсновноеСредство;
		Запись.Организация = мОрганизация;
		Запись.СрокПолезногоИспользования = Выборка.РасчетныйСрокПолезногоИспользования;
		Запись.СрокИспользованияДляВычисленияАмортизации = Выборка.РасчетныйСрокПолезногоИспользования;
		Запись.СтоимостьДляВычисленияАмортизации = Выборка.ПервоначальнаяСтоимостьППАПоГрафикуНаОтчетнуюДату;
		Запись.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.Линейный;
	КонецЦикла;
	
	Движения.ПараметрыАмортизацииОСБухгалтерскийУчет.Записать();
	
КонецПроцедуры

Процедура СформироватьЗаписиРетроКорректировок()
	
	Если НЕ ОтразитьРетроКорректировку Тогда
		Возврат;
	КонецЕсли;
	
	Организация = Справочники.Организации.НайтиПоКоду("000000001");
	
	Для каждого Строка Из РетроКорректировки Цикл  
		
		СпособОтраженияРасходов = ПолучитьСпособОтраженияРасходовПоОС(Строка.ОбъектППА, Дата);
		Если НЕ СпособОтраженияРасходов = Неопределено  Тогда
			ПодразделениеОрганизации91 = СпособОтраженияРасходов.ПодразделениеОрганизации91;
		Иначе
			ПодразделениеОрганизации91 = Справочники.ПодразделенияОрганизаций.НайтиПоКоду("000001478"); //Санкт-Петербург
		КонецЕсли;
		
		//Бухгалтерский учет
		//Амортизация		
		Если Строка.Амортизация <> 0 тогда  
			ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
			ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("91.02.1");
			ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("02.03");
			ЗаписьНЗХ.Организация = Организация;
			ЗаписьНЗХ.Содержание = "Корректировка амортизации ППА за прошлые периоды";
			ЗаписьНЗХ.Период =  Дата;
			ЗаписьНЗХ.Сумма = Строка.Амортизация;  
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, СтатьяПДР_Амортизация); 
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, ПодразделениеОрганизации91); 
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Строка.ОбъектППА);
		КонецЕсли;
		
		//Проценты
		Если Строка.ПроцентныеРасходы <> 0 тогда
			ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
			ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("91.02.1");
			ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.5");
			ЗаписьНЗХ.Организация = Организация;
			ЗаписьНЗХ.Содержание = "Корректировка процентов по долгосрочной аренде за прошлые периоды";
			ЗаписьНЗХ.Период =  Дата;
			ЗаписьНЗХ.Сумма = Строка.ПроцентныеРасходы;  
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, СтатьяПДР_Проценты); 
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, ПодразделениеОрганизации91); 
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Договор.Владелец);
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Договор);
		КонецЕсли;
		
		//АренднаяПлата
		Если Строка.АренднаяПлата <> 0 тогда
			ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
			ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.1");
			ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.2");
			ЗаписьНЗХ.Организация = Организация;
			ЗаписьНЗХ.Содержание = "Корректировка начисления арендной платы по долгосрочной аренде за прошлые периоды";
			ЗаписьНЗХ.Период =  Дата;
			ЗаписьНЗХ.Сумма = Строка.АренднаяПлата + Строка.НДС;  
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Договор.Владелец); 
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Договор); 
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Договор.Владелец);
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, Договор);
		КонецЕсли;
		
		//НДС
		Если Строка.НДС <> 0 тогда
			ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
			ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.1");
			ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.9");
			ЗаписьНЗХ.Организация = Организация;
			ЗаписьНЗХ.Содержание = "Корректировка НДС по долгосрочной аренде за прошлые периоды";
			ЗаписьНЗХ.Период =  Дата;
			ЗаписьНЗХ.Сумма = Строка.НДС;  
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Договор.Владелец);
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, Договор);
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Договор.Владелец);
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Договор);
		КонецЕсли;	
		
		//Налоговый учет
		//Амортизация		
		Если Строка.Амортизация <> 0 тогда  
			ЗаписьНЗХ = Движения.Налоговый.Добавить();
			ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("91.02.7");
			ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("02.03");
			ЗаписьНЗХ.Организация = Организация;
			ЗаписьНЗХ.Содержание = "Корректировка амортизации ППА за прошлые периоды";
			ЗаписьНЗХ.Период =  Дата;
			ЗаписьНЗХ.Сумма = Строка.Амортизация;  
			ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ВР;
			ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.ВР;
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, СтатьяПДР_Амортизация); 
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Строка.ОбъектППА);
		КонецЕсли;
		
		//Проценты
		Если Строка.ПроцентныеРасходы <> 0 тогда
			
			//1
			ЗаписьНЗХ = Движения.Налоговый.Добавить();
			ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("91.02.7");
			ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("ПВ");
			ЗаписьНЗХ.Организация = Организация;
			ЗаписьНЗХ.Содержание = "Корректировка процентов по долгосрочной аренде за прошлые периоды";
			ЗаписьНЗХ.Период =  Дата;
			ЗаписьНЗХ.Сумма = Строка.ПроцентныеРасходы;  
			ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ВР;
			ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.ВР;
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, СтатьяПДР_Проценты); 
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Перечисления.УсловияПоступленияИВыбытияИмущества.ЗаПлату);
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Договор.Владелец);
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 3, Договор);
			
			////2
			//ЗаписьНЗХ = Движения.Налоговый.Добавить();
			//ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("НЕ.03");
			//ЗаписьНЗХ.Организация = Организация;
			//ЗаписьНЗХ.Содержание = "Корректировка процентов по долгосрочной аренде за прошлые периоды";
			//ЗаписьНЗХ.Период =  Дата;
			//ЗаписьНЗХ.Сумма = Строка.ПроцентныеРасходы;  
			//ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.НУ;
		КонецЕсли;
		
		//АренднаяПлата
		Если Строка.АренднаяПлата <> 0 тогда 
			
			Если Строка.АренднаяПлата > 0 Тогда
				//1
				ЗаписьНЗХ = Движения.Налоговый.Добавить();
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("91.02.7");
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("01.К");
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Корректировка начисления арендной платы по долгосрочной аренде за прошлые периоды";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = Строка.АренднаяПлата; 
				ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.НУ;
				ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.НУ;
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, СтатьяПДР_Проценты); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Строка.ОбъектППА);
				
				//2
				ЗаписьНЗХ = Движения.Налоговый.Добавить();
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("91.02.7");
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("01.К");
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Корректировка начисления арендной платы по долгосрочной аренде за прошлые периоды";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = -Строка.АренднаяПлата; 
				ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ВР;
				ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.ВР;
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, СтатьяПДР_Проценты); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Строка.ОбъектППА);
			КонецЕсли;                
			
			Если Строка.АренднаяПлата < 0 Тогда
				//1
				ЗаписьНЗХ = Движения.Налоговый.Добавить();
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("91.01.7");
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("01.К");
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Корректировка начисления арендной платы по долгосрочной аренде за прошлые периоды";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = -Строка.АренднаяПлата; 
				ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.НУ;
				ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.НУ;
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, СтатьяПДР_Проценты); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Строка.ОбъектППА);
				
				//2
				ЗаписьНЗХ = Движения.Налоговый.Добавить();
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("91.01.7");
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("01.К");
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Корректировка начисления арендной платы по долгосрочной аренде за прошлые периоды";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = Строка.АренднаяПлата; 
				ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ВР;
				ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.ВР;
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, СтатьяПДР_Проценты); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Строка.ОбъектППА);
			КонецЕсли;                
			
			
			
			//// Смолина сказала, что корректировок 01К не должно быть на счете 91
			////1
			//ЗаписьНЗХ = Движения.Налоговый.Добавить();
			//ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("01.К");
			//ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("ПВ");
			//ЗаписьНЗХ.Организация = Организация;
			//ЗаписьНЗХ.Содержание = "Корректировка начисления арендной платы по долгосрочной аренде за прошлые периоды";
			//ЗаписьНЗХ.Период =  Дата;
			//ЗаписьНЗХ.Сумма = -Строка.АренднаяПлата; 
			//ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.НУ;
			//ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.НУ;
			//БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Строка.ОбъектППА);
			//БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Перечисления.УсловияПоступленияИВыбытияИмущества.ЗаПлату);  
			//БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Договор.Владелец);  
			//БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 3, Договор);  	
			//
			////2
			//ЗаписьНЗХ = Движения.Налоговый.Добавить();
			//ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("01.К");
			//ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("ПВ");
			//ЗаписьНЗХ.Организация = Организация;
			//ЗаписьНЗХ.Содержание = "Корректировка начисления арендной платы по долгосрочной аренде за прошлые периоды";
			//ЗаписьНЗХ.Период =  Дата;
			//ЗаписьНЗХ.Сумма = Строка.АренднаяПлата; 
			//ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ВР;
			//ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.ВР;
			//БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Строка.ОбъектППА);
			//БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Перечисления.УсловияПоступленияИВыбытияИмущества.ЗаПлату);  
			//БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Договор.Владелец);  
			//БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 3, Договор);  	
			
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура СписатьОстаткиПриВыбытииОбъектаППА(ТЗСоставППА, ДатаСторно, Дата, Договор)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	т.ОбъектППА,
	|	т.ПоказательБазыРаспределения,
	|	т.СтоимостьППА
	|ПОМЕСТИТЬ ВТ_СписокППА
	|ИЗ
	|	&ТЗСоставППА КАК т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Показатель,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) КАК Обязательство,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.НДС) КАК НДС,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) КАК СтоимостьППА,
	|	ВЫБОР
	|		КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|				ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|				ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Выбытие
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокППА КАК ВТ_СписокППА
	|		ПО (ВТ_СписокППА.ОбъектППА = _УчетДолгосрочнойАрендыостатки.ОбъектППА)
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период < &ДатаДокумента
	|	И _УчетДолгосрочнойАрендыостатки.Договор = &Договор
	|	И _УчетДолгосрочнойАрендыостатки.ПериодГрафика < &ДатаНачала
	|	И НЕ _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.ПриведеннаяСтоимость)
	|	И НЕ _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.СтоимостьППА)
	|	И ВЫБОР
	|			КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|					ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|					ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	ВЫБОР
	|		КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|				ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|				ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.Показатель
	|
	|ИМЕЮЩИЕ
	|	(СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) <> 0
	|		ИЛИ СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) <> 0)";
	
	Запрос.Параметры.Вставить("ДатаНачала", ДатаСторно); 
	Запрос.Параметры.Вставить("Договор", Договор); 
	Запрос.Параметры.Вставить("ДатаДокумента", Дата); 
	Запрос.Параметры.Вставить("ТЗСоставППА", ТЗСоставППА);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.График = Ссылка;
		Движение.Период = Дата;
		Движение.Договор = Выборка.Договор;
		Движение.ОбъектППА = Выборка.ОбъектППА; 
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Выбытие;
		Движение.Показатель = Выборка.Показатель;
		Движение.ПериодГрафика = Выборка.ПериодГрафика;
		Движение.ПериодДисконтирования = Выборка.ПериодДисконтирования;
		Движение.Обязательство = -Выборка.Обязательство;
		Движение.НДС = -Выборка.НДС; 
		Движение.СтоимостьППА = -Выборка.СтоимостьППА; 
		Движение.ДатаСобытия = ДатаНачала;
	КонецЦикла; 
	
	
	//СторноПриведеннойСтоимости
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	т.ОбъектППА,
	|	т.ПоказательБазыРаспределения,
	|	т.СтоимостьППА
	|ПОМЕСТИТЬ ВТ_СписокППА
	|ИЗ
	|	&ТЗСоставППА КАК т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Показатель,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) КАК Обязательство,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) КАК СтоимостьППА,
	|	ВЫБОР
	|		КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|				ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|				ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Выбытие
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокППА КАК ВТ_СписокППА
	|		ПО (ВТ_СписокППА.ОбъектППА = _УчетДолгосрочнойАрендыостатки.ОбъектППА)
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Период < &ДатаДокумента
	|	И _УчетДолгосрочнойАрендыостатки.Договор = &Договор
	|	И _УчетДолгосрочнойАрендыостатки.ПериодДисконтирования < &ДатаНачала
	|	И _УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.ПриведеннаяСтоимость)
	|	И ВЫБОР
	|			КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|					ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|					ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	ВЫБОР
	|		КОГДА ВТ_СписокППА.ОбъектППА ЕСТЬ NULL
	|				ИЛИ ВТ_СписокППА.ПоказательБазыРаспределения = 0
	|				ИЛИ ВТ_СписокППА.СтоимостьППА = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	_УчетДолгосрочнойАрендыостатки.Показатель
	|
	|ИМЕЮЩИЕ
	|	(СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) <> 0
	|		ИЛИ СУММА(_УчетДолгосрочнойАрендыостатки.СтоимостьППА) <> 0)";
	
	Запрос.Параметры.Вставить("ТЗСоставППА", ТЗСоставППА);
	Запрос.Параметры.Вставить("ДатаНачала", ДатаСторно); 
	Запрос.Параметры.Вставить("Договор", Договор); 
	Запрос.Параметры.Вставить("ДатаДокумента", Дата); 
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.График = Ссылка;
		Движение.Период = Дата;
		Движение.Договор = Выборка.Договор;
		Движение.ОбъектППА = Выборка.ОбъектППА; 
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Выбытие;
		Движение.Показатель = Выборка.Показатель;
		Движение.ПериодГрафика = Дата(1,1,1);
		Движение.ПериодДисконтирования = Выборка.ПериодДисконтирования;
		Движение.Обязательство = -Выборка.Обязательство;
		Движение.ДатаСобытия = ДатаНачала;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСпособОтраженияРасходовПоОС(ОсновноеСредство, Период)
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеСредства.Ссылка КАК ОсновноеСредство,
	|	ОсновныеСредства.ГруппаОС.Ссылка КАК ГруппаОС,
	|	ОсновныеСредства.ТипОС,
	|	МестонахождениеОСБухгалтерскийУчетСрезПоследних._АдресМестонахождения КАК Адрес,
	|	СпособыОтраженияРасходовПоАмортизацииСпособы.СтатьяЗатрат,
	|	СпособыОтраженияРасходовПоАмортизацииСпособы.НоменклатурнаяГруппа,
	|	СпособыОтраженияРасходовПоАмортизацииСпособы.Подразделение,
	|	СпособыОтраженияРасходовПоАмортизацииСпособы.ПодразделениеОрганизации,
	|	СпособыОтраженияРасходовПоАмортизацииСпособы.СчетЗатрат,
	|	Проекты.Ссылка КАК Проект
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&Период, ) КАК МестонахождениеОСБухгалтерскийУчетСрезПоследних
	|		ПО (МестонахождениеОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство = ОсновныеСредства.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&Период, ) КАК СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчетСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияРасходовПоАмортизации.Способы КАК СпособыОтраженияРасходовПоАмортизацииСпособы
	|			ПО СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчетСрезПоследних.СпособыОтраженияРасходовПоАмортизации = СпособыОтраженияРасходовПоАмортизацииСпособы.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
	|			ПО (СпособыОтраженияРасходовПоАмортизацииСпособы.ПодразделениеОрганизации.КодПоОКТМО = Проекты.Код)
	|		ПО (СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство = ОсновныеСредства.Ссылка)
	|ГДЕ
	|	ОсновныеСредства.Ссылка = &ОсновноеСредство";
	
	Запрос.УстановитьПараметр ("ОсновноеСредство", ОсновноеСредство);
	Запрос.УстановитьПараметр ("Период", Период);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	БитПроекты = Справочники.ПодразделенияОрганизаций.НайтиПоКоду("000001332");
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Структура = Новый Структура;
		Структура.Вставить("ОсновноеСредство", ВыборкаДетальныеЗаписи.ОсновноеСредство);
		Структура.Вставить("ГруппаОС", ВыборкаДетальныеЗаписи.ГруппаОС);
		Структура.Вставить("ТипОС", ВыборкаДетальныеЗаписи.ТипОС);
		Структура.Вставить("Адрес", ВыборкаДетальныеЗаписи.Адрес);
		Структура.Вставить("СтатьяЗатрат", ВыборкаДетальныеЗаписи.СтатьяЗатрат);
		Структура.Вставить("НоменклатурнаяГруппа", ВыборкаДетальныеЗаписи.НоменклатурнаяГруппа);
		Структура.Вставить("Подразделение", ВыборкаДетальныеЗаписи.Подразделение);
		Структура.Вставить("ПодразделениеОрганизации", ВыборкаДетальныеЗаписи.ПодразделениеОрганизации);
		Структура.Вставить("Проект", ВыборкаДетальныеЗаписи.Проект);
		Структура.Вставить("СчетЗатрат", ВыборкаДетальныеЗаписи.СчетЗатрат);
		
		Если ВыборкаДетальныеЗаписи.ПодразделениеОрганизации.Родитель = БитПроекты Тогда
			ПодразделениеОрганизации91 = ВыборкаДетальныеЗаписи.ПодразделениеОрганизации;
		ИначеЕсли ВыборкаДетальныеЗаписи.ПодразделениеОрганизации.Родитель.Родитель = БитПроекты Тогда
			ПодразделениеОрганизации91 = ВыборкаДетальныеЗаписи.ПодразделениеОрганизации.Родитель;
		ИначеЕсли ВыборкаДетальныеЗаписи.ПодразделениеОрганизации.Родитель.Родитель.Родитель = БитПроекты Тогда
			ПодразделениеОрганизации91 = ВыборкаДетальныеЗаписи.ПодразделениеОрганизации.Родитель.Родитель;
		ИначеЕсли ВыборкаДетальныеЗаписи.ПодразделениеОрганизации.Родитель.Родитель.Родитель.Родитель = БитПроекты Тогда
			ПодразделениеОрганизации91 = ВыборкаДетальныеЗаписи.ПодразделениеОрганизации.Родитель.Родитель.Родитель;
		ИначеЕсли ВыборкаДетальныеЗаписи.ПодразделениеОрганизации.Родитель.Родитель.Родитель.Родитель.Родитель = БитПроекты Тогда
			ПодразделениеОрганизации91 = ВыборкаДетальныеЗаписи.ПодразделениеОрганизации.Родитель.Родитель.Родитель.Родитель;
		Иначе 
			ПодразделениеОрганизации91 = Справочники.ПодразделенияОрганизаций.НайтиПоКоду("000001478"); //Санкт-Петербург
		КонецЕсли;
		Структура.Вставить("ПодразделениеОрганизации91", ПодразделениеОрганизации91);
		
		Результат = Структура;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПолучитьСпособОтраженияРасходовПоОС()

Процедура РасторжениеБУиНУ()
	
	Если НЕ ВидГрафика = Перечисления._ВидыГрафиковДолгосрочнойАренды.Расторжение Тогда
		Возврат;
	КонецЕсли; 
	
	Движения.Хозрасчетный.Записывать = Ложь;
	Движения.Хозрасчетный.Очистить();
	Движения.Налоговый.Записывать = Ложь;
	Движения.Налоговый.Очистить();
	
	ФинРезОтРасторженияБУ = 0;
	
	Организация = Справочники.Организации.НайтиПоКоду("000000001");
	ТЗ_БУ = РасторжениеБУ.Выгрузить();
	ТЗ_НУ = РасторжениеНУ.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление._Учет.Бухгалтерский) КАК Учет,
	|	ТЗ.Счет,
	|	ТЗ.ОбъектППА,
	|	ТЗ.СуммаДт,
	|	ТЗ.СуммаКт
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	&ТЗ_БУ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление._Учет.Налоговый) КАК Учет,
	|	ТЗ_НУ.Счет,
	|	ТЗ_НУ.ВидУчета,
	|	ТЗ_НУ.ОбъектППА,
	|	ТЗ_НУ.СуммаДт,
	|	ТЗ_НУ.СуммаКт
	|ПОМЕСТИТЬ ВТ_НУ
	|ИЗ
	|	&ТЗ_НУ КАК ТЗ_НУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Учет,
	|	NULL КАК ВидУчета,
	|	ВТ.Счет,
	|	ВЫБОР
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АрендованноеИмущество)
	|				ИЛИ ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АмортизацияАрендованногоИмущества)
	|				ИЛИ ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОбесценениеОсновныхСредств)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновныеСредства)
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АрендныеОбязательства)
	|				ИЛИ ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПроцентыПоАренде)
	|				ИЛИ ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоАренднымОбязательствам)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоАренде)
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СчетСинтетический,
	|	ВТ.ОбъектППА,
	|	ВТ.СуммаДт,
	|	ВТ.СуммаКт,
	|	ВЫБОР
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АрендованноеИмущество)
	|			ТОГДА -(ВТ.СуммаДт - ВТ.СуммаКт)
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АмортизацияАрендованногоИмущества)
	|			ТОГДА ВТ.СуммаКт - ВТ.СуммаДт
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОбесценениеОсновныхСредств)
	|			ТОГДА ВТ.СуммаКт - ВТ.СуммаДт
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АрендныеОбязательства)
	|			ТОГДА ВТ.СуммаКт - ВТ.СуммаДт
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПроцентыПоАренде)
	|			ТОГДА -(ВТ.СуммаДт - ВТ.СуммаКт)
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСпоАренднымОбязательствам)
	|			ТОГДА -(ВТ.СуммаДт - ВТ.СуммаКт)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ ВТ_РасчетБУ
	|ИЗ
	|	ВТ КАК ВТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Учет,
	|	ВТ.ВидУчета,
	|	ВТ.Счет,
	|	ВЫБОР
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Налоговый.АрендованноеИмущество)
	|				ИЛИ ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Налоговый.АмортизацияАрендованногоИмущества)
	|				ИЛИ ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Налоговый.Обесценение_ОС)
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Налоговый.ОсновныеСредства)
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Налоговый.КорректировкаСтоимостиАрендованногоИмущества)
	|			ТОГДА ВТ.Счет
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СчетСинтетический,
	|	ВТ.ОбъектППА,
	|	ВТ.СуммаДт,
	|	ВТ.СуммаКт,
	|	ВЫБОР
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Налоговый.АрендованноеИмущество)
	|			ТОГДА -(ВТ.СуммаДт - ВТ.СуммаКт)
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Налоговый.АмортизацияАрендованногоИмущества)
	|			ТОГДА ВТ.СуммаКт - ВТ.СуммаДт
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Налоговый.Обесценение_ОС)
	|			ТОГДА ВТ.СуммаКт - ВТ.СуммаДт
	|		КОГДА ВТ.Счет = ЗНАЧЕНИЕ(ПланСчетов.Налоговый.КорректировкаСтоимостиАрендованногоИмущества)
	|			ТОГДА 0
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ ВТ_РасчетНУ
	|ИЗ
	|	ВТ_НУ КАК ВТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РасчетБУ.Учет,
	|	ВТ_РасчетБУ.ВидУчета,
	|	ВТ_РасчетБУ.Счет,
	|	ВТ_РасчетБУ.СчетСинтетический,
	|	ВТ_РасчетБУ.ОбъектППА,
	|	ВТ_РасчетБУ.СуммаДт,
	|	ВТ_РасчетБУ.СуммаКт,
	|	ВТ_РасчетБУ.Сумма
	|ПОМЕСТИТЬ ВТ_Общая
	|ИЗ
	|	ВТ_РасчетБУ КАК ВТ_РасчетБУ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_РасчетНУ.Учет,
	|	ВТ_РасчетНУ.ВидУчета,
	|	ВТ_РасчетНУ.Счет,
	|	ВТ_РасчетНУ.СчетСинтетический,
	|	ВТ_РасчетНУ.ОбъектППА,
	|	ВТ_РасчетНУ.СуммаДт,
	|	ВТ_РасчетНУ.СуммаКт,
	|	ВТ_РасчетНУ.Сумма
	|ИЗ
	|	ВТ_РасчетНУ КАК ВТ_РасчетНУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Общая.Учет КАК Учет,
	|	ВТ_Общая.ВидУчета КАК ВидУчета,
	|	ВТ_Общая.Счет,
	|	ВТ_Общая.СчетСинтетический КАК СчетСинтетический,
	|	ВТ_Общая.ОбъектППА КАК ОбъектППА,
	|	ВТ_Общая.СуммаДт,
	|	ВТ_Общая.СуммаКт,
	|	ВТ_Общая.Сумма КАК Сумма
	|ИЗ
	|	ВТ_Общая КАК ВТ_Общая
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Общая.Учет.Порядок
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	Учет,
	|	СчетСинтетический,
	|	ОбъектППА,
	|	ВидУчета";
	
	//Сортировка по виду учета, т.к. сначала требуется получить и отразить ФинРез по БУ, а потом он используется и в НУ
	//Хотя, как вариант, можно взять ту же цифру из реквизита ФинРезультатРасторжениеБУ 
	
	Запрос.Параметры.Вставить("ТЗ_БУ", ТЗ_БУ);
	Запрос.Параметры.Вставить("ТЗ_НУ", ТЗ_НУ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаВыше = РезультатЗапроса;
	ВыборкаУчет = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Учет");
	
	Пока ВыборкаУчет.Следующий() Цикл
		ВыборкаВыше = ВыборкаУчет;
		ВыборкаСчетСинтетический = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СчетСинтетический");
		Пока ВыборкаСчетСинтетический.Следующий() Цикл
			ВыборкаВыше = ВыборкаСчетСинтетический;
			ВыборкаОбъектППА = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОбъектППА");
			Пока ВыборкаОбъектППА.Следующий() Цикл
				ВыборкаВыше = ВыборкаОбъектППА;
				ВыборкаВидУчета = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ВидУчета");
				Пока ВыборкаВидУчета.Следующий() Цикл
					ВыборкаВыше = ВыборкаВидУчета;
					ВыборкаДетальныеЗаписи = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, );
					Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
						
						//Хозрасчетный
						Если 
							ВыборкаУчет.Учет = Перечисления._Учет.Бухгалтерский 
							и ОтражатьВБухгалтерскомУчете
							Тогда
							
							// Списание ППА	на 01.09					
							Если ВыборкаСчетСинтетический.СчетСинтетический.Код = "01"  Тогда
								
								Если ВыборкаДетальныеЗаписи.Счет.Код = "01.03" Тогда
									ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
									ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01.09");
									ЗаписьНЗХ.СчетКт = ВыборкаДетальныеЗаписи.Счет;
									ЗаписьНЗХ.Организация = Организация;
									ЗаписьНЗХ.Содержание = "Списание ППА в связи с расторжением договора";
									ЗаписьНЗХ.Период =  Дата;
									ЗаписьНЗХ.Сумма = ВыборкаДетальныеЗаписи.СуммаДт;  
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, ВыборкаДетальныеЗаписи.ОбъектППА); 
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, ВыборкаДетальныеЗаписи.ОбъектППА);
								КонецЕсли;
								
								Если ВыборкаДетальныеЗаписи.Счет.Код = "02.03" Тогда
									ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
									ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01.09");
									ЗаписьНЗХ.СчетДт = ВыборкаДетальныеЗаписи.Счет;
									ЗаписьНЗХ.Организация = Организация;
									ЗаписьНЗХ.Содержание = "Списание ППА в связи с расторжением договора";
									ЗаписьНЗХ.Период =  Дата;
									ЗаписьНЗХ.Сумма = ВыборкаДетальныеЗаписи.СуммаКт;  
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, ВыборкаДетальныеЗаписи.ОбъектППА); 
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, ВыборкаДетальныеЗаписи.ОбъектППА);
								КонецЕсли;
								
								Если ВыборкаДетальныеЗаписи.Счет.Код = "02.54" Тогда
									ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
									ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01.09");
									ЗаписьНЗХ.СчетДт = ВыборкаДетальныеЗаписи.Счет;
									ЗаписьНЗХ.Организация = Организация;
									ЗаписьНЗХ.Содержание = "Списание ППА в связи с расторжением договора";
									ЗаписьНЗХ.Период =  Дата;
									ЗаписьНЗХ.Сумма = ВыборкаДетальныеЗаписи.СуммаКт;  
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, ВыборкаДетальныеЗаписи.ОбъектППА); 
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, ВыборкаДетальныеЗаписи.ОбъектППА);
								КонецЕсли;
								
							КонецЕсли;
							
							//Сворачиваем 76.07
							Если ВыборкаСчетСинтетический.СчетСинтетический.Код = "76.07" Тогда
								
								Если ВыборкаДетальныеЗаписи.Счет.Код = "76.07.5" или ВыборкаДетальныеЗаписи.Счет.Код = "76.07.9" Тогда
									ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
									ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.1");
									ЗаписьНЗХ.СчетКт = ВыборкаДетальныеЗаписи.Счет;
									ЗаписьНЗХ.Организация = Организация;
									ЗаписьНЗХ.Содержание = "Списание приведенной стоимости в связи с расторжением договора";
									ЗаписьНЗХ.Период =  Дата;
									ЗаписьНЗХ.Сумма = ВыборкаДетальныеЗаписи.СуммаДт;  
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Договор.Владелец); 
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, Договор); 
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Договор.Владелец);
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Договор);
								КонецЕсли;
							КонецЕсли;							
						КонецЕсли; 
						
						//Налоговый
						Если 
							ВыборкаУчет.Учет = Перечисления._Учет.Налоговый
							и ОтражатьВНалоговомУчете
							Тогда  
							
							// Списание ППА	на 01.09					
							Если ВыборкаСчетСинтетический.СчетСинтетический.Код = "01"  Тогда
								Если ВыборкаДетальныеЗаписи.Счет.Код = "01.03" Тогда
									ЗаписьНЗХ = Движения.Налоговый.Добавить();
									ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("01.09");
									ЗаписьНЗХ.СчетКт = ВыборкаДетальныеЗаписи.Счет;
									ЗаписьНЗХ.ВидУчетаДт = ВыборкаДетальныеЗаписи.ВидУчета; 
									ЗаписьНЗХ.ВидУчетаКт = ВыборкаДетальныеЗаписи.ВидУчета; 
									ЗаписьНЗХ.Организация = Организация;
									ЗаписьНЗХ.Содержание = "Списание ППА в связи с расторжением договора";
									ЗаписьНЗХ.Период =  Дата;
									ЗаписьНЗХ.Сумма = ВыборкаДетальныеЗаписи.СуммаДт;  
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, ВыборкаДетальныеЗаписи.ОбъектППА); 
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, ВыборкаДетальныеЗаписи.ОбъектППА);
								КонецЕсли;
								
								Если ВыборкаДетальныеЗаписи.Счет.Код = "02.03" Тогда
									ЗаписьНЗХ = Движения.Налоговый.Добавить();
									ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("01.09");
									ЗаписьНЗХ.СчетДт = ВыборкаДетальныеЗаписи.Счет;
									ЗаписьНЗХ.ВидУчетаДт = ВыборкаДетальныеЗаписи.ВидУчета; 
									ЗаписьНЗХ.ВидУчетаКт = ВыборкаДетальныеЗаписи.ВидУчета; 
									ЗаписьНЗХ.Организация = Организация;
									ЗаписьНЗХ.Содержание = "Списание ППА в связи с расторжением договора";
									ЗаписьНЗХ.Период =  Дата;
									ЗаписьНЗХ.Сумма = ВыборкаДетальныеЗаписи.СуммаКт;  
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, ВыборкаДетальныеЗаписи.ОбъектППА); 
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, ВыборкаДетальныеЗаписи.ОбъектППА);
								КонецЕсли;
								
								Если ВыборкаДетальныеЗаписи.Счет.Код = "02.54" Тогда
									ЗаписьНЗХ = Движения.Налоговый.Добавить();
									ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("01.09");
									ЗаписьНЗХ.СчетДт = ВыборкаДетальныеЗаписи.Счет;
									ЗаписьНЗХ.ВидУчетаДт = ВыборкаДетальныеЗаписи.ВидУчета; 
									ЗаписьНЗХ.ВидУчетаКт = ВыборкаДетальныеЗаписи.ВидУчета; 
									ЗаписьНЗХ.Организация = Организация;
									ЗаписьНЗХ.Содержание = "Списание ППА в связи с расторжением договора";
									ЗаписьНЗХ.Период =  Дата;
									ЗаписьНЗХ.Сумма = ВыборкаДетальныеЗаписи.СуммаКт;  
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, ВыборкаДетальныеЗаписи.ОбъектППА); 
									БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, ВыборкаДетальныеЗаписи.ОбъектППА);
								КонецЕсли;
							КонецЕсли;
							
							// Списание 01К					
							Если ВыборкаСчетСинтетический.СчетСинтетический.Код = "01.К"  Тогда
								ЗаписьНЗХ = Движения.Налоговый.Добавить();
								ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("ПВ");
								ЗаписьНЗХ.СчетДт = ВыборкаДетальныеЗаписи.Счет;
								ЗаписьНЗХ.ВидУчетаДт = ВыборкаДетальныеЗаписи.ВидУчета; 
								ЗаписьНЗХ.ВидУчетаКт = ВыборкаДетальныеЗаписи.ВидУчета; 
								ЗаписьНЗХ.Организация = Организация;
								ЗаписьНЗХ.Содержание = "Списание 01.К в связи с расторжением договора";
								ЗаписьНЗХ.Период =  Дата;
								ЗаписьНЗХ.Сумма = -ВыборкаДетальныеЗаписи.СуммаДт;  
								БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Перечисления.УсловияПоступленияИВыбытияИмущества.Другие);  
								БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Договор.Владелец);  
								БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 3, Договор);  	
								БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, ВыборкаДетальныеЗаписи.ОбъектППА);
							КонецЕсли;
							
							
						КонецЕсли;
						
						//ВыборкаДетальныеЗаписи
					КонецЦикла;
					
					//Налоговый 
					Если ВыборкаУчет.Учет = Перечисления._Учет.Налоговый 
						и ОтражатьВНалоговомУчете	
						Тогда
						// Списание счета 01.09 на 76.07.1 по ППА
						Если ВыборкаСчетСинтетический.СчетСинтетический.Код = "01" Тогда
							ЗаписьНЗХ = Движения.Налоговый.Добавить();
							ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("ПВ");
							ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("01.09"); 
							ЗаписьНЗХ.ВидУчетаДт = ВыборкаВидУчета.ВидУчета; 
							ЗаписьНЗХ.ВидУчетаКт = ВыборкаВидУчета.ВидУчета; 
							ЗаписьНЗХ.Организация = Организация;
							ЗаписьНЗХ.Содержание = "Списание ППА в связи с расторжением договора";
							ЗаписьНЗХ.Период =  Дата;
							ЗаписьНЗХ.Сумма = -ВыборкаВидУчета.Сумма;  
							БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Перечисления.УсловияПоступленияИВыбытияИмущества.ЗаПлату);  
							БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, Договор.Владелец);  
							БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 3, Договор);  	
							БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, ВыборкаОбъектППА.ОбъектППА); 
						КонецЕсли;
					КонецЕсли;	
					
					
					//ВыборкаВидУчета
				КонецЦикла;
				
				//Хозрасчетный 
				Если ВыборкаУчет.Учет = Перечисления._Учет.Бухгалтерский 
					и ОтражатьВБухгалтерскомУчете
					Тогда
					// Списание счета 01.09 на 76.07.1 по ППА
					Если ВыборкаСчетСинтетический.СчетСинтетический.Код = "01" Тогда
						ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
						ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.1");
						ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("01.09");
						ЗаписьНЗХ.Организация = Организация;
						ЗаписьНЗХ.Содержание = "Списание ППА в связи с расторжением договора";
						ЗаписьНЗХ.Период =  Дата;
						ЗаписьНЗХ.Сумма = -ВыборкаОбъектППА.Сумма;  
						БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Договор.Владелец);  
						БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, Договор);  	
						БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, ВыборкаОбъектППА.ОбъектППА); 
					КонецЕсли;
				КонецЕсли;	
				
				//ВыборкаОбъектППА
			КонецЦикла;
			
			//ВыборкаСчетСинтетический
		КонецЦикла; 
		
		//ВыборкаУчет
		
		//Хозрасчетный Финрез
		Если 
			ВыборкаУчет.Учет = Перечисления._Учет.Бухгалтерский
			и ОтражатьВБухгалтерскомУчете
			Тогда
			
			ФинРезОтРасторженияБУ = ВыборкаУчет.Сумма;
			
			Если ФинРезОтРасторженияБУ > 0 Тогда
				ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.1");
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.ПрочиеДоходы;
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Финансовый результат в связи с расторжением договора";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = ФинРезОтРасторженияБУ;  
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Договор.Владелец);  
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, Договор);  	
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, СтатьяПДР_Расторжение); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, ПодразделениеОрганизации_Расторжение); 
			КонецЕсли;
			
			Если ФинРезОтРасторженияБУ < 0 Тогда
				ЗаписьНЗХ = Движения.Хозрасчетный.Добавить();
				ЗаписьНЗХ.СчетДт = ПланыСчетов.Хозрасчетный.ПрочиеРасходыНеОблагаемыеЕНВД;
				ЗаписьНЗХ.СчетКт = ПланыСчетов.Хозрасчетный.НайтиПоКоду("76.07.1");
				ЗаписьНЗХ.Организация = Организация;
				ЗаписьНЗХ.Содержание = "Финансовый результат в связи с расторжением договора";
				ЗаписьНЗХ.Период =  Дата;
				ЗаписьНЗХ.Сумма = -ФинРезОтРасторженияБУ;  
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, СтатьяПДР_Расторжение); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, ПодразделениеОрганизации_Расторжение); 
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Договор.Владелец);  
				БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Договор);  	
			КонецЕсли;
			
		КонецЕсли;
		
		//ВыборкаУчет	
	КонецЦикла;
	
	//ФинРез в НУ  
	Если ОтражатьВНалоговомУчете Тогда 
		
		Если ФинРезОтРасторженияБУ > 0 Тогда
			ЗаписьНЗХ = Движения.Налоговый.Добавить();
			ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("ПВ");
			ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("91.01.7"); 
			ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ВР; 
			ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.ВР; 
			ЗаписьНЗХ.Организация = Организация;
			ЗаписьНЗХ.Содержание = "Финансовый результат в связи с расторжением договора";
			ЗаписьНЗХ.Период =  Дата;
			ЗаписьНЗХ.Сумма = ФинРезОтРасторженияБУ;  
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, Перечисления.УсловияПоступленияИВыбытияИмущества.Другие); 		
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 2, Договор.Владелец); 
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 3, Договор);  	
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, СтатьяПДР_Расторжение); 
		КонецЕсли;
		
		Если ФинРезОтРасторженияБУ < 0 Тогда
			ЗаписьНЗХ = Движения.Налоговый.Добавить();
			ЗаписьНЗХ.СчетДт = ПланыСчетов.Налоговый.НайтиПоКоду("91.02.7");
			ЗаписьНЗХ.СчетКт = ПланыСчетов.Налоговый.НайтиПоКоду("ПВ");
			ЗаписьНЗХ.ВидУчетаДт = Перечисления.ВидыУчетаПоПБУ18.ВР; 
			ЗаписьНЗХ.ВидУчетаКт = Перечисления.ВидыУчетаПоПБУ18.ВР; 
			ЗаписьНЗХ.Организация = Организация;
			ЗаписьНЗХ.Содержание = "Финансовый результат в связи с расторжением договора";
			ЗаписьНЗХ.Период =  Дата;
			ЗаписьНЗХ.Сумма = -ФинРезОтРасторженияБУ;  
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетДт, ЗаписьНЗХ.СубконтоДт, 1, СтатьяПДР_Расторжение); 
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 1, Перечисления.УсловияПоступленияИВыбытияИмущества.Другие);  
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 2, Договор.Владелец);  
			БухгалтерскийУчет.УстановитьСубконто(ЗаписьНЗХ.СчетКт, ЗаписьНЗХ.СубконтоКт, 3, Договор);  	
		КонецЕсли;
		
	КонецЕсли; 
	
	Движения.Хозрасчетный.Записать();
	Движения.Налоговый.Записать();
	
КонецПроцедуры

Процедура ПересчитатьТолькоАренднуюПлатуВГрафиках2026()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.Показатель,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) КАК Обязательство,
	|	СУММА(_УчетДолгосрочнойАрендыостатки.НДС) КАК НДС
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК _УчетДолгосрочнойАрендыостатки
	|ГДЕ
	|	_УчетДолгосрочнойАрендыостатки.Показатель = ЗНАЧЕНИЕ(Перечисление._ПоказателиГрафиковДолгосрочнойАренды.АрендныеПлатежи)
	|	И _УчетДолгосрочнойАрендыостатки.Регистратор = &Регистратор
	|	И _УчетДолгосрочнойАрендыостатки.ВидДвиженияГрафика в (&ВидыДвижения)
	|
	|СГРУППИРОВАТЬ ПО
	|	_УчетДолгосрочнойАрендыостатки.ОбъектППА,
	|	_УчетДолгосрочнойАрендыостатки.ПериодДисконтирования,
	|	_УчетДолгосрочнойАрендыостатки.ПериодГрафика,
	|	_УчетДолгосрочнойАрендыостатки.Договор,
	|	_УчетДолгосрочнойАрендыостатки.Показатель
	|
	|ИМЕЮЩИЕ
	|	СУММА(_УчетДолгосрочнойАрендыостатки.Обязательство) <> 0";
	
	ВидыДвижения = Новый СписокЗначений;
	ВидыДвижения.Добавить(Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Поступление);
	ВидыДвижения.Добавить(Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Начисление);
	
	Запрос.Параметры.Вставить("ВидыДвижения", ВидыДвижения); 
	Запрос.Параметры.Вставить("Регистратор", Ссылка.ПредыдущаяРедакция);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.График = Ссылка;
		Движение.Период = Дата;
		Движение.Договор = Выборка.Договор;
		Движение.ОбъектППА = Выборка.ОбъектППА; 
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		
		Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Сторно;
		
		Движение.Показатель = Выборка.Показатель;
		Движение.ПериодГрафика = Выборка.ПериодГрафика;
		Движение.ПериодДисконтирования = Выборка.ПериодДисконтирования;
		Движение.Обязательство = -Выборка.Обязательство;
		Движение.НДС = -Выборка.НДС; 
		Движение.СтоимостьППА = 0;
		Движение.ДатаСобытия = ДатаНачала;
		
	КонецЦикла; 	

	//Новые строки графика
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.УИДСтрокиГрафика,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбъектППА,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.АренднаяПлата,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.НДС,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ПриведеннаяСтоимость,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ПроцентныеРасходы,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбязательствоНаНачало,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ИзменениеОбязательства,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ОбязательствоНаКонец,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.ДолгосрочнаяЧасть,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.КраткосрочнаяЧасть,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.СтоимостьППА,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.Амортизация,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.АмортизацияНакопленная,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.БалансоваяСтоимость,
	|	_ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.АктивностьСтроки,
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.ДатаС,
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.ДатаПо КАК ДатаПо,
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Дней
	|ИЗ
	|	Документ._ГрафикДоговораДолгосрочнойАренды.ГрафикПоДоговору КАК ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений._ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик КАК _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик
	|		ПО ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Ссылка = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.График
	|			И ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.УИДСтроки = _ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.УИДСтрокиГрафика
	|ГДЕ
	|	ГрафикДоговораДолгосрочнойАрендыГрафикПоДоговору.Ссылка = &Ссылка"; 
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка); 	
	Запрос.Параметры.Вставить("ДатаИтогов", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		//АрендныеПлатежи Начисление
		Движение = Движения._УчетДолгосрочнойАренды.Добавить();
		Движение.Период = Дата;
		Движение.Договор = Договор;
		Движение.График = Ссылка;
		Движение.ОбъектППА = Выборка.ОбъектППА;
		Движение.ИДСтроки = Новый УникальныйИдентификатор;
		
		Движение.ВидДвиженияГрафика = Перечисления._ВидыДвиженийГрафиковДолгосрочнойАренды.Начисление;
		
		Движение.Показатель = перечисления._ПоказателиГрафиковДолгосрочнойАренды.АрендныеПлатежи;
		Движение.ПериодГрафика = Выборка.ДатаПо;
		Движение.ПериодДисконтирования = Дата(1,1,1);
		Движение.Обязательство = -Выборка.АренднаяПлата;  
		Движение.НДС = -Выборка.НДС;  
		Движение.СтоимостьППА = 0; 
		Движение.ДатаСобытия = ДатаНачала;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СторноПредыдущегоДокумента()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	РегистрСведений._УчетДолгосрочнойАренды КАК Движения
	|ГДЕ
	|	Движения.Регистратор = &ДокументИсточник";

	Запрос.Параметры.Вставить("ДокументИсточник", ПредыдущаяРедакция);

	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл

		Движение = Движения._УчетДолгосрочнойАренды.Добавить();

		ЗаполнитьЗначенияСвойств(Движение, Выборка);

		Движение.Период       = Дата;                 // дата сторно
		Движение.Регистратор   = Ссылка;               // текущий документ
		Движение.ИДСтроки      = Новый УникальныйИдентификатор;

		Движение.Обязательство          = -Выборка.Обязательство;
		Движение.СтоимостьППА           = -Выборка.СтоимостьППА;
		Движение.НДС                    = -Выборка.НДС;
		Движение.АвансОбязательство     = -Выборка.АвансОбязательство;
		Движение.АвансАктив             = -Выборка.АвансАктив;
		Движение.АвансНДС               = -Выборка.АвансНДС;

	КонецЦикла;

КонецПроцедуры

Процедура СторноПроводокБУиНУ()

	// Очищаем
	Движения.Хозрасчетный.Записывать = Ложь;
	Движения.Хозрасчетный.Очистить();
	Движения.Налоговый.Записывать = Ложь;
	Движения.Налоговый.Очистить();

	// Сторно Хозрасчетного (только если отражается в БУ)
	Если ОтражатьВБухгалтерскомУчете Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ * ИЗ РегистрБухгалтерии.Хозрасчетный ГДЕ Регистратор = &ПредыдущаяРедакция";
		Запрос.Параметры.Вставить("ПредыдущаяРедакция", ПредыдущаяРедакция);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Движение = Движения.Хозрасчетный.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			Движение.Период = Дата;
			Движение.Регистратор = Ссылка;
			Движение.Сумма = -Выборка.Сумма;
		КонецЦикла;
	КонецЕсли;

	// Сторно Налогового (только если отражается в НУ)
	Если ОтражатьВНалоговомУчете Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ * ИЗ РегистрБухгалтерии.Налоговый ГДЕ Регистратор = &ПредыдущаяРедакция";
		Запрос.Параметры.Вставить("ПредыдущаяРедакция", ПредыдущаяРедакция);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Движение = Движения.Налоговый.Добавить();
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			Движение.Период = Дата;
			Движение.Регистратор = Ссылка;
			Движение.Сумма = -Выборка.Сумма;
		КонецЦикла;
	КонецЕсли;

	// Записываем
	Движения.Хозрасчетный.Записать();
	Движения.Налоговый.Записать();

КонецПроцедуры

Функция ПроверитьСбалансированностьГрафика(Договор, Дата) Экспорт

    Результат = Новый Структура;
    Результат.Вставить("БалансОк", Истина);
    Результат.Вставить("ТекстСообщения", "");

    Запрос = Новый Запрос;
    Запрос.Текст = 
    "ВЫБРАТЬ
    |    Остатки.ОбъектППА,
    |    СУММА(Остатки.Обязательство) КАК Обязательство,
    |    СУММА(Остатки.СтоимостьППА) КАК СтоимостьППА,
    |    СУММА(Остатки.АвансОбязательство) КАК АвансОбязательство,
    |    СУММА(Остатки.АвансНДС) КАК АвансНДС,
    |    СУММА(Остатки.АвансАктив) КАК АвансАктив
    |ИЗ
    |    РегистрСведений._УчетДолгосрочнойАренды КАК Остатки
    |ГДЕ
    |    Остатки.Договор = &Договор
    |    И Остатки.Период <= &Период
    |
    |СГРУППИРОВАТЬ ПО
    |    Остатки.ОбъектППА";

    Запрос.Параметры.Вставить("Договор", Договор);
    Запрос.Параметры.Вставить("Период", Дата);

    ИтогОбязательство      = 0;
    ИтогСтоимостьППА       = 0;
    ИтогАвансОбязательство = 0;
    ИтогАвансНДС           = 0;
    ИтогАвансАктив         = 0;

    ТекстДетали = "";

	Выборка = Запрос.Выполнить().Выбрать();
    Пока Выборка.Следующий() Цикл

        ИтогОбязательство      = ИтогОбязательство      + Выборка.Обязательство;
        ИтогСтоимостьППА       = ИтогСтоимостьППА       + Выборка.СтоимостьППА;
        ИтогАвансОбязательство = ИтогАвансОбязательство + Выборка.АвансОбязательство;
        ИтогАвансНДС           = ИтогАвансНДС           + Выборка.АвансНДС;
        ИтогАвансАктив         = ИтогАвансАктив         + Выборка.АвансАктив;

        Если Выборка.Обязательство > 0.01 ИЛИ Выборка.Обязательство < -0.01
            ИЛИ Выборка.СтоимостьППА > 0.01 ИЛИ Выборка.СтоимостьППА < -0.01
            ИЛИ Выборка.АвансОбязательство > 0.01 ИЛИ Выборка.АвансОбязательство < -0.01
            ИЛИ Выборка.АвансНДС > 0.01 ИЛИ Выборка.АвансНДС < -0.01
            ИЛИ Выборка.АвансАктив > 0.01 ИЛИ Выборка.АвансАктив < -0.01
        Тогда
            Результат.БалансОк = Ложь;

            ТекстОбъекта = СтрШаблон("Разбаланс по объекту: %1", Выборка.ОбъектППА);

            Если Выборка.Обязательство > 0.01 ИЛИ Выборка.Обязательство < -0.01 Тогда
                ТекстОбъекта = ТекстОбъекта + СтрШаблон(
                    Символы.ПС + "  Обязательство: %1",
                    Формат(Выборка.Обязательство, "ЧДЦ=2")
                );
            КонецЕсли;

            Если Выборка.СтоимостьППА > 0.01 ИЛИ Выборка.СтоимостьППА < -0.01 Тогда
                ТекстОбъекта = ТекстОбъекта + СтрШаблон(
                    Символы.ПС + "  Стоимость ППА: %1",
                    Формат(Выборка.СтоимостьППА, "ЧДЦ=2")
                );
            КонецЕсли;

            Если Выборка.АвансОбязательство > 0.01 ИЛИ Выборка.АвансОбязательство < -0.01 Тогда
                ТекстОбъекта = ТекстОбъекта + СтрШаблон(
                    Символы.ПС + "  Аванс обязательство: %1",
                    Формат(Выборка.АвансОбязательство, "ЧДЦ=2")
                );
            КонецЕсли;

            Если Выборка.АвансНДС > 0.01 ИЛИ Выборка.АвансНДС < -0.01 Тогда
                ТекстОбъекта = ТекстОбъекта + СтрШаблон(
                    Символы.ПС + "  Аванс НДС: %1",
                    Формат(Выборка.АвансНДС, "ЧДЦ=2")
                );
            КонецЕсли;

            Если Выборка.АвансАктив > 0.01 ИЛИ Выборка.АвансАктив < -0.01 Тогда
                ТекстОбъекта = ТекстОбъекта + СтрШаблон(
                    Символы.ПС + "  Аванс актив: %1",
                    Формат(Выборка.АвансАктив, "ЧДЦ=2")
                );
            КонецЕсли;

            ТекстДетали = ТекстДетали + ТекстОбъекта + Символы.ПС;
        КонецЕсли;

    КонецЦикла;

    Если НЕ Результат.БалансОк Тогда

        ТекстСообщения = "Разбаланс по договору в целом:";

        Если ИтогОбязательство > 0.01 ИЛИ ИтогОбязательство < -0.01 Тогда
            ТекстСообщения = ТекстСообщения + СтрШаблон(
                Символы.ПС + "  Обязательство: %1",
                Формат(ИтогОбязательство, "ЧДЦ=2")
            );
        КонецЕсли;

        Если ИтогСтоимостьППА > 0.01 ИЛИ ИтогСтоимостьППА < -0.01 Тогда
            ТекстСообщения = ТекстСообщения + СтрШаблон(
                Символы.ПС + "  Стоимость ППА: %1",
                Формат(ИтогСтоимостьППА, "ЧДЦ=2")
            );
        КонецЕсли;

        Если ИтогАвансОбязательство > 0.01 ИЛИ ИтогАвансОбязательство < -0.01 Тогда
            ТекстСообщения = ТекстСообщения + СтрШаблон(
                Символы.ПС + "  Аванс обязательство: %1",
                Формат(ИтогАвансОбязательство, "ЧДЦ=2")
            );
        КонецЕсли;

        Если ИтогАвансНДС > 0.01 ИЛИ ИтогАвансНДС < -0.01 Тогда
            ТекстСообщения = ТекстСообщения + СтрШаблон(
                Символы.ПС + "  Аванс НДС: %1",
                Формат(ИтогАвансНДС, "ЧДЦ=2")
            );
        КонецЕсли;

        Если ИтогАвансАктив > 0.01 ИЛИ ИтогАвансАктив < -0.01 Тогда
            ТекстСообщения = ТекстСообщения + СтрШаблон(
                Символы.ПС + "  Аванс актив: %1",
                Формат(ИтогАвансАктив, "ЧДЦ=2")
            );
        КонецЕсли;

        Результат.ТекстСообщения = ТекстСообщения + Символы.ПС + "----------------------" + Символы.ПС + ТекстДетали;

    КонецЕсли;

    Возврат Результат;

КонецФункции



&НаСервере
Процедура РассчитатьИндивидуальныеГрафикиНаСервере()
	
	
	РазбивкаПоСоставу = РегистрыСведений._ГрафикДоговораДолгосрочнойАрендыДетальныйГрафик.СоздатьНаборЗаписей();
	РазбивкаПоСоставу.Отбор.График.Установить(Объект.Ссылка);
	РазбивкаПоСоставу.Прочитать();
	РазбивкаПоСоставу.Очистить();
	
	ГрафикНачинаетсяПервогоЧисла = НачалоМесяца(Объект.ДатаНачала) = Объект.ДатаНачала;
	
	//ТЗОГ это ТЗОбщийГрафик
	//Здесь будет сводный график по договору
	ТЗОГСДатами = Объект.ГрафикПоДоговору.Выгрузить(); 
	
	СписокПолейДляОчистки =	ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок("ПриведеннаяСтоимость,ПроцентныеРасходы,ОбязательствоНаНачало,ОбязательствоНаКонец,ИзменениеОбязательства,ДолгосрочнаяЧасть,КраткосрочнаяЧасть,СтоимостьППА,Амортизация,АмортизацияНакопленная,БалансоваяСтоимость",",");
	Для каждого Строка Из ТЗОГСДатами Цикл
		Для каждого Элемент Из СписокПолейДляОчистки Цикл
			Строка[Элемент] = 0;
		КонецЦикла;
	КонецЦикла;
	
	//ТЗИГ это ТЗИндивидуальныеГрафики
	//Подготовим ТЗ без лишних колонок
	ТЗИГДляКопирования = ТЗОГСДатами.Скопировать();
	ТЗИГДляКопирования.Колонки.Добавить("ОбъектППА", Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
	ТЗИГДляКопирования.Сортировать("ДатаПо");
	
	МассивСтрокДляУдаления = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок("Активность,Реквизит1,ИсходныйНомерСтроки",","); 
	Для каждого Элемент Из МассивСтрокДляУдаления Цикл
		Колонка = ТЗИГДляКопирования.Колонки.Найти(Элемент);
		Если Не Колонка = Неопределено Тогда
			ТЗИГДляКопирования.Колонки.Удалить(Колонка);
		КонецЕсли;
	КонецЦикла;
	
	ТЗОГДляВсехОбъектов = ТЗИГДляКопирования.Скопировать();
	ТЗОГДляВсехОбъектов.Очистить();
	
	//Подготовим ТЗ со списком арендованных объектов
	ТЗСостав = Объект.СоставППА.Выгрузить();
	МассивСтрокДляУдаления = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок("НомерСтроки,СтатьяПрочихДоходовИРасходов,ПодразделениеОрганизации91,Примечание,ИсходныйНомерСтроки",",");
	Для каждого Элемент Из МассивСтрокДляУдаления Цикл
		Колонка = ТЗСостав.Колонки.Найти(Элемент);
		Если Не Колонка = Неопределено Тогда
			ТЗСостав.Колонки.Удалить(Колонка);
		КонецЕсли;
	КонецЦикла;  
	
	ИтогБазыРаспределения = ТЗСостав.Итог("ПоказательБазыРаспределения");
	
	ГлОбщаяСтоимостьППА = 0;
	ГлОбщаяПриведеннаяСтоимость = 0;
	ГлОбщаяАмортизацияНаНачало = 0;
	ГлОстатокОПСНАНачало = 0;
	ГлКорректировка = 0;
	
	Для каждого СтрокаСостав Из ТзСостав Цикл 
		
		ТЗИГ =  ТЗИГДляКопирования.Скопировать();
		
		ДатаНачала 						= Объект.ДатаНачала;
		ДатаОкончания 					= Объект.ДатаОкончания;
		СтавкаДисконтирования 			= Объект.СтавкаДисконтирования;
		ДатаС 							= ДатаНачала;
		ОбщаяПриведеннаяСтоимость 		= 0; 
		АвансПоОбъекту					= 0;
		
		//Приведенная стоимость		
		Для каждого СтрокаИГ Из ТЗИГ Цикл
			СтрокаИГ.АренднаяПлата = СтрокаИГ.АренднаяПлата	/ ИтогБазыРаспределения * СтрокаСостав.ПоказательБазыРаспределения;
			
			СтрокаИГ.НДС 		= СтрокаИГ.НДС			/ ИтогБазыРаспределения * СтрокаСостав.ПоказательБазыРаспределения;
			СтрокаИГ.НДСсАванса = СтрокаИГ.НДСсАванса	/ ИтогБазыРаспределения * СтрокаСостав.ПоказательБазыРаспределения;
			СтрокаИГ.НДС_Всего 	= СтрокаИГ.НДС_Всего	/ ИтогБазыРаспределения * СтрокаСостав.ПоказательБазыРаспределения;
			
			ДнейСНачала = (НачалоДня(СтрокаИГ.ДатаПо) - НачалоДня(ДатаНачала)) / (60 * 60 * 24) + 1;
			СтрокаИГ.ПриведеннаяСтоимость = СтрокаИГ.АренднаяПлата / pow((1 + СтавкаДисконтирования / 100) , (ДнейСНачала / 365));
			ОбщаяПриведеннаяСтоимость = ОбщаяПриведеннаяСтоимость + СтрокаИГ.ПриведеннаяСтоимость; 
			СтрокаИГ.ОбъектППА = СтрокаСостав.ОбъектППА;
		КонецЦикла;	
		ОбязательствоНаНачало = ОбщаяПриведеннаяСтоимость;  
		ГлОбщаяПриведеннаяСтоимость = ГлОбщаяПриведеннаяСтоимость + ОбщаяПриведеннаяСтоимость;
		
		//Процентные расходы 76.05 
		Для каждого СтрокаИГ Из ТЗИГ Цикл
			СтрокаИГ.ОбязательствоНаНачало = ОбязательствоНаНачало;
			СтрокаИГ.ПроцентныеРасходы = СтрокаИГ.ОбязательствоНаНачало * (
			pow( 
			((100 + СтавкаДисконтирования) / 100) 
			, 
			(СтрокаИГ.Дней / 365)
			) 
			- 1);
			СтрокаИГ.ОбязательствоНаКонец = СтрокаИГ.ОбязательствоНаНачало + СтрокаИГ.ПроцентныеРасходы - СтрокаИГ.АренднаяПлата; 
			//Если окр(СтрокаИГ.ОбязательствоНаКонец,2) = 0 Тогда
			//	СтрокаИГ.ОбязательствоНаКонец = 0 ;
			//КонецЕсли;
			СтрокаИГ.ИзменениеОбязательства = СтрокаИГ.ОбязательствоНаКонец - СтрокаИГ.ОбязательствоНаНачало;                  
			ОбязательствоНаНачало = СтрокаИГ.ОбязательствоНаКонец;		
		КонецЦикла;	
		
		//Долгосрочная и краткосрочная части обязательства
		Для каждого СтрокаИГ Из ТЗИГ Цикл           
			ТЗИГ_ДЧ = ТЗИГ.Скопировать(,"ДатаПо, ИзменениеОбязательства");
			СтрокиДляУдаления = новый Массив;
			ДатаНаГодБольше = ДобавитьМесяц(СтрокаИГ.ДатаПо, 12);
			Для каждого СтрокаТЗИГ_ДЧ Из ТЗИГ_ДЧ Цикл
				Если СтрокаТЗИГ_ДЧ.ДатаПо <= ДатаНаГодБольше Тогда
					СтрокиДляУдаления.Добавить(СтрокаТЗИГ_ДЧ);
				КонецЕсли;
			КонецЦикла;
			Для каждого СтрокаДляУдаления Из СтрокиДляУдаления Цикл
				ТЗИГ_ДЧ.Удалить(СтрокаДляУдаления);	
			КонецЦикла; 
			СтрокаИГ.ДолгосрочнаяЧасть = -ТЗИГ_ДЧ.Итог("ИзменениеОбязательства");
			СтрокаИГ.КраткосрочнаяЧасть = СтрокаИГ.ОбязательствоНаКонец - СтрокаИГ.ДолгосрочнаяЧасть;
		КонецЦикла;
		
		//Расчет корректировки Д01.03 К76.07
		
		ПараметрыПолученияОстатков = Новый Структура;
		ПараметрыПолученияОстатков.Вставить("Договор", 							Объект.Договор);
		ПараметрыПолученияОстатков.Вставить("ОбъектППА", 						СтрокаСостав.ОбъектППА);
		ПараметрыПолученияОстатков.Вставить("ДатаНачалаГрафика", 				Объект.ДатаНачала);
		ПараметрыПолученияОстатков.Вставить("ДатаДокумента", 					Объект.Дата);
		ПараметрыПолученияОстатков.Вставить("График", 							Объект.Ссылка);
		ПараметрыПолученияОстатков.Вставить("ГрафикНачинаетсяПервогоЧисла", 	ГрафикНачинаетсяПервогоЧисла);
		
		ОстаткиНаНачало = ПолучитьОстаткиПоОбъектуППАНаДату(ПараметрыПолученияОстатков); 
		ЭтоНовыйОбъектППА = ОстаткиНаНачало.ЭтоНовыйОбъектППА;
		
		ОПСПоНовомуГрафику = ТЗИГ.Итог("ПриведеннаяСтоимость");
		
		Если ЭтоНовыйОбъектППА Тогда
			Корректировка = 0;
		Иначе
			Если ГрафикНачинаетсяПервогоЧисла Тогда
				Корректировка = ?(ОстаткиНаНачало.ОбязательствоОстаток <> 0, ОПСПоНовомуГрафику - ОстаткиНаНачало.ОбязательствоОстаток , 0);
			Иначе
				//Корректировка = ?(ОстаткиНаНачало.ОбязательствоОстаток <> 0, ОПСПоНовомуГрафику - ОстаткиНаНачало.ОбязательствоОстаток + ТЗИГ[0].ИзменениеОбязательства , 0);
				Корректировка = ?(ОстаткиНаНачало.ОбязательствоОстаток <> 0, ОПСПоНовомуГрафику - ОстаткиНаНачало.ОбязательствоОстаток, 0);
			КонецЕсли;
		КонецЕсли;
		
		//Корректировка = ?(Объект.ПредыдущаяРедакция <> документы.ГрафикДоговораДолгосрочнойАренды.ПустаяСсылка(), ОПСПоНовомуГрафику - ОстаткиНаНачало.ОбязательствоОстаток, 0);
		
		//Расчет стоимости ППА 01.03
		//Если это новый график, нужно брать ОПС + ОбеспечительныйПлатеж,
		//если изменения в договор, нужно увеличивать стоимость ППА на корректировку
		Если ЭтоНовыйОбъектППА Тогда
			СтоимостьППА = ОПСПоНовомуГрафику + Объект.Аванс / ИтогБазыРаспределения * СтрокаСостав.ПоказательБазыРаспределения;
		Иначе			
			СтоимостьППА = ОстаткиНаНачало.ПервоначальнаяСтоимость + Корректировка;
		КонецЕсли;
		
		АвансПоОбъекту = Объект.Аванс / ИтогБазыРаспределения * СтрокаСостав.ПоказательБазыРаспределения;
		
		//Расчет амортизации ППА
		АмортизацияНакопленная = ОстаткиНаНачало.НакопленнаяАмортизация;
		БалансоваяСтоимость = СтоимостьППА - АмортизацияНакопленная;
		ГлОбщаяСтоимостьППА = ГлОбщаяСтоимостьППА + СтоимостьППА;
		
		АмортизацияАвансаНакопленная = 0;
		
		ТЗИГ_РасчетСПИ = ТЗИГ.Скопировать(,"ДатаПо");
		Для каждого СтрокаТЗИГ_РасчетСПИ  Из ТЗИГ_РасчетСПИ Цикл
			СтрокаТЗИГ_РасчетСПИ.ДатаПо = НачалоМесяца(СтрокаТЗИГ_РасчетСПИ.ДатаПо);
		КонецЦикла;
		ТЗИГ_РасчетСПИ.Свернуть("ДатаПо");
		
		
		Если ЭтоНовыйОбъектППА Тогда
			СПИ = ТЗИГ_РасчетСПИ.Количество() - 1; 
		Иначе 
			Если ГрафикНачинаетсяПервогоЧисла Тогда
				СПИ = ТЗИГ_РасчетСПИ.Количество(); 
			Иначе
				СПИ = ТЗИГ_РасчетСПИ.Количество() - 1; 
			КонецЕсли;
		КонецЕсли;       
		
		ОстатокСПИ = СПИ; 
		Амортизация = 0;
		
		АмортизацияАванса = 0;
		
		Для каждого СтрокаИГ Из ТЗИГ Цикл 
			СтрокаИГ.СтоимостьППА = СтоимостьППА; 
			Если СтрокаИГ.НомерСтроки = 1 Тогда
				Если ЭтоНовыйОбъектППА Тогда
					Амортизация = 0;
					АмортизацияАванса = 0;
				Иначе 
					Если ГрафикНачинаетсяПервогоЧисла Тогда
						Амортизация = БалансоваяСтоимость / ОстатокСПИ;
						АмортизацияАванса = (АвансПоОбъекту - АмортизацияАвансаНакопленная) / ОстатокСПИ;
						ОстатокСПИ = ОстатокСПИ - 1;
					Иначе
						Амортизация = 0;
						АмортизацияАванса = 0;
					КонецЕсли;
				КонецЕсли;
			Иначе     
				Амортизация = БалансоваяСтоимость / ОстатокСПИ;
				АмортизацияАванса = (АвансПоОбъекту - АмортизацияАвансаНакопленная) / ОстатокСПИ;
				ОстатокСПИ = ОстатокСПИ - 1;
			КонецЕсли;
			
			АмортизацияНакопленная = АмортизацияНакопленная + Амортизация;
			АмортизацияАвансаНакопленная = АмортизацияАвансаНакопленная + АмортизацияАванса;
			
			БалансоваяСтоимость =  СтоимостьППА - АмортизацияНакопленная;
			СтрокаИГ.Амортизация = Амортизация;			
			СтрокаИГ.АмортизацияНакопленная = АмортизацияНакопленная; 
			СтрокаИГ.АмортизацияАванса 				= АмортизацияАванса;			
			СтрокаИГ.АмортизацияАвансаНакопленная 	= АмортизацияАвансаНакопленная; 
			СтрокаИГ.БалансоваяСтоимость 			= БалансоваяСтоимость;
			
		КонецЦикла;
		
		ГлОбщаяАмортизацияНаНачало = ГлОбщаяАмортизацияНаНачало + ОстаткиНаНачало.НакопленнаяАмортизация;
		ГлОстатокОПСНАНачало = ГлОстатокОПСНАНачало + ОстаткиНаНачало.ОбязательствоОстаток;
		ГлКорректировка = ГлКорректировка + Корректировка;
		
		//Создание записей детального графика в регистре сведений
		//и перенос индивидуального графика в общую таблицу
		Для каждого СтрокаИГ Из ТЗИГ Цикл 
			ЗаписьРазбивкиПоСоставу = РазбивкаПоСоставу.Добавить(); 
			ЗаполнитьЗначенияСвойств(ЗаписьРазбивкиПоСоставу,СтрокаИГ); 
			ЗаписьРазбивкиПоСоставу.Регистратор = Объект.Ссылка;
			ЗаписьРазбивкиПоСоставу.График = Объект.Ссылка;
			ЗаписьРазбивкиПоСоставу.УИДСтрокиГрафика = СтрокаИГ.УИДСтроки;
			
			СтрокаТЗДляВсехОбъектов =  ТЗОГДляВсехОбъектов.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТЗДляВсехОбъектов,СтрокаИГ); 
		КонецЦикла;	
		
		//Заполнение ТЧ СоставППА
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ОбъектППА", СтрокаСостав.ОбъектППА);
		Строки = Объект.СоставППА.НайтиСтроки(ПараметрыОтбора);
		Строки[0].СтоимостьППА = СтоимостьППА;
		Строки[0].НакопленнаяАмортизация = ОстаткиНаНачало.НакопленнаяАмортизация;
		Строки[0].Аванс = АвансПоОбъекту;
		
	КонецЦикла; 
	
	//Свертка общей таблица
	МассивСтрокДляУдаления = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок("ДатаС,ДатаПо,Дней,ОбъектППА,НомерСтроки",",");
	Для каждого Элемент Из МассивСтрокДляУдаления Цикл
		Колонка = ТЗОГДляВсехОбъектов.Колонки.Найти(Элемент);
		Если Не Колонка = Неопределено Тогда
			ТЗОГДляВсехОбъектов.Колонки.Удалить(Колонка);
		КонецЕсли;
	КонецЦикла;
	
	ТЗОГДляВсехОбъектов.Свернуть("УИДСтроки","АренднаяПлата,ЗачетАванса,ПриведеннаяСтоимость,ПроцентныеРасходы,ОбязательствоНаНачало,ОбязательствоНаКонец,ИзменениеОбязательства,ДолгосрочнаяЧасть,КраткосрочнаяЧасть,СтоимостьППА,Амортизация,АмортизацияАванса,АмортизацияНакопленная,АмортизацияАвансаНакопленная,БалансоваяСтоимость");
	
	//Заполнение ТЧ_ГрафикПоДоговору свернутыми данными общего графика
	Для каждого СтрокаСвернутогоИГ Из ТЗОГДляВсехОбъектов Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("УИДСтроки", СтрокаСвернутогоИГ.УИДСтроки);
		Строки = Объект.ГрафикПоДоговору.НайтиСтроки(ПараметрыОтбора);
		ЗаполнитьЗначенияСвойств(Строки[0],СтрокаСвернутогоИГ,"АренднаяПлата,ЗачетАванса,ПриведеннаяСтоимость,ПроцентныеРасходы,ОбязательствоНаНачало,ОбязательствоНаКонец,ИзменениеОбязательства,ДолгосрочнаяЧасть,КраткосрочнаяЧасть,СтоимостьППА,Амортизация,АмортизацияАванса,АмортизацияНакопленная,АмортизацияАвансаНакопленная,БалансоваяСтоимость");
	КонецЦикла; 
	
	//Заполнение справочных итоговых реквизитов
	объект.СтоимостьППА 			= ГлОбщаяСтоимостьППА;
	объект.ОПС 						= ГлОбщаяПриведеннаяСтоимость;
	объект.АмППАНаНачало 			= ГлОбщаяАмортизацияНаНачало;
	объект.ПредОПСНаДатуРедакции 	= ГлОстатокОПСНАНачало;
	объект.Корректировка 			= ГлКорректировка;
	
	Записать();
	РазбивкаПоСоставу.Записать();
	
КонецПроцедуры
