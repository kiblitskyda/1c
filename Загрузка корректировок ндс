
#Область СлужебныеПроцедурыИФункции

Процедура ПриОткрытии()
	
	ЗаполнитьАналитикуРегионов();
	ЭлементыФормы.ПолеИсходнойТаблицы.ОтображатьЗаголовки = Истина;
	
	ДатаРегистрации = НачалоМесяца(ТекущаяДата()) - 1;
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Вопрос("Закрыть обработку?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры 

Процедура КнопкаВыполнитьНажатие(Кнопка)
	// Вставить содержимое обработчика.
КонецПроцедуры

Процедура СвернутьВсеУзлы()
	
	ТаблицаФормы = ЭлементыФормы.СгруппированныеДанные;
	ДеревоЗначений = ТаблицаФормы.Значение;
	
	// Устанавливаем фокус на первую строку верхнего уровня
	Если ДеревоЗначений.Строки.Количество() > 0 Тогда
		ТаблицаФормы.ТекущаяСтрока = ДеревоЗначений.Строки[0];
	КонецЕсли;
	
	Для Каждого Строка Из ДеревоЗначений.Строки Цикл
		СвернутьВетку(ТаблицаФормы, Строка);
	КонецЦикла;
	
КонецПроцедуры

Процедура РазвернутьВсеУзлы()
	
	ТаблицаФормы = ЭлементыФормы.СгруппированныеДанные;
	ДеревоЗначений = ТаблицаФормы.Значение;
	
	// Устанавливаем фокус на первую строку верхнего уровня
	Если ДеревоЗначений.Строки.Количество() > 0 Тогда
		ТаблицаФормы.ТекущаяСтрока = ДеревоЗначений.Строки[0];
	КонецЕсли;
	
	Для Каждого Строка Из ДеревоЗначений.Строки Цикл
		РазвернутьВетку(ТаблицаФормы, Строка);
	КонецЦикла;
	
КонецПроцедуры

Процедура СвернутьВетку(ТаблицаФормы, СтрокаДерева)
	
	ТаблицаФормы.Свернуть(СтрокаДерева);
	
	Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.Строки Цикл
		СвернутьВетку(ТаблицаФормы, ПодчиненнаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

Процедура РазвернутьВетку(ТаблицаФормы, СтрокаДерева)
	
	ТаблицаФормы.Развернуть(СтрокаДерева);
	
	Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.Строки Цикл
		РазвернутьВетку(ТаблицаФормы, ПодчиненнаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

Процедура СвернутьДоУровня(Уровень)
	
	ТаблицаФормы = ЭлементыФормы.СгруппированныеДанные;
	ДеревоЗначений = ТаблицаФормы.Значение;
	
	// Сначала все разворачиваем
	РазвернутьВсеУзлы();
	
	// Затем сворачиваем все строки ниже указанного уровня
	Для Каждого Строка Из ДеревоЗначений.Строки Цикл
		СвернутьВеткуДоУровня(ТаблицаФормы, Строка, 1, Уровень);
	КонецЦикла;
	
КонецПроцедуры

Процедура РазвернутьДоУровня(Уровень)
	
	ТаблицаФормы = ЭлементыФормы.СгруппированныеДанные;
	ДеревоЗначений = ТаблицаФормы.Значение;
	
	// Сначала все сворачиваем
	СвернутьВсеУзлы();
	
	// Затем разворачиваем все строки до указанного уровня
	Для Каждого Строка Из ДеревоЗначений.Строки Цикл
		РазвернутьВеткуДоУровня(ТаблицаФормы, Строка, 1, Уровень);
	КонецЦикла;
	
КонецПроцедуры

Процедура СвернутьВеткуДоУровня(ТаблицаФормы, СтрокаДерева, ТекущийУровень, НужныйУровень)
	
	Если ТекущийУровень >= НужныйУровень Тогда
		// На нужном уровне и глубже - сворачиваем всю ветку полностью
		СвернутьВетку(ТаблицаФормы, СтрокаДерева);
	Иначе
		// Выше нужного уровня - оставляем развернутым и обрабатываем подчиненные строки
		ТаблицаФормы.Развернуть(СтрокаДерева);
		
		Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.Строки Цикл
			СвернутьВеткуДоУровня(ТаблицаФормы, ПодчиненнаяСтрока, ТекущийУровень + 1, НужныйУровень);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура РазвернутьВеткуДоУровня(ТаблицаФормы, СтрокаДерева, ТекущийУровень, НужныйУровень)
	
	ТаблицаФормы.Развернуть(СтрокаДерева);
	
	Если ТекущийУровень < НужныйУровень Тогда
		// Продолжаем разворачивать глубже
		Для Каждого ПодчиненнаяСтрока Из СтрокаДерева.Строки Цикл
			РазвернутьВеткуДоУровня(ТаблицаФормы, ПодчиненнаяСтрока, ТекущийУровень + 1, НужныйУровень);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура СвернутьДоУровня1()
	СвернутьДоУровня(1); // Показать только первый уровень
КонецПроцедуры

Процедура СвернутьДоУровня2()
	СвернутьДоУровня(2); // Показать уровни 1-2
КонецПроцедуры

Процедура СвернутьДоУровня3()
	СвернутьДоУровня(3); // Показать уровни 1-3
КонецПроцедуры

Процедура РазвернутьДоУровня1()
	РазвернутьДоУровня(1); // Развернуть только первый уровень
КонецПроцедуры

Процедура РазвернутьДоУровня2()
	РазвернутьДоУровня(2); // Развернуть уровни 1-2
КонецПроцедуры

Процедура РазвернутьДоУровня3()
	РазвернутьДоУровня(3); // Развернуть уровни 1-3
КонецПроцедуры

Процедура РазвернутьДоУровня4()
	РазвернутьДоУровня(4); // Развернуть уровни 1-3
КонецПроцедуры

Процедура РазвернутьДоУровня5(Кнопка)
	РазвернутьДоУровня(5); 
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаИзФайла 

Процедура ИмяФайлаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИмяФайла = ПолучитьИмяФайла(ИмяФайла);
	Если Не ПустаяСтрока(ИмяФайла) Тогда
		ЗагрузитьИзФайла();
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьИмяФайла(ПолноеИмяФайла = "", Сохранение = Ложь)
	
	РежимДиалога = ?(Сохранение, 
						РежимДиалогаВыбораФайла.Сохранение, 
						РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалога);
	ДиалогВыбораФайла.Фильтр = "Таблица Excel (*.xls;*.xlsx)|*.xls;*.xlsx|Таблица 1С (*.mxl)|*.mxl";  
	
	ПозицияПоследнейТочки = СтрНайти(ПолноеИмяФайла, ".", НаправлениеПоиска.СКонца);
	Если ПозицияПоследнейТочки > 0 Тогда 
		ДиалогВыбораФайла.Каталог = Лев(ПолноеИмяФайла, ПозицияПоследнейТочки - 1);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПолноеИмяФайла) Тогда
		ДиалогВыбораФайла.ПолноеИмяФайла = ПолноеИмяФайла;
	КонецЕсли;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		Возврат ДиалогВыбораФайла.ПолноеИмяФайла; 
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции 

Процедура ЗагрузитьИзФайла()
	
	Если ИмяФайла = Неопределено ИЛИ СокрЛП(ИмяФайла) = "" Тогда
		ИмяФайла = ПолучитьИмяФайла();
	КонецЕсли;	
	
	Если ИмяФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТабДокумент = ЭлементыФормы.ПолеИсходнойТаблицы;
	ТабДокумент.Очистить();
	ТаблицаДанных.Очистить(); 
	ТипизированныеДанные.Колонки.Очистить();
	
	ЭтаФорма.Обновить();
	
	ТабДокумент.Прочитать(ИмяФайла, СпособЧтенияЗначенийТабличногоДокумента.Значение);
	
	ПоследняяСтрока = ТабДокумент.ВысотаТаблицы;
	ПоследняяКолонка = ТабДокумент.ШиринаТаблицы;
	
	ОбластьЯчеек = ТабДокумент.Область(1, 1, ПоследняяСтрока, ПоследняяКолонка);
	ИсточникДанных = Новый ОписаниеИсточникаДанных(ОбластьЯчеек);
	ПостроительОтчета = Новый ПостроительОтчета;
	ПостроительОтчета.ИсточникДанных = ИсточникДанных;  
	ПостроительОтчета.Выполнить(); 
	ТаблицаДанных = ПостроительОтчета.Результат.Выгрузить();
	
	КолонкиДляУдаления = новый Массив;
	Для каждого Колонка Из ТаблицаДанных.Колонки Цикл
		Если Колонка.Имя = "КодПроекта_источникаТеплоснабжения_" или Колонка.Имя = "НаименованиеПроекта_источникаТеплоснабжения_" Тогда
			КолонкиДляУдаления.Добавить(Колонка);
		КонецЕсли;
	КонецЦикла;
	Для каждого Колонка Из КолонкиДляУдаления Цикл
		ТаблицаДанных.Колонки.Удалить(Колонка);
	КонецЦикла; 
	
	НоваяКолонка = ТаблицаДанных.Колонки.Вставить(0, "НомерСтрокиXl", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,0)));
	
	Счетчик = 1;
	Для каждого Строка Из ТаблицаДанных Цикл
		Счетчик = Счетчик + 1;
		Строка.НомерСтрокиXl = Счетчик;
	КонецЦикла;
	
	ЭлементыФормы.ТаблицаДанных.СоздатьКолонки();
	
	СоздатьСтруктуруТаблицыТипизированныхДанных();
	ЗаполнитьТипизированныеДанные();
	ЭлементыФормы.ТипизированныеДанные.СоздатьКолонки();
	ОтформатироватьТипизированныеДанные();
	
	ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы.ЗагруженныеДанные;
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработкаЗагруженныхДанных

Процедура ЗаполнитьТипизированныеДанные()
	
	СтавкиНДС = ПолучитьСтавкиНДС();
	
	Для каждого Строка Из ТаблицаДанных Цикл
		
		НоваяСтрока = ТипизированныеДанные.Добавить();
		НоваяСтрока.Номенклатура = Справочники.Номенклатура.НайтиПоКоду(Строка.КодНоменклатуры);
		НоваяСтрока.СубъектРФ = Справочники._СубъектыРФ.НайтиПоКоду(Строка.КодСубъектаРФ);
		НоваяСтрока.НоменклатурнаяГруппа = НоваяСтрока.Номенклатура.НоменклатурнаяГруппа; 
		НоваяСтрока.ПериодРасчета = ПолучитьДатуИзСтроки(Строка.ПериодРасчёта);
		НоваяСтрока.ОтражаетсяВМесяце = ПолучитьДатуИзСтроки(Строка.ОтражаетсяВМесяце);
		НоваяСтрока.ОтражаетсяВКвартале = ПолучитьДатуИзСтроки(Строка.ОтражаетсяВКвартале);
		НоваяСтрока.СтавкаНДС = СтавкиНДС[Строка.СтавкаНДС]; 
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка, "СодержаниеНоменклатуры, НомерСтрокиXl, Цена, Количество, Сумма, СуммаНДС, ВидПерерасчёта, ДополнительныйЛист, Книга"); 
		НоваяСтрока.СуммаСНДС = НоваяСтрока.Сумма + НоваяСтрока.СуммаНДС;
		
		ЗаполнитьКонтрагентаИДоговор (НоваяСтрока);
		
	КонецЦикла; 
	
	ЗаполнитьДокументыРеализации();	
	
КонецПроцедуры

Процедура ЗаполнитьДокументыРеализации()
	
	ТЗ  = ТипизированныеДанные.Скопировать(, "НомерСтрокиXl, Контрагент, ДоговорКонтрагента, ПериодРасчета");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Т.НомерСтрокиXl,
	|	Т.Контрагент,
	|	Т.ДоговорКонтрагента,
	|	Т.ПериодРасчета
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	&ТЗ КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.НомерСтрокиXl КАК НомерСтрокиXl,
	|	ВТ.Контрагент,
	|	ВТ.ДоговорКонтрагента,
	|	ВТ.ПериодРасчета,
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг._ТехническаяРеализацияЭС
	|ИЗ
	|	ВТ КАК ВТ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ВТ.Контрагент = РеализацияТоваровУслуг.Контрагент
	|			И ВТ.ДоговорКонтрагента = РеализацияТоваровУслуг.ДоговорКонтрагента
	|			И (НАЧАЛОПЕРИОДА(ВТ.ПериодРасчета, МЕСЯЦ) <= РеализацияТоваровУслуг.Дата)
	|			И (КОНЕЦПЕРИОДА(ВТ.ПериодРасчета, МЕСЯЦ) >= РеализацияТоваровУслуг.Дата)
	|ГДЕ
	|	РеализацияТоваровУслуг._ТехническаяРеализацияЭС = ИСТИНА
	|	И РеализацияТоваровУслуг.ПометкаУдаления = ЛОЖЬ
	|ИТОГИ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Ссылка)
	|ПО
	|	НомерСтрокиXl";
	
	Запрос.УстановитьПараметр("ТЗ", ТЗ);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаНомерСтрокиXl = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НомерСтрокиXl");
	Пока ВыборкаНомерСтрокиXl.Следующий() Цикл
		СтрокаТЗ = ТипизированныеДанные.Найти(ВыборкаНомерСтрокиXl.НомерСтрокиXl, "НомерСтрокиXl");
		Если НЕ СтрокаТЗ = Неопределено Тогда
			СтрокаТЗ.КоличествоНайденныхДокументовРеализации = ВыборкаНомерСтрокиXl.Ссылка;
			ВыборкаДетальныеЗаписи = ВыборкаНомерСтрокиXl.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, );
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				СтрокаТЗ.ДокументРеализации = ВыборкаДетальныеЗаписи.Ссылка;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКонтрагентаИДоговор (Строка)
	
	СтрокаАналитики = АналитикаРегионов.Найти(Строка.СубъектРФ, "СубъектРФ");	
	
	Если НЕ СтрокаАналитики = Неопределено Тогда
		Строка.Контрагент = СтрокаАналитики.Контрагент;
		Строка.ДоговорКонтрагента = СтрокаАналитики.Договор;
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьСтруктуруТаблицыТипизированныхДанных()
	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("НомерСтрокиXl", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,0)),"Строка файла");
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("СубъектРФ", новый ОписаниеТипов("СправочникСсылка._СубъектыРФ"), "Субъект РФ");	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("Контрагент", новый ОписаниеТипов("СправочникСсылка.Контрагенты"));	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("ДоговорКонтрагента", новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"), "Договор");	
	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("Номенклатура", новый ОписаниеТипов("СправочникСсылка.Номенклатура"));	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("НоменклатурнаяГруппа", новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"), "Номенклатурная группа");	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("СодержаниеНоменклатуры", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(255)), "Содержание номенклатуры" );
	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("ПериодРасчета", новый ОписаниеТипов("Дата"), "Период расчета");	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("ОтражаетсяВМесяце", новый ОписаниеТипов("Дата"), "Отражается в месяце");	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("ОтражаетсяВКвартале", новый ОписаниеТипов("Дата"), "Отражается в квартале");
	
	
	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("ВидПерерасчёта", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(255)), "Вид перерасчета" );
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("ДополнительныйЛист", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(255)), "Доп. лист" );
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("Книга", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(255)) );
	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)));
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"), "Ставка НДС");              
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("СуммаНДС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)), "Сумма НДС");
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("СуммаСНДС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2)), "В т.ч. НДС");
	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("ДокументРеализации", новый ОписаниеТипов("ДокументСсылка.РеализацияТоваровУслуг"), "Документ реализации");	
	НоваяКолонка = ТипизированныеДанные.Колонки.Добавить("КоличествоНайденныхДокументовРеализации", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,0)), "N найденных док.реализ.");	
	
КонецПроцедуры

Функция ПолучитьДатуИзСтроки(ДатаСтрокой)

	// Ожидаемый формат входа: ДД.ММ.ГГГГ (например, 25.12.2023)
	// Преобразуем в формат ГГГГММДД для функции Дата()
	
	Если ПустаяСтрока(ДатаСтрокой) Тогда
		Возврат Дата('00010101');
	КонецЕсли;
	
	Если ТипЗнч(ДатаСтрокой) = Тип("Дата") Тогда
		Возврат ДатаСтрокой;
	КонецЕсли;
	
	Попытка
		
		Если СтрДлина(ДатаСтрокой) = 10 Тогда
			Год   = Число(Прав(ДатаСтрокой, 4));
			Месяц = Число(Сред(ДатаСтрокой, 4, 2));
			День  = Число(Лев(ДатаСтрокой, 2));
		ИначеЕсли СтрДлина(ДатаСтрокой) = 8 Тогда
			Год   = Число(Прав(ДатаСтрокой, 2));
			Месяц = Число(Сред(ДатаСтрокой, 4, 2));
			День  = Число(Лев(ДатаСтрокой, 2));
		КонецЕсли;
		
		Возврат Дата(Год, Месяц, День);
		
	Исключение
		// В случае ошибки возвращаем пустую дату
		Возврат Дата('00010101');
	КонецПопытки;
	
КонецФункции

Функция ПолучитьСтавкиНДС()
	
	СтавкиНДС = Новый Соответствие;
	
	Для каждого перечисление Из Метаданные.Перечисления.СтавкиНДС.ЗначенияПеречисления Цикл
		СтавкиНДС.Вставить(Перечисление.Синоним, Перечисления.СтавкиНДС[перечисление.имя]);
	КонецЦикла;
	
	Возврат СтавкиНДС;
	
КонецФункции

#КонецОбласти

#Область ФорматированиеИОтображение 

Процедура ОтформатироватьТипизированныеДанные()

	Таблица = ЭлементыФормы.ТипизированныеДанные;
	
	// 1. Базовая настройка ширины колонок
	// Сначала устанавливаем ширину по умолчанию для всех
	Для каждого Колонка Из Таблица.Колонки Цикл
		Колонка.Ширина = 5;
	КонецЦикла;
	
	// 2. Корректировка ширины для ключевых колонок
	Таблица.Колонки.Номенклатура.Ширина = 12;
	Таблица.Колонки.НоменклатурнаяГруппа.Ширина = 12;
	Таблица.Колонки.СодержаниеНоменклатуры.Ширина = 12;
	Таблица.Колонки.СубъектРФ.Ширина = 5;          // Можно и не писать, т.к. и так 5, но оставим для наглядности
	Таблица.Колонки.НомерСтрокиXl.Ширина = 1;

	// 3. Настройка форматов дат
	Таблица.Колонки.ПериодРасчета.Формат = "ДФ=dd.MM.yyyy";
	Таблица.Колонки.ОтражаетсяВМесяце.Формат = "ДФ=dd.MM.yyyy";
	Таблица.Колонки.ОтражаетсяВКвартале.Формат = "ДФ=dd.MM.yyyy";

	// 4. Настройка выравнивания (горизонтальное положение)
	Таблица.Колонки.СтавкаНДС.ГоризонтальноеПоложениеВКолонке = ГоризонтальноеПоложение.Центр;
	Таблица.Колонки.КоличествоНайденныхДокументовРеализации.ГоризонтальноеПоложениеВКолонке = ГоризонтальноеПоложение.Центр;

	// 5. Настройка подвала таблицы (итоги)
	Таблица.Колонки.Сумма.ОтображатьВПодвале = Истина;
	Таблица.Колонки.Сумма.ТекстПодвала = ТипизированныеДанные.Итог("Сумма");
	Таблица.Колонки.Сумма.ГоризонтальноеПоложениеВПодвале = ГоризонтальноеПоложение.Право;
	
	Таблица.Колонки.СуммаНДС.ОтображатьВПодвале = Истина;
	Таблица.Колонки.СуммаНДС.ТекстПодвала = ТипизированныеДанные.Итог("СуммаНДС");
	Таблица.Колонки.СуммаНДС.ГоризонтальноеПоложениеВПодвале = ГоризонтальноеПоложение.Право;
	
	Таблица.Колонки.СуммаСНДС.ОтображатьВПодвале = Истина;
	Таблица.Колонки.СуммаСНДС.ТекстПодвала = ТипизированныеДанные.Итог("СуммаСНДС");
	Таблица.Колонки.СуммаСНДС.ГоризонтальноеПоложениеВПодвале = ГоризонтальноеПоложение.Право;
	
	
КонецПроцедуры

Процедура ОтформатироватьСгруппированныеДанные()

	Таблица = ЭлементыФормы.СгруппированныеДанные;
	
	// 1. Сброс настроек колонок к базовым значениям
	Таблица.ВысотаПодвала = 1; 
	
	Для каждого Колонка Из Таблица.Колонки Цикл
		Колонка.Ширина = 3;
		Колонка.ЭлементУправления.КнопкаОчистки = Ложь;
		Колонка.ОтображатьВПодвале = Ложь; 
		Колонка.ОтображатьИтогиВПодвале = Ложь; 
	КонецЦикла;
	
	// Скрыть служебные колонки
	Таблица.Колонки.Уровень.Видимость = Ложь;
	Таблица.Колонки.УИДСтроки.Видимость = Ложь;

	// 2. Настройка ширины колонок
	Таблица.Колонки.СубъектРФ.Ширина = 5;
	Таблица.Колонки.ДокументРеализации.Ширина = 8;  // Было 10, потом 8, оставим 8
	Таблица.Колонки.Номенклатура.Ширина = 8;
	Таблица.Колонки.Знак.Ширина = 1;
	Таблица.Колонки.Счет.Ширина = 1;

	// 3. Настройка заголовков колонок (ТекстШапки)
	Таблица.Колонки.Знак.ТекстШапки = "+/-";
	Таблица.Колонки.ДополнительныйЛист.ТекстШапки = "Доп. лист";
	Таблица.Колонки.ВидПерерасчёта.ТекстШапки = "Вид перерасчета";
	Таблица.Колонки.ДокументРеализации.ТекстШапки = "Документ реализации";
	Таблица.Колонки.НомерСтрокиXl.ТекстШапки = "Строка файла";
	Таблица.Колонки.СубъектРФ.ТекстШапки = "Субъект РФ";
	Таблица.Колонки.ДоговорКонтрагента.ТекстШапки = "Договор";
	Таблица.Колонки.НоменклатурнаяГруппа.ТекстШапки = "Номенклатурная группа";
	Таблица.Колонки.ПериодРасчета.ТекстШапки = "Период расчета";
	Таблица.Колонки.ОтражаетсяВМесяце.ТекстШапки = "Отражается в месяце";
	Таблица.Колонки.ОтражаетсяВКвартале.ТекстШапки = "Отражается в квартале";
	Таблица.Колонки.СтавкаНДС.ТекстШапки = "Ставка НДС";
	Таблица.Колонки.СуммаНДС.ТекстШапки = "Сумма НДС";
	Таблица.Колонки.СуммаСНДС.ТекстШапки = "В т.ч. НДС";
	Таблица.Колонки.СодержаниеНоменклатуры.ТекстШапки = "Содержание номенклатуры"; 
	Таблица.Колонки.КодНоменклатуры.ТекстШапки = "Код"; 

	// 4. Настройка форматов
	Таблица.Колонки.ПериодРасчета.Формат = "ДФ='МMМM yyyy'";
	Таблица.Колонки.ОтражаетсяВМесяце.Формат = "ДФ=dd.MM.yyyy";
	Таблица.Колонки.ОтражаетсяВКвартале.Формат = "ДФ='к ""кв."" гггг'";
	
	Таблица.Колонки.Цена.Формат = "ЧДЦ=2";
	Таблица.Колонки.Сумма.Формат = "ЧДЦ=2";
	Таблица.Колонки.СуммаНДС.Формат = "ЧДЦ=2";

	// 5. Настройка положения (ПоложениеКолонки) и выравнивания
	Таблица.Колонки.НомерСтрокиXl.ГоризонтальноеПоложениеВКолонке = ГоризонтальноеПоложение.Центр;
	Таблица.Колонки.СтавкаНДС.ГоризонтальноеПоложениеВКолонке = ГоризонтальноеПоложение.Центр;
	Таблица.Колонки.Знак.ГоризонтальноеПоложениеВКолонке = ГоризонтальноеПоложение.Центр;
	
	// Настройка многострочности
	Таблица.Колонки.Контрагент.Положение = ПоложениеКолонки.НаСледующейСтроке;
	Таблица.Колонки.ДоговорКонтрагента.Положение = ПоложениеКолонки.НаСледующейСтроке;
	Таблица.Колонки.ДополнительныйЛист.Положение = ПоложениеКолонки.НаСледующейСтроке;
	Таблица.Колонки.ВидПерерасчёта.Положение = ПоложениеКолонки.НаСледующейСтроке;
	Таблица.Колонки.Книга.Положение = ПоложениеКолонки.НаСледующейСтроке;
	Таблица.Колонки.НоменклатурнаяГруппа.Положение = ПоложениеКолонки.НаСледующейСтроке;
	Таблица.Колонки.ОтражаетсяВКвартале.Положение = ПоложениеКолонки.ВТойЖеКолонке;
	Таблица.Колонки.СодержаниеНоменклатуры.Положение = ПоложениеКолонки.НаСледующейСтроке;
	Таблица.Колонки.КодНоменклатуры.Положение = ПоложениеКолонки.ВТойЖеКолонке;
	
	Таблица.Колонки.Знак.Положение = ПоложениеКолонки.ВТойЖеКолонке;
	Таблица.Колонки.ДокументРеализации.Положение = ПоложениеКолонки.НоваяКолонка;
	Таблица.Колонки.ПериодРасчета.Положение = ПоложениеКолонки.НоваяКолонка;

	// 6. Включение кнопок открытия
	Таблица.Колонки.Контрагент.ЭлементУправления.КнопкаОткрытия = Истина;
	Таблица.Колонки.ДоговорКонтрагента.ЭлементУправления.КнопкаОткрытия = Истина;
	Таблица.Колонки.ДокументРеализации.ЭлементУправления.КнопкаОткрытия = Истина;

	// 7. Настройка подвала таблицы
	Таблица.Колонки.Сумма.ОтображатьВПодвале = Истина;
	Таблица.Колонки.Сумма.ТекстПодвала = СгруппированныеДанные.Строки.Итог("Сумма");
	Таблица.Колонки.Сумма.ГоризонтальноеПоложениеВПодвале = ГоризонтальноеПоложение.Право;

	Таблица.Колонки.СуммаНДС.ОтображатьВПодвале = Истина;
	Таблица.Колонки.СуммаНДС.ТекстПодвала = СгруппированныеДанные.Строки.Итог("СуммаНДС");
	Таблица.Колонки.СуммаНДС.ГоризонтальноеПоложениеВПодвале = ГоризонтальноеПоложение.Право;
	
	Таблица.Колонки.СуммаСНДС.ОтображатьВПодвале = Истина;
	Таблица.Колонки.СуммаСНДС.ТекстПодвала = СгруппированныеДанные.Строки.Итог("СуммаСНДС");
	Таблица.Колонки.СуммаСНДС.ГоризонтальноеПоложениеВПодвале = ГоризонтальноеПоложение.Право;
	
	Для каждого Колонка Из Таблица.Колонки Цикл
		Колонка.ОтображатьВПодвале = Ложь; 
	КонецЦикла;
	
	// 8. Переключение страницы панели
	ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы.СгруппированныеДанные;
	
КонецПроцедуры

Процедура ТипизированныеДанныеПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.КоличествоНайденныхДокументовРеализации > 1 Тогда
		ЦветФона = Новый Цвет(255, 255, 0);
		ОформлениеСтроки.Ячейки.КоличествоНайденныхДокументовРеализации.ЦветФона = ЦветФона;
	КонецЕсли;
	
КонецПроцедуры

Процедура СгруппированныеДанныеПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.Уровень = "Договор" Тогда
		
		ЦветФона = Новый Цвет(248, 242, 216);
		ОформлениеСтроки.ЦветФона = ЦветФона;
		ЖирныйШрифт = новый Шрифт("Arial", 8, Истина, Ложь, Ложь, Ложь); //имя,размер,полужирный,наклонный,подчеркивание,зачеркивание
		ОформлениеСтроки.Шрифт = ЖирныйШрифт ;
		
	КонецЕсли;                
	
	Если ДанныеСтроки.Уровень = "ВидПерерасчёта" Тогда
		
		ЦветФона = Новый Цвет(251, 249, 236);
		ОформлениеСтроки.ЦветФона = ЦветФона;
		
		ЖирныйШрифт = новый Шрифт("Arial", 8, Истина, Ложь, Ложь, Ложь); //имя,размер,полужирный,наклонный,подчеркивание,зачеркивание
		ОформлениеСтроки.Шрифт = ЖирныйШрифт ;
		
	КонецЕсли;    
	
	Если ДанныеСтроки.Уровень = "ПериодРасчета" Тогда
		
		ЦветФона = Новый Цвет(246, 246, 255);
		ОформлениеСтроки.ЦветФона = ЦветФона;
		
		ЖирныйШрифт = новый Шрифт("Arial", 8, Истина, Ложь, Ложь, Ложь); //имя,размер,полужирный,наклонный,подчеркивание,зачеркивание
		ОформлениеСтроки.Шрифт = ЖирныйШрифт ;
		
	КонецЕсли; 	
	
	Если ДанныеСтроки.Уровень = "ДокументРеализации" Тогда
		
		ЦветФона = Новый Цвет(251, 251, 255);
		ОформлениеСтроки.ЦветФона = ЦветФона;
		
		ЖирныйШрифт = новый Шрифт("Arial", 8, Истина, Ложь, Ложь, Ложь); //имя,размер,полужирный,наклонный,подчеркивание,зачеркивание
		ОформлениеСтроки.Шрифт = ЖирныйШрифт ;
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.ДокументРеализации) Тогда
			ЦветФона = Новый Цвет(255, 255, 0);
			ОформлениеСтроки.Ячейки.ДокументРеализации.ЦветФона = ЦветФона;
		КонецЕсли;
		
		Если Не ЭлементыФормы.СгруппированныеДанные.Колонки.Найти("КоличествоНайденныхКорректировок") = Неопределено  Тогда
			Если ДанныеСтроки.КоличествоНайденныхКорректировок <> 1 Тогда
				ЦветФона = Новый Цвет(255, 255, 0);
				ОформлениеСтроки.Ячейки.КоличествоНайденныхКорректировок.ЦветФона = ЦветФона;
			КонецЕсли;
		КонецЕсли;   
		
		Если Не ЭлементыФормы.СгруппированныеДанные.Колонки.Найти("ПакетнаяОбработка") = Неопределено  Тогда
			Если ДанныеСтроки.ПакетнаяОбработка Тогда
				ЦветФона = Новый Цвет(0, 255, 0);
				ОформлениеСтроки.Ячейки.ПакетнаяОбработка.ЦветФона = ЦветФона;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если 
		ДанныеСтроки.Уровень = "СтрокаФайла" и 
		Не ЭлементыФормы.СгруппированныеДанные.Колонки.Найти("СтрокаКорректируемогоДокумента") = Неопределено 
		и ЗначениеЗаполнено(ДанныеСтроки.Родитель.КорректируемыйДокумент)
	Тогда

		Если ДанныеСтроки.СтрокаКорректируемогоДокумента = 0 Тогда
				ЦветФона = Новый Цвет(240, 240, 0);
				ОформлениеСтроки.Ячейки.СтрокаКорректируемогоДокумента.ЦветФона = ЦветФона;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДанныеСтроки.Уровень = "СтрокаКорректируемогоДокумента" Тогда
		
		ЦветТекста = Новый Цвет(0, 0, 127);
		ОформлениеСтроки.ЦветТекста = ЦветТекста;

		ЦветФона = Новый Цвет(250, 250, 250);

		ОформлениеСтроки.ЦветФона = ЦветФона;
		//ЖирныйШрифт = новый Шрифт("Arial", 8, Истина, Ложь, Ложь, Ложь); //имя,размер,полужирный,наклонный,подчеркивание,зачеркивание
		//ОформлениеСтроки.Шрифт = ЖирныйШрифт ;  

	КонецЕсли; 	
	
	Если Не ЭлементыФормы.СгруппированныеДанные.Колонки.Найти("ПакетнаяОбработка") = Неопределено Тогда
		
		Если ДанныеСтроки.Уровень = "ДокументРеализации" Тогда
			ОформлениеСтроки.Ячейки.ПакетнаяОбработка.Видимость = Истина;	
		Иначе
			ОформлениеСтроки.Ячейки.ПакетнаяОбработка.Видимость = Ложь;			
			//Отображать = Ложь 
			//Доступность = Ложь	
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ГруппировкаДанных

Процедура КнопкаСгруппироватьЗагруженныеДанныеНажатие(Элемент)
	
	СгруппироватьЗагруженныеДанные();	
	
КонецПроцедуры

Процедура СгруппироватьЗагруженныеДанные()
	
	СгруппированныеДанные.Колонки.Очистить();
	СгруппироватьТипизированныеДанные();
	ДобавитьПоляКорректировок();
	
КонецПроцедуры 

Процедура СгруппироватьТипизированныеДанные ()
	
	Если ТипизированныеДанные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТЗ  = ТипизированныеДанные.Скопировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Т.НомерСтрокиXl,
	|	Т.СубъектРФ,
	|	Т.Контрагент,
	|	Т.ДоговорКонтрагента,
	|	Т.Номенклатура,
	|	Т.НоменклатурнаяГруппа,
	|	Т.СодержаниеНоменклатуры,
	|	Т.ПериодРасчета,
	|	Т.ОтражаетсяВМесяце,
	|	Т.ОтражаетсяВКвартале,
	|	Т.ВидПерерасчёта,
	|	Т.ДополнительныйЛист,
	|	Т.Книга,
	|	Т.Цена,
	|	Т.Количество,
	|	Т.Сумма,
	|	Т.СуммаСНДС,
	|	Т.СтавкаНДС,
	|	Т.СуммаНДС,
	|	Т.ДокументРеализации,
	|	Т.КоличествоНайденныхДокументовРеализации
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	&ТЗ КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.НомерСтрокиXl,
	|	ВТ.СубъектРФ,
	|	ВТ.Контрагент,
	|	ВТ.ДоговорКонтрагента,
	|	ВТ.Номенклатура,
	|	ВТ.НоменклатурнаяГруппа,
	|	ВТ.СодержаниеНоменклатуры,
	|	ВТ.ПериодРасчета,
	|	ВТ.ОтражаетсяВМесяце,
	|	ВТ.ОтражаетсяВКвартале,
	|	ВТ.ВидПерерасчёта,
	|	ВТ.ДополнительныйЛист,
	|	ВТ.Книга,
	|	ВТ.Цена,
	|	ВТ.Количество,
	|	ВТ.Сумма,
	|	ВТ.СтавкаНДС,
	|	ВТ.СуммаНДС,
	|	ВТ.СуммаСНДС,
	|	ВТ.ДокументРеализации,
	|	ВТ.КоличествоНайденныхДокументовРеализации,
	|	ВЫБОР
	|		КОГДА ВТ.Сумма < 0
	|			ТОГДА ""минус""
	|		ИНАЧЕ ""плюс""
	|	КОНЕЦ КАК Знак,
	|	ВЫБОР
	|		КОГДА ГОД(ВТ.ПериодРасчета) = &ТекущийГод
	|			ТОГДА ""90""
	|		ИНАЧЕ ""91""
	|	КОНЕЦ КАК СчетКорректировки
	|ПОМЕСТИТЬ ВТ1
	|ИЗ
	|	ВТ КАК ВТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ1.СубъектРФ КАК СубъектРФ,
	|	ВТ1.Контрагент КАК Контрагент,
	|	ВТ1.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ1.СчетКорректировки КАК Счет,
	|	ВТ1.Знак КАК Знак,
	|	ВТ1.ДополнительныйЛист КАК ДополнительныйЛист,
	|	ВТ1.ВидПерерасчёта КАК ВидПерерасчёта,
	|	ВТ1.ПериодРасчета КАК ПериодРасчета,
	|	ВТ1.ДокументРеализации КАК ДокументРеализации,
	|	ВТ1.ОтражаетсяВКвартале КАК ОтражаетсяВКвартале,
	|	ВТ1.НомерСтрокиXl,
	|	ВТ1.Номенклатура, ВТ1.Номенклатура.Код как КодНоменклатуры,
	|	ВТ1.НоменклатурнаяГруппа,
	|	ВТ1.СодержаниеНоменклатуры,
	|	ВТ1.ОтражаетсяВМесяце,
	|	ВТ1.Книга,
	|	ВТ1.Цена,
	|	ВТ1.Количество,
	|	ВТ1.Сумма КАК Сумма,
	|	ВТ1.СтавкаНДС,
	|	ВТ1.СуммаНДС КАК СуммаНДС,
	|	ВТ1.СуммаСНДС КАК СуммаСНДС,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(250)) КАК Уровень
	|ИЗ
	|	ВТ1 КАК ВТ1
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(СуммаНДС),
	|	СУММА(СуммаСНДС)
	|ПО
	|	СубъектРФ,
	|	Контрагент,
	|	ДоговорКонтрагента,
	|	Счет,
	|	Знак,
	|	ДополнительныйЛист,
	|	ВидПерерасчёта,
	|	ПериодРасчета,
	|	ОтражаетсяВКвартале,
	|	ДокументРеализации";
	
	
	Запрос.УстановитьПараметр("ТЗ", ТЗ);
	Запрос.УстановитьПараметр("ТекущийГод", Год(ДатаРегистрации));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДереваЗначений = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией).Скопировать();
	СтруктураДереваЗначений.Строки.Очистить();
	СгруппированныеДанные = СтруктураДереваЗначений.Скопировать(); 
	
	СгруппированныеДанные.Колонки.Добавить("УИДСтроки", Новый ОписаниеТипов("УникальныйИдентификатор"), "УИДСтроки"); 
	
	
	ВыборкаСубъектРФ = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СубъектРФ");
	Пока ВыборкаСубъектРФ.Следующий() Цикл 
		
		ВыборкаВыше = ВыборкаСубъектРФ;
		ВыборкаКонтрагент = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Контрагент");
		Пока ВыборкаКонтрагент.Следующий() Цикл 
			
			ВыборкаВыше = ВыборкаКонтрагент;
			ВыборкаДоговорКонтрагента = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ДоговорКонтрагента");
			Пока ВыборкаДоговорКонтрагента.Следующий() Цикл 
				
				НоваяГруппировкаДоговорКонтрагента = СгруппированныеДанные.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяГруппировкаДоговорКонтрагента, ВыборкаДоговорКонтрагента);
				НоваяГруппировкаДоговорКонтрагента.Уровень = "Договор";
				НоваяГруппировкаДоговорКонтрагента.УИДСтроки = Новый УникальныйИдентификатор;
				
				ВыборкаВыше = ВыборкаДоговорКонтрагента;
				ВыборкаСчетКорректировки = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Счет");
				Пока ВыборкаСчетКорректировки.Следующий() Цикл 
					
					ВыборкаВыше = ВыборкаСчетКорректировки;
					ВыборкаЗнак = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Знак");
					Пока ВыборкаЗнак.Следующий() Цикл
						
						ВыборкаВыше = ВыборкаЗнак;
						ВыборкаДополнительныйЛист = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ДополнительныйЛист");
						Пока ВыборкаДополнительныйЛист.Следующий() Цикл
							
							ВыборкаВыше = ВыборкаДополнительныйЛист;
							ВыборкаВидПерерасчёта = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ВидПерерасчёта");
							Пока ВыборкаВидПерерасчёта.Следующий() Цикл
								
								НоваяГруппировкаСчет = НоваяГруппировкаДоговорКонтрагента.Строки.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяГруппировкаСчет, ВыборкаВидПерерасчёта,, 
								"
								|	СубъектРФ,
								|	Контрагент,
								|	ДоговорКонтрагента
								|	"
								);
								НоваяГруппировкаСчет.Уровень = "ВидПерерасчёта";
								НоваяГруппировкаСчет.УИДСтроки = Новый УникальныйИдентификатор;

								ВыборкаВыше = ВыборкаВидПерерасчёта;
								ВыборкаПериодРасчета = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ПериодРасчета");
								Пока ВыборкаПериодРасчета.Следующий() Цикл 
								
										НоваяГруппировкаПериодРасчета = НоваяГруппировкаСчет.Строки.Добавить();
										ЗаполнитьЗначенияСвойств(НоваяГруппировкаПериодРасчета, ВыборкаПериодРасчета,, 
										"	Счет,
										|	Знак,
										|	ДополнительныйЛист,
										|	ВидПерерасчёта,
										|	СубъектРФ,
										|	Контрагент,
										|	ДоговорКонтрагента"
										);
										НоваяГруппировкаПериодРасчета.Уровень = "ПериодРасчета";
										НоваяГруппировкаПериодРасчета.УИДСтроки = Новый УникальныйИдентификатор;
										
									ВыборкаВыше = ВыборкаПериодРасчета;
									ВыборкаОтражаетсяВКвартале = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОтражаетсяВКвартале");
									Пока ВыборкаОтражаетсяВКвартале.Следующий() Цикл 
										
										ВыборкаВыше = ВыборкаОтражаетсяВКвартале;
										ВыборкаДокументРеализации = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ДокументРеализации");
										Пока ВыборкаДокументРеализации.Следующий() Цикл 
											
											НоваяГруппировкаДокумент = НоваяГруппировкаПериодРасчета.Строки.Добавить();
											ЗаполнитьЗначенияСвойств(НоваяГруппировкаДокумент, ВыборкаДокументРеализации,, 
											"	Счет,
											|	Знак,
											|	ДополнительныйЛист,
											|	ВидПерерасчёта,
											|	СубъектРФ,
											|	Контрагент,
											|	ДоговорКонтрагента, 
											|	ПериодРасчета"
											);
											НоваяГруппировкаДокумент.Уровень = "ДокументРеализации";
											НоваяГруппировкаДокумент.УИДСтроки = Новый УникальныйИдентификатор;
											
											ВыборкаВыше = ВыборкаДокументРеализации;
											ВыборкаДетальныеЗаписи = ВыборкаВыше.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, );
											Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
												
												НоваяСтрока = НоваяГруппировкаДокумент.Строки.Добавить();
												ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи,, 
												"	Счет,
												|	Знак,
												|	ДополнительныйЛист,
												|	ВидПерерасчёта, 
												|	СубъектРФ,
												|	Контрагент,
												|	ДоговорКонтрагента,
												|	ПериодРасчета,
												|	ДокументРеализации, 
												|	ОтражаетсяВКвартале"
												);  
												НоваяСтрока.Уровень = "СтрокаФайла";
												НоваяСтрока.УИДСтроки = Новый УникальныйИдентификатор;
												
											КонецЦикла;	
										КонецЦикла;							
									КонецЦикла; 
								КонецЦикла; 
							КонецЦикла;
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ЭлементыФормы.СгруппированныеДанные.СоздатьКолонки();  
	
	ОтформатироватьСгруппированныеДанные();
	
КонецПроцедуры

Процедура ДобавитьПоляКорректировок()
	
	Таблица = ЭлементыФормы.СгруппированныеДанные;
	
	// 1. Проверка, что колонки еще не добавлены (проверяем реквизиты, а не элемент формы)
	Если СгруппированныеДанные.Колонки.Найти("КорректируемыйДокумент") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// 2. Добавление колонок в реквизит таблицы (источник данных)
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	МассивТипов.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
	ОписаниеТиповКорректируемогоДокумента = Новый ОписаниеТипов(МассивТипов);
	
	СгруппированныеДанные.Колонки.Добавить("КорректируемыйДокумент", 			ОписаниеТиповКорректируемогоДокумента, "Корректируемый документ");
	СгруппированныеДанные.Колонки.Добавить("КоличествоНайденныхКорректировок", 	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,0)), "Количество найденных корректировок");
	СгруппированныеДанные.Колонки.Добавить("НайденныеКорректировки", 			Новый ОписаниеТипов("СписокЗначений"), "Найденные корректировки");
	СгруппированныеДанные.Колонки.Добавить("КорректирующийДокумент", 			Новый ОписаниеТипов("ДокументСсылка.КорректировкаРеализации"), "Корректирующий документ");
	СгруппированныеДанные.Колонки.Добавить("СтрокаКорректируемогоДокумента",	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,0)), "Строка корректируемого документа");
	СгруппированныеДанные.Колонки.Добавить("НайденныеСтрокиКорректируемогоДокумента",	Новый ОписаниеТипов("СписокЗначений"), "НайденныеСтрокиКорректируемогоДокумента");
	СгруппированныеДанные.Колонки.Добавить("ПакетнаяОбработка",					Новый ОписаниеТипов("Булево"), "Пакетная обработка");
	
	// 3. Добавление и настройка колонок в элементе формы (отображение)
	ИндексБазовойКолонки = Таблица.Колонки.Индекс(Таблица.Колонки["ДокументРеализации"]);
	
	// Колонка "Корректируемый документ"
	НоваяКолонка = Таблица.Колонки.Вставить(ИндексБазовойКолонки + 2, "Корректируемый документ");
	НоваяКолонка.Данные = "КорректируемыйДокумент";
	НоваяКолонка.Имя = "КорректируемыйДокумент";
	НоваяКолонка.Ширина = 8;
	НоваяКолонка.Положение = ПоложениеКолонки.НаСледующейСтроке;
	НоваяКолонка.ОтображатьВПодвале = Ложь;
	НоваяКолонка.ЭлементУправления.КнопкаСпискаВыбора = Истина;
	НоваяКолонка.ЭлементУправления.КнопкаОткрытия = Истина;
	НоваяКолонка.ЭлементУправления.КнопкаОчистки = истина;
	НоваяКолонка.ЭлементУправления.УстановитьДействие("ПриИзменении", Новый Действие("КорректируемыйДокументПриИзменении"));
	
	// Колонка "Количество найденных корректировок"
	НоваяКолонка = Таблица.Колонки.Вставить(ИндексБазовойКолонки + 3, "Найдено");
	НоваяКолонка.Данные = "КоличествоНайденныхКорректировок";
	НоваяКолонка.Имя = "КоличествоНайденныхКорректировок";
	НоваяКолонка.Ширина = 1;
	НоваяКолонка.Положение = ПоложениеКолонки.ВТойЖеКолонке;
	НоваяКолонка.ОтображатьВПодвале = Ложь;
	НоваяКолонка.ГоризонтальноеПоложениеВКолонке = ГоризонтальноеПоложение.Лево;
	НоваяКолонка.ЭлементУправления.КнопкаОчистки = Ложь;
	
	// Колонка "Список корректировок" (скрытая)
	НоваяКолонка = Таблица.Колонки.Вставить(ИндексБазовойКолонки + 4, "Список");
	НоваяКолонка.Данные = "НайденныеКорректировки";
	НоваяКолонка.Имя = "НайденныеКорректировки";
	НоваяКолонка.Ширина = 10;
	НоваяКолонка.Положение = ПоложениеКолонки.НаСледующейСтроке;
	НоваяКолонка.ОтображатьВПодвале = Ложь;
	НоваяКолонка.Видимость = Ложь;
	НоваяКолонка.ЭлементУправления.КнопкаОчистки = Ложь;
	
	// Колонка "Корректирующий документ"
	НоваяКолонка = Таблица.Колонки.Вставить(ИндексБазовойКолонки + 5, "Корректирующий документ");
	НоваяКолонка.Данные = "КорректирующийДокумент";
	НоваяКолонка.Имя = "КорректирующийДокумент";
	НоваяКолонка.Ширина = 8;
	НоваяКолонка.Положение = ПоложениеКолонки.НаСледующейСтроке;
	НоваяКолонка.ОтображатьВПодвале = Ложь;
	НоваяКолонка.ЭлементУправления.КнопкаСпискаВыбора = Истина;
	НоваяКолонка.ЭлементУправления.КнопкаОткрытия = Истина;
	НоваяКолонка.ЭлементУправления.КнопкаОчистки = Истина;
	
	
	// Колонка "СтрокаКорректируемогоДокумента"
	НоваяКолонка = Таблица.Колонки.Вставить(ИндексБазовойКолонки + 7, "Строка корректируемого документа");
	НоваяКолонка.Данные = "СтрокаКорректируемогоДокумента";
	НоваяКолонка.Имя = "СтрокаКорректируемогоДокумента";
	НоваяКолонка.Ширина = 4;
	НоваяКолонка.Положение = ПоложениеКолонки.НоваяКолонка;
	НоваяКолонка.ОтображатьВПодвале = Ложь;
	НоваяКолонка.ЭлементУправления.КнопкаСпискаВыбора = Истина;
	НоваяКолонка.ЭлементУправления.КнопкаВыбора = Истина;
	НоваяКолонка.ГоризонтальноеПоложениеВКолонке = ГоризонтальноеПоложение.Центр;
	//НоваяКолонка.ЭлементУправления.КнопкаОткрытия = Истина;
	НоваяКолонка.ЭлементУправления.КнопкаОчистки = Истина;
	НоваяКолонка.ЭлементУправления.УстановитьДействие("НачалоВыбора", Новый Действие("СтрокаКорректируемогоДокументаНачалоВыбора"));
	НоваяКолонка.ЭлементУправления.УстановитьДействие("ПриИзменении", Новый Действие("СтрокаКорректируемогоДокументаПриИзменении"));
	НоваяКолонка.ЭлементУправления.КартинкаКнопкиВыбора = ЭлементыФормы.ПолеКартинки1.Картинка;
	
	// Колонка "НайденныеСтрокиКорректируемогоДокумента"
	НоваяКолонка = Таблица.Колонки.Добавить("НайденныеСтрокиКорректируемогоДокумента");
	НоваяКолонка.Данные = "НайденныеСтрокиКорректируемогоДокумента";
	НоваяКолонка.Имя = "НайденныеСтрокиКорректируемогоДокумента";
	НоваяКолонка.Ширина = 1;
	НоваяКолонка.Видимость = Ложь; 
	
	// Колонка "Пакетная обработка"
	НоваяКолонка = Таблица.Колонки.Добавить("ПакетнаяОбработка", "Пакетная обработка"); 
	НоваяКолонка.УстановитьЭлементУправления(Тип("Флажок"));
	НоваяКолонка.РежимРедактирования    = РежимРедактированияКолонки.Непосредственно; 
	НоваяКолонка.ДанныеФлажка = "ПакетнаяОбработка";
	//НоваяКолонка.Имя = "ПакетнаяОбработка";
	НоваяКолонка.Ширина = 1;
	//НоваяКолонка.Видимость = Ложь;
	НоваяКолонка.ГоризонтальноеПоложениеВКолонке = ГоризонтальноеПоложение.Центр;
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиИКонстанты

Процедура ЗаполнитьАналитикуРегионов()
	
	//СПб
	НоваяСтрока = АналитикаРегионов.Добавить();
	НоваяСтрока.СубъектРФ = Справочники._СубъектыРФ.НайтиПоКоду("000000001");  
	НоваяСтрока.Контрагент = Справочники.Контрагенты.НайтиПоКоду("000002613"); 
	НоваяСтрока.Договор = Справочники.ДоговорыКонтрагентов.НайтиПоКоду("000009093");
	НоваяСтрока.Подразделение = Справочники.Подразделения.НайтиПоКоду("000000151");
	НоваяСтрока.ПодразделениеОрганизации = Справочники.ПодразделенияОрганизаций.НайтиПоКоду("000001478");
	НоваяСтрока.Проект = Справочники.Проекты.НайтиПоКоду("000002265");
	
	//ЛО
	НоваяСтрока = АналитикаРегионов.Добавить();
	НоваяСтрока.СубъектРФ = Справочники._СубъектыРФ.НайтиПоКоду("000000003"); 
	НоваяСтрока.Контрагент = Справочники.Контрагенты.НайтиПоКоду("000012712");       
	НоваяСтрока.Договор = Справочники.ДоговорыКонтрагентов.НайтиПоКоду("000026709"); 
	НоваяСтрока.Подразделение = Справочники.Подразделения.НайтиПоКоду("000000153");
	НоваяСтрока.ПодразделениеОрганизации = Справочники.ПодразделенияОрганизаций.НайтиПоКоду("000001333");
	НоваяСтрока.Проект = Справочники.Проекты.НайтиПоКоду("000002266");
	
	//РК
	НоваяСтрока = АналитикаРегионов.Добавить();
	НоваяСтрока.СубъектРФ = Справочники._СубъектыРФ.НайтиПоКоду("000000002"); 
	НоваяСтрока.Контрагент = Справочники.Контрагенты.НайтиПоКоду("000002612"); 
	НоваяСтрока.Договор = Справочники.ДоговорыКонтрагентов.НайтиПоКоду("000009094"); 
	НоваяСтрока.Подразделение = Справочники.Подразделения.НайтиПоКоду("000000152");
	НоваяСтрока.ПодразделениеОрганизации = Справочники.ПодразделенияОрганизаций.НайтиПоКоду("000001354");
	НоваяСтрока.Проект = Справочники.Проекты.НайтиПоКоду("000002264");
	
	//ИскатьПоСодержаниюНоменклатуры = Истина; // Реквизит больше не используется
	Допуск = 0.01;
	ПроводитьДокументы = Истина;
	СоздаватьСчетаФактуры = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область Печать

Процедура ВывестиДеревоЗначенийВТабличныйДокумент(СтрокаДерева, УровеньВложенности = 0, КоллекцияКолонок, ТабличныйДокумент, ОбластьСтроки)
	
	Для Каждого ТекущаяСтрока Из СтрокаДерева.Строки Цикл
		ИндексКолонки = 0;
		ПрефиксОтступа = "";
		Для Счетчик = 1 По УровеньВложенности Цикл   
			ПрефиксОтступа = ПрефиксОтступа + " ";
		КонецЦикла;
		Для Каждого ОписаниеКолонки Из КоллекцияКолонок Цикл
			ИндексКолонки = ИндексКолонки + 1;
			Если ИндексКолонки = 1 Тогда
				ЗначениеЯчейки = ПрефиксОтступа + ТекущаяСтрока[ОписаниеКолонки.Имя];
			Иначе
				ЗначениеЯчейки = ТекущаяСтрока[ОписаниеКолонки.Имя];
			КонецЕсли;
			ОбластьСтроки.Область(1, ИндексКолонки).Текст = ЗначениеЯчейки;
		КонецЦикла;
		ТабличныйДокумент.Вывести(ОбластьСтроки, УровеньВложенности + 1);
		ВывестиДеревоЗначенийВТабличныйДокумент(ТекущаяСтрока, УровеньВложенности + 1, КоллекцияКолонок, ТабличныйДокумент, ОбластьСтроки);
	КонецЦикла;
	
КонецПроцедуры

Процедура СгруппированныеДанныеВТабличныйДокумент()      
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ОбластьЗаголовка = ТабличныйДокумент.ПолучитьОбласть("R1");
	
	ИндексКолонки = 0;
	Для Каждого ОписаниеКолонки Из СгруппированныеДанные.Колонки Цикл
		ИндексКолонки = ИндексКолонки + 1;
		ОбластьЗаголовка.Область(1, ИндексКолонки).Текст = ОписаниеКолонки.Имя;
	КонецЦикла;
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовка);     
	
	ОбластьСтрокиДанных = ТабличныйДокумент.ПолучитьОбласть("R2");
	
	ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
	ВывестиДеревоЗначенийВТабличныйДокумент(СгруппированныеДанные, 0, СгруппированныеДанные.Колонки, ТабличныйДокумент, ОбластьСтрокиДанных);
	ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
	
	ТабличныйДокумент.Показать();
	
	// ВременныйКаталог = КаталогВременныхФайлов();
	// ИмяФайла = "Журнал выгрузки " + Формат(ТекущаяДата(), "ДФ = 'гггг-ММ-дд_чч-мм-сс'") + ".xls";
	// ПолноеИмяФайла = ВременныйКаталог + ИмяФайла;                                                                  
	// ТабличныйДокумент.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.XLS);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКорректировками

Функция НайтиПоследнююКорректировку(ИсходныйДокумент)
	
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("КорректируемыйДокумент", Неопределено);
	СтруктураРезультат.Вставить("КоличествоНайденныхКорректировок", 0);
	СтруктураРезультат.Вставить("СписокНайденныхКорректировок", Новый СписокЗначений);
	
	// Запрос для поиска всех прямых потомков текущего документа
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КорректировкаРеализации.Ссылка,
	|	КорректировкаРеализации.Дата КАК ДатаДокумента
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|ГДЕ
	|	КорректировкаРеализации.ИсправляемыйДокументРеализации = &РодительскийДокумент
	|	И КорректировкаРеализации.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаРеализации.Дата,
	|	КорректировкаРеализации.Номер";
	
	ОбойтиВетки(ИсходныйДокумент, СтруктураРезультат, Запрос);
	
	// Находим самую позднюю корректировку среди всех концов веток
	Если СтруктураРезультат.СписокНайденныхКорректировок.Количество() > 0 Тогда
		
		// Сортируем список по дате (от поздних к ранним)
		СтруктураРезультат.СписокНайденныхКорректировок.СортироватьПоПредставлению(НаправлениеСортировки.Убыв);
		
		// Самая поздняя - первая в отсортированном списке
		СтруктураРезультат.КорректируемыйДокумент = СтруктураРезультат.СписокНайденныхКорректировок[0].Значение;
		
		// Восстанавливаем человеческие представления
		Для Каждого ЭлементСписка Из СтруктураРезультат.СписокНайденныхКорректировок Цикл
			ЭлементСписка.Представление = Строка(ЭлементСписка.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СтруктураРезультат;
	
КонецФункции

Процедура ОбойтиВетки(ТекущийДокумент, СтруктураРезультат, Запрос)
	
	Запрос.УстановитьПараметр("РодительскийДокумент", ТекущийДокумент);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	СчетчикПотомков = 0;
	
	Пока Выборка.Следующий() Цикл
		
		СчетчикПотомков = СчетчикПотомков + 1;
		ДочернийДокумент = Выборка.Ссылка;
		
		// Рекурсивно обходим потомков этого документа
		ОбойтиВетки(ДочернийДокумент, СтруктураРезультат, Запрос);
		
	КонецЦикла;
	
	// Если у текущего документа нет потомков, значит это конец ветки
	Если СчетчикПотомков = 0 Тогда
		СтруктураРезультат.СписокНайденныхКорректировок.Добавить(ТекущийДокумент, Формат(ТекущийДокумент.Дата, "ДФ=ггггММддЧЧммсс"));
		СтруктураРезультат.КоличествоНайденныхКорректировок = СтруктураРезультат.КоличествоНайденныхКорректировок + 1;
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанель1СоздатьКорректировки(Кнопка)
	
	Если СгруппированныеДанные.Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаРегион Из СгруппированныеДанные.Строки Цикл
		Для каждого СтрокаВариация Из СтрокаРегион.Строки Цикл
			Для каждого СтрокаДокумент Из СтрокаВариация.Строки Цикл
				
				СтруктураКорректируемыйДокумент 				= НайтиПоследнююКорректировку(СтрокаДокумент.ДокументРеализации);				
				
				СтрокаДокумент.КорректируемыйДокумент 			= СтруктураКорректируемыйДокумент.КорректируемыйДокумент;
				СтрокаДокумент.КоличествоНайденныхКорректировок = СтруктураКорректируемыйДокумент.КоличествоНайденныхКорректировок; 
				СтрокаДокумент.НайденныеКорректировки 			= СтруктураКорректируемыйДокумент.СписокНайденныхКорректировок;  
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;	
	
КонецПроцедуры

Процедура СгруппированныеДанныеПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если НЕ ЭлементыФормы.СгруппированныеДанные.Колонки.Найти("КоличествоНайденныхКорректировок") = Неопределено Тогда
		ЭлементыФормы.СгруппированныеДанные.Колонки.КорректируемыйДокумент.ЭлементУправления.СписокВыбора.очистить();
		Для каждого НайденнаяКорректировка Из ТекущиеДанные.НайденныеКорректировки Цикл
			ЭлементыФормы.СгруппированныеДанные.Колонки.КорректируемыйДокумент.ЭлементУправления.СписокВыбора.добавить(НайденнаяКорректировка.Значение);	
		КонецЦикла;
	КонецЕсли;        
	
	Если НЕ ЭлементыФормы.СгруппированныеДанные.Колонки.Найти("НайденныеСтрокиКорректируемогоДокумента") = Неопределено и ТекущиеДанные.Уровень = "СтрокаФайла" Тогда
		ЭлементыФормы.СгруппированныеДанные.Колонки.СтрокаКорректируемогоДокумента.ЭлементУправления.СписокВыбора.очистить();
		Для каждого НайденнаяСтрока Из ТекущиеДанные.НайденныеСтрокиКорректируемогоДокумента Цикл
			ЭлементыФормы.СгруппированныеДанные.Колонки.СтрокаКорректируемогоДокумента.ЭлементУправления.СписокВыбора.добавить(НайденнаяСтрока.Значение);	
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанель2НайтиПоследнююКорректировкуДляСтроки(Кнопка)
	
	ТекущиеДанные = ЭлементыФормы.СгруппированныеДанные.ТекущиеДанные;
	НайтиПоследнююКорректировкуДляСтроки(ТекущиеДанные); 
	
КонецПроцедуры

//Процедура НайтиПоследнююКорректировкуДляСтроки(ТекущиеДанные, ФокусНаСтроку = Истина)
//	
//	СтрокаДляОбработки = ВернутьСтрокуДокументРеализации(ТекущиеДанные);

//	Если НЕ СтрокаДляОбработки = Неопределено Тогда
//		СтруктураКорректируемыйДокумент 						= НайтиПоследнююКорректировку(СтрокаДляОбработки.ДокументРеализации);				
//		СтрокаДляОбработки.КорректируемыйДокумент 				= СтруктураКорректируемыйДокумент.КорректируемыйДокумент;
//		СтрокаДляОбработки.КоличествоНайденныхКорректировок 	= СтруктураКорректируемыйДокумент.КоличествоНайденныхКорректировок; 
//		СтрокаДляОбработки.НайденныеКорректировки 				= СтруктураКорректируемыйДокумент.СписокНайденныхКорректировок;  
//	КонецЕсли;

//	ПодобратьСтрокиКорректируемогоДокумента(СтрокаДляОбработки, ФокусНаСтроку);
//	
//КонецПроцедуры  

Процедура НайтиПоследнююКорректировкуДляСтроки(ТекущиеДанные, ФокусНаСтроку = Истина)
	
	СтрокаДляОбработки = ВернутьСтрокуДокументРеализации(ТекущиеДанные);

	Если НЕ СтрокаДляОбработки = Неопределено Тогда
		
		// Находим последнюю корректировку
		СтруктураКорректируемыйДокумент = НайтиПоследнююКорректировку(СтрокаДляОбработки.ДокументРеализации);				
		СтрокаДляОбработки.КорректируемыйДокумент = СтруктураКорректируемыйДокумент.КорректируемыйДокумент;
		СтрокаДляОбработки.КоличествоНайденныхКорректировок = СтруктураКорректируемыйДокумент.КоличествоНайденныхКорректировок; 
		СтрокаДляОбработки.НайденныеКорректировки = СтруктураКорректируемыйДокумент.СписокНайденныхКорректировок;  
		
		// Подбираем строки
		ПодобратьСтрокиКорректируемогоДокумента(СтрокаДляОбработки, ФокусНаСтроку);
		
	
	КонецЕсли;
	
КонецПроцедуры

Функция ВернутьСтрокуДокументРеализации(ТекущиеДанные)
	
	СтрокаДляОбработки = Неопределено;
	
	Если ТекущиеДанные.Уровень = "СтрокаФайла" Тогда
		СтрокаДляОбработки = ТекущиеДанные.Родитель;	
	ИначеЕсли ТекущиеДанные.Уровень = "ДокументРеализации" Тогда   
		СтрокаДляОбработки = ТекущиеДанные;
	ИначеЕсли ТекущиеДанные.Уровень = "СтрокаКорректируемогоДокумента" Тогда   
		СтрокаДляОбработки = ТекущиеДанные.Родитель.Родитель;
	КонецЕсли;	
	
	Возврат СтрокаДляОбработки;

КонецФункции // ()

Процедура ПодобратьСтрокиКорректируемогоДокумента(СтрокаДокумент, ФокусНаСтроку = Истина)

	Для каждого СтрокаФайла Из СтрокаДокумент.Строки Цикл
	
		ПараметрыПоиска = Новый Структура;
		ПараметрыПоиска.Вставить("КорректируемыйДокумент",	СтрокаДокумент.КорректируемыйДокумент	);
		ПараметрыПоиска.Вставить("ДокументРеализации",		СтрокаДокумент.ДокументРеализации		);
		ПараметрыПоиска.Вставить("Номенклатура",			СтрокаФайла.Номенклатура					);
		ПараметрыПоиска.Вставить("СодержаниеНоменклатуры",	СтрокаФайла.СодержаниеНоменклатуры			);
		ПараметрыПоиска.Вставить("Цена",					СтрокаФайла.Цена							);
		
		НайденныеСтроки = НайтиСтрокиКорректируемогоДокумента(ПараметрыПоиска);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаФайла.СтрокаКорректируемогоДокумента = НайденныеСтроки[0].НомерСтроки;
		Иначе
			СтрокаФайла.СтрокаКорректируемогоДокумента = 0;
		КонецЕсли;
		
		СтрокаФайла.Строки.Очистить();
		
		СтрокаФайла.НайденныеСтрокиКорректируемогоДокумента = Неопределено;
				
		Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			СтрокаКорректируемогоДокумента = СтрокаФайла.Строки.Добавить();	
			ЗаполнитьУровеньСтрокиКорректируемогоДокумента(СтрокаКорректируемогоДокумента, НайденнаяСтрока);
            СтрокаФайла.НайденныеСтрокиКорректируемогоДокумента.Добавить(НайденнаяСтрока.НомерСтроки);
		КонецЦикла;	
		
	КонецЦикла;	
	
		// Проверяем успешность подбора
		Если СтрокиКорректируемогоДокументаПодобраны(СтрокаДокумент) Тогда
			// Все строки файла получили свои строки корректируемого документа - успех!
			СтрокаДокумент.ПакетнаяОбработка = Ложь;
			
			// Информируем пользователя об успехе
			Сообщить(СтрШаблон("Для документа %1 успешно подобраны все строки", 
				СтрокаДокумент.ДокументРеализации));
		Иначе
			// Не все строки подобраны - флаг оставляем для повторной обработки
			// ПакетнаяОбработка остается как была (Истина или Ложь)
			Если СтрокаДокумент.ПакетнаяОбработка = Истина  Тогда
				Сообщить(СтрШаблон("ВНИМАНИЕ: Для документа %1 не все строки подобраны. " + 
					"Документ останется в очереди на пакетную обработку.", 
					СтрокаДокумент.ДокументРеализации));
			Иначе
				Сообщить(СтрШаблон("ВНИМАНИЕ: Для документа %1 не все строки подобраны. " + 
					"", 
					СтрокаДокумент.ДокументРеализации));
			КонецЕсли;	
			// Предупреждаем пользователя о проблеме
		КонецЕсли;
		
	
	
	
	Если ФокусНаСтроку Тогда
		РазвернутьВетку(ЭлементыФормы.СгруппированныеДанные, СтрокаДокумент);
		ЭлементыФормы.СгруппированныеДанные.ТекущаяСтрока = СтрокаДокумент;
		ЭлементыФормы.СгруппированныеДанные.ТекущаяКолонка = ЭлементыФормы.СгруппированныеДанные.Колонки.КорректируемыйДокумент;
	КонецЕсли;
	
КонецПроцедуры

Процедура КорректируемыйДокументПриИзменении(Элемент)

	ТекущиеДанные = ЭлементыФормы.СгруппированныеДанные.ТекущиеДанные;
	ПодобратьСтрокиКорректируемогоДокумента(ТекущиеДанные);

КонецПроцедуры

Функция НайтиСтрокиКорректируемогоДокумента(ПараметрыПоиска)
	
	Результат = Неопределено;	
	
	Если ПараметрыПоиска.ДокументРеализации.Дата < Дата(2026, 1, 1) Тогда
		
		//КорректируемыйДокумент.Дата < Дата(2026, 1, 1)
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрСведений_СоответствиеАналитикРеализацииДляКорректировкиНДС_ЭС.ТРНоменклатура как Номенклатура,
		|	РегистрСведений_СоответствиеАналитикРеализацииДляКорректировкиНДС_ЭС.ТРСодержаниеНоменклатуры как Содержание
		|Поместить Мэппинг
		|ИЗ
		|	РегистрСведений._СоответствиеАналитикРеализацииДляКорректировкиНДС_ЭС КАК РегистрСведений_СоответствиеАналитикРеализацииДляКорректировкиНДС_ЭС
		|ГДЕ
		|	РегистрСведений_СоответствиеАналитикРеализацииДляКорректировкиНДС_ЭС.Номенклатура = &Номенклатура
		|	И РегистрСведений_СоответствиеАналитикРеализацииДляКорректировкиНДС_ЭС.СодержаниеНоменклатуры = &СодержаниеНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТЧ.Количество,
		|	ТЧ.Номенклатура,
		|	ТЧ.СубконтоБУ,
		|	ТЧ.НомерСтроки,
		|	ТЧ.Содержание,
		|	ТЧ.СтавкаНДС,
		|	ТЧ.Сумма,
		|	ТЧ.СуммаНДС,
		|	ТЧ.Цена
		|ИЗ
		|	Документ.КорректировкаРеализации.Услуги КАК ТЧ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Мэппинг КАК Мэппинг
		|		ПО (Мэппинг.Номенклатура = ТЧ.Номенклатура)
		|			И (Мэппинг.Содержание = (ВЫРАЗИТЬ(ТЧ.Содержание КАК СТРОКА(250))))
		|ГДЕ
		|	ТЧ.Ссылка = &КорректируемыйДокумент
		|	И ТЧ.Цена МЕЖДУ &ЦенаОт И &ЦенаДо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТЧ.Количество,
		|	ТЧ.Номенклатура,
		|	ТЧ.СубконтоБУ,
		|	ТЧ.НомерСтроки,
		|	ТЧ.Содержание,
		|	ТЧ.СтавкаНДС,
		|	ТЧ.Сумма,
		|	ТЧ.СуммаНДС,
		|	ТЧ.Цена
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Услуги КАК ТЧ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Мэппинг КАК Мэппинг
		|		ПО (Мэппинг.Номенклатура = ТЧ.Номенклатура)
		|			И (Мэппинг.Содержание = (ВЫРАЗИТЬ(ТЧ.Содержание КАК СТРОКА(250))))
		|ГДЕ
		|	ТЧ.Ссылка = &КорректируемыйДокумент
		|	И ТЧ.Цена МЕЖДУ &ЦенаОт И &ЦенаДо";
		
		Запрос.УстановитьПараметр("Номенклатура", 			ПараметрыПоиска.Номенклатура						);
		Запрос.УстановитьПараметр("СодержаниеНоменклатуры", СокрЛП(ПараметрыПоиска.СодержаниеНоменклатуры)		);
		Запрос.УстановитьПараметр("КорректируемыйДокумент", ПараметрыПоиска.КорректируемыйДокумент				);
		Запрос.УстановитьПараметр("ЦенаОт", 				ПараметрыПоиска.Цена - Допуск						);
		Запрос.УстановитьПараметр("ЦенаДо", 				ПараметрыПоиска.Цена + Допуск						);
		
		РезультатЗапроса = Запрос.Выполнить();
		Результат = РезультатЗапроса.Выгрузить();
		
	Иначе  
		
		//КорректируемыйДокумент.Дата >= Дата(2026, 1, 1) 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЧ.Количество,
		|	ТЧ.Номенклатура,
		|	ТЧ.СубконтоБУ,
		|	ТЧ.НомерСтроки,
		|	ТЧ.Содержание,
		|	ТЧ.СтавкаНДС,
		|	ТЧ.Сумма,
		|	ТЧ.СуммаНДС,
		|	ТЧ.Цена
		|ИЗ
		|	Документ.КорректировкаРеализации.Услуги КАК ТЧ
		|ГДЕ
		|	ТЧ.Ссылка = &КорректируемыйДокумент
		//|	И ТЧ.Цена МЕЖДУ &ЦенаОт И &ЦенаДо
		|	И ТЧ.Номенклатура = &Номенклатура
		|	И Выразить(ТЧ.Содержание как Строка(250)) = &Содержание
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТЧ.Количество,
		|	ТЧ.Номенклатура,
		|	ТЧ.СубконтоБУ,
		|	ТЧ.НомерСтроки,
		|	ТЧ.Содержание,
		|	ТЧ.СтавкаНДС,
		|	ТЧ.Сумма,
		|	ТЧ.СуммаНДС,
		|	ТЧ.Цена
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Услуги КАК ТЧ
		|ГДЕ
		|	ТЧ.Ссылка = &КорректируемыйДокумент
		//|	И ТЧ.Цена МЕЖДУ &ЦенаОт И &ЦенаДо
		|	И ТЧ.Номенклатура = &Номенклатура
		|	И Выразить(ТЧ.Содержание как Строка(250)) = &Содержание
		|";
		
		Запрос.УстановитьПараметр("Номенклатура", 			ПараметрыПоиска.Номенклатура						);
		Запрос.УстановитьПараметр("Содержание", 			СокрЛП(ПараметрыПоиска.СодержаниеНоменклатуры)		);
		Запрос.УстановитьПараметр("КорректируемыйДокумент", ПараметрыПоиска.КорректируемыйДокумент				);
		//Запрос.УстановитьПараметр("ЦенаОт", 				ПараметрыПоиска.Цена - Допуск						);
		//Запрос.УстановитьПараметр("ЦенаДо", 				ПараметрыПоиска.Цена + Допуск						);
		
		РезультатЗапроса = Запрос.Выполнить();
		Результат = РезультатЗапроса.Выгрузить();  
		
	КонецЕсли;
	
	Возврат Результат;	

КонецФункции

Процедура Перенести(Кнопка)
	
	ТекущиеДанные = ЭлементыФормы.СгруппированныеДанные.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено ИЛИ Не ТекущиеДанные.Уровень = "СтрокаФайла" Тогда
		Возврат;
	КонецЕсли;

	ДругаяФорма = ЭтотОбъект.ПолучитьФорму("ПереносСтроки"); 
	ДругаяФорма.ВладелецФормы = ЭтаФорма;
	ДругаяФорма.Открыть();
	
КонецПроцедуры 

Процедура ПересчитатьИтогиВверхПоИерархии(СтрокаДокумента) Экспорт
	
	Если СтрокаДокумента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Поднимаемся от документа до самого верха
	ТекущаяСтрока = СтрокаДокумента;
	
	Пока ТекущаяСтрока <> Неопределено Цикл
		
		// Пересчитываем ТЕКУЩУЮ группу на основе всех ее детей
		СуммаГруппы = 0;
		СуммаНДСГруппы = 0; 
		СуммаСНДСГруппы = 0;
		
		Для Каждого ДочерняяСтрока Из ТекущаяСтрока.Строки Цикл
			СуммаГруппы = СуммаГруппы + ДочерняяСтрока.Сумма;
			СуммаНДСГруппы = СуммаНДСГруппы + ДочерняяСтрока.СуммаНДС;
			СуммаСНДСГруппы = СуммаСНДСГруппы + ДочерняяСтрока.СуммаСНДС;
		КонецЦикла;
		
		ТекущаяСтрока.Сумма = СуммаГруппы;
		ТекущаяСтрока.СуммаНДС = СуммаНДСГруппы;
		ТекущаяСтрока.СуммаСНДС = СуммаСНДСГруппы;
		
		// Поднимаемся выше
		ТекущаяСтрока = ТекущаяСтрока.Родитель;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СтрокаКорректируемогоДокументаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = ЭлементыФормы.СгруппированныеДанные.ТекущиеДанные;	
	
	Если ТекущиеДанные.Уровень = "СтрокаФайла" Тогда
		СтандартнаяОбработка = Ложь;	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	КорректировкаРеализацииУслуги.НомерСтроки,
			|	КорректировкаРеализацииУслуги.Номенклатура,
			|	КорректировкаРеализацииУслуги.Содержание,
			|	КорректировкаРеализацииУслуги.Цена,
			|	КорректировкаРеализацииУслуги.Количество,
			|	КорректировкаРеализацииУслуги.Сумма,
			|	КорректировкаРеализацииУслуги.СтавкаНДС,
			|	КорректировкаРеализацииУслуги.СуммаНДС,
			|	КорректировкаРеализацииУслуги.СубконтоБУ
			|ИЗ
			|	Документ.КорректировкаРеализации.Услуги КАК КорректировкаРеализацииУслуги
			|ГДЕ
			|	КорректировкаРеализацииУслуги.Ссылка = &Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Документ.НомерСтроки,
			|	Документ.Номенклатура,
			|	Документ.Содержание,
			|	Документ.Цена,
			|	Документ.Количество,
			|	Документ.Сумма,
			|	Документ.СтавкаНДС,
			|	Документ.СуммаНДС,
			|	Документ.СубконтоБУ
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Услуги КАК Документ
			|ГДЕ
			|	Документ.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", 		ТекущиеДанные.Родитель.КорректируемыйДокумент);
		РезультатЗапроса = Запрос.Выполнить();
		Результат = РезультатЗапроса.Выгрузить();
		
		РезультатВыбора = Результат.ВыбратьСтроку();
		
		Если РезультатВыбора <> Неопределено Тогда  
			
			ТекущиеДанные.СтрокаКорректируемогоДокумента = РезультатВыбора.НомерСтроки;
			
			//ТекущиеДанные.Строки.Очистить();  
			УдалитьСтрокиНеИзСпискаНайденных(ТекущиеДанные);
			
			СписокНомеров = Новый СписокЗначений;
			Для каждого Строка Из ТекущиеДанные.Строки Цикл
				СписокНомеров.Добавить(Строка.СтрокаКорректируемогоДокумента);
			КонецЦикла;
			
			РезультатПоиска = СписокНомеров.НайтиПоЗначению(РезультатВыбора.НомерСтроки);
			Если РезультатПоиска = Неопределено Тогда
				НоваяСтрока = ТекущиеДанные.Строки.Добавить();
				ЗаполнитьУровеньСтрокиКорректируемогоДокумента(НоваяСтрока, РезультатВыбора);				
				РазвернутьВетку(ЭлементыФормы.СгруппированныеДанные, ТекущиеДанные);
			КонецЕсли; 
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура СтрокаКорректируемогоДокументаПриИзменении (Элемент)
	
	ТекущиеДанные = ЭлементыФормы.СгруппированныеДанные.ТекущиеДанные; 
	
	Если НЕ ТекущиеДанные.Уровень = "СтрокаФайла" Тогда
		Возврат;
	КонецЕсли;	
	
	УдалитьСтрокиНеИзСпискаНайденных(ТекущиеДанные); 
	
	Если ТекущиеДанные.СтрокаКорректируемогоДокумента = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КорректировкаРеализацииУслуги.НомерСтроки,
		|	КорректировкаРеализацииУслуги.Номенклатура,
		|	КорректировкаРеализацииУслуги.Содержание,
		|	КорректировкаРеализацииУслуги.Цена,
		|	КорректировкаРеализацииУслуги.Количество,
		|	КорректировкаРеализацииУслуги.Сумма,
		|	КорректировкаРеализацииУслуги.СтавкаНДС,
		|	КорректировкаРеализацииУслуги.СуммаНДС,
		|	КорректировкаРеализацииУслуги.СубконтоБУ
		|ИЗ
		|	Документ.КорректировкаРеализации.Услуги КАК КорректировкаРеализацииУслуги
		|ГДЕ
		|	КорректировкаРеализацииУслуги.Ссылка = &Ссылка и НомерСтроки = &НомерСтроки 
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Документ.НомерСтроки,
		|	Документ.Номенклатура,
		|	Документ.Содержание,
		|	Документ.Цена,
		|	Документ.Количество,
		|	Документ.Сумма,
		|	Документ.СтавкаНДС,
		|	Документ.СуммаНДС,
		|	Документ.СубконтоБУ
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Услуги КАК Документ
		|ГДЕ
		|	Документ.Ссылка = &Ссылка и НомерСтроки = &НомерСтроки ";
	
	Запрос.УстановитьПараметр("Ссылка", 		ТекущиеДанные.Родитель.КорректируемыйДокумент); 
	Запрос.УстановитьПараметр("НомерСтроки", 	ТекущиеДанные.СтрокаКорректируемогоДокумента);

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить("Строка с таким номером отсутствует в документе");
		ТекущиеДанные.СтрокаКорректируемогоДокумента = 0;
		Возврат;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		РезультатВыбора = Выборка;		
		ТекущиеДанные.СтрокаКорректируемогоДокумента = РезультатВыбора.НомерСтроки;
		СписокНомеров = Новый СписокЗначений;
		Для каждого Строка Из ТекущиеДанные.Строки Цикл
			СписокНомеров.Добавить(Строка.СтрокаКорректируемогоДокумента);
		КонецЦикла;
		РезультатПоиска = СписокНомеров.НайтиПоЗначению(РезультатВыбора.НомерСтроки);
		Если РезультатПоиска = Неопределено Тогда
			НоваяСтрока = ТекущиеДанные.Строки.Добавить();
			ЗаполнитьУровеньСтрокиКорректируемогоДокумента(НоваяСтрока, РезультатВыбора);
			РазвернутьВетку(ЭлементыФормы.СгруппированныеДанные, ТекущиеДанные);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры 

Процедура ЗаполнитьУровеньСтрокиКорректируемогоДокумента(СтрокаКорректируемогоДокументаПриемник, СтрокаКорректируемогоДокументаИсточник)

    СтрокаКорректируемогоДокументаПриемник.Количество 						= СтрокаКорректируемогоДокументаИсточник.Количество;
	СтрокаКорректируемогоДокументаПриемник.Номенклатура 					= СтрокаКорректируемогоДокументаИсточник.Номенклатура;
	СтрокаКорректируемогоДокументаПриемник.КодНоменклатуры 					= СтрокаКорректируемогоДокументаИсточник.Номенклатура.Код;
	СтрокаКорректируемогоДокументаПриемник.НоменклатурнаяГруппа 			= СтрокаКорректируемогоДокументаИсточник.СубконтоБУ;
	СтрокаКорректируемогоДокументаПриемник.СтрокаКорректируемогоДокумента	= СтрокаКорректируемогоДокументаИсточник.НомерСтроки;
	СтрокаКорректируемогоДокументаПриемник.СодержаниеНоменклатуры 			= СтрокаКорректируемогоДокументаИсточник.Содержание;
	СтрокаКорректируемогоДокументаПриемник.СтавкаНДС 						= СтрокаКорректируемогоДокументаИсточник.СтавкаНДС;
	СтрокаКорректируемогоДокументаПриемник.Сумма 							= СтрокаКорректируемогоДокументаИсточник.Сумма;
	СтрокаКорректируемогоДокументаПриемник.СуммаНДС 						= СтрокаКорректируемогоДокументаИсточник.СуммаНДС;
	СтрокаКорректируемогоДокументаПриемник.СуммаСНДС 						= СтрокаКорректируемогоДокументаИсточник.Сумма + СтрокаКорректируемогоДокументаИсточник.СуммаНДС;
	СтрокаКорректируемогоДокументаПриемник.Уровень							= "СтрокаКорректируемогоДокумента";
	СтрокаКорректируемогоДокументаПриемник.Цена 							= СтрокаКорректируемогоДокументаИсточник.Цена;

КонецПроцедуры

Процедура УдалитьСтрокиНеИзСпискаНайденных(ТекущиеДанные)

	СписокНомеров = Новый СписокЗначений;

	Для каждого Элемент Из ЭлементыФормы.СгруппированныеДанные.Колонки.СтрокаКорректируемогоДокумента.ЭлементУправления.СписокВыбора Цикл
		СписокНомеров.Добавить(Элемент.Значение);
	КонецЦикла;     
	
	РезультатПоиска = Неопределено;
	МассивСтрокДляУдаления = Новый Массив;
	
	Для каждого Строка Из ТекущиеДанные.Строки Цикл
		РезультатПоиска = СписокНомеров.НайтиПоЗначению(Строка.СтрокаКорректируемогоДокумента);
		Если РезультатПоиска =  Неопределено  Тогда
			МассивСтрокДляУдаления.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;		
	
	Для каждого СтрокаКУдалению Из МассивСтрокДляУдаления Цикл
	    ТекущиеДанные.Строки.удалить(СтрокаКУдалению);
	КонецЦикла;
	

КонецПроцедуры

Процедура СгруппированныеДанныеПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.СгруппированныеДанные.ТекущиеДанные;	
	
	Если ТекущиеДанные.Уровень = "СтрокаФайла" или ТекущиеДанные.Уровень = "ДокументРеализации" или ТекущиеДанные.Уровень = "СтрокаКорректируемогоДокумента" Тогда
		ЭлементыФормы.КонтекстноеМенюСгруппированныеДанные.Кнопки.НайтиПоследнююКорректировкуДляСтроки.Доступность = Истина;
		ЭлементыФормы.КонтекстноеМенюСгруппированныеДанные.Кнопки.СоздатьКорректировочныйДокумент.Доступность = Истина;
		ЭлементыФормы.КонтекстноеМенюСгруппированныеДанные.Кнопки.СоздатьКорректировочныйДокументБезСФ.Доступность = Истина;
	Иначе
		ЭлементыФормы.КонтекстноеМенюСгруппированныеДанные.Кнопки.НайтиПоследнююКорректировкуДляСтроки.Доступность = Ложь;		
		ЭлементыФормы.КонтекстноеМенюСгруппированныеДанные.Кнопки.СоздатьКорректировочныйДокумент.Доступность = Ложь;		
		ЭлементыФормы.КонтекстноеМенюСгруппированныеДанные.Кнопки.СоздатьКорректировочныйДокументБезСФ.Доступность = Ложь;		
	КонецЕсли;

	Если ТекущиеДанные.Уровень = "СтрокаФайла" Тогда
		ЭлементыФормы.КонтекстноеМенюСгруппированныеДанные.Кнопки.Перенести.Доступность = Истина;
	Иначе
		ЭлементыФормы.КонтекстноеМенюСгруппированныеДанные.Кнопки.Перенести.Доступность = Ложь;		
	КонецЕсли;
	
	Если ТекущиеДанные.Уровень = "Договор" 
		или 
		ТекущиеДанные.Уровень = "ВидПерерасчёта"
		или
		ТекущиеДанные.Уровень = "ПериодРасчета"
		или
		ТекущиеДанные.Уровень = "СтрокаКорректируемогоДокумента"
	Тогда
		Для каждого Колонка Из ЭлементыФормы.СгруппированныеДанные.Колонки Цикл
			Колонка.ТолькоПросмотр = Истина;
		КонецЦикла;
	ИначеЕсли ТекущиеДанные.Уровень = "ДокументРеализации" Тогда
		Для каждого Колонка Из ЭлементыФормы.СгруппированныеДанные.Колонки Цикл
			Если 
				Колонка.Имя = "ДокументРеализации" 
				или 
				Колонка.Имя = "КорректирующийДокумент" 
				или 
				Колонка.Имя = "КорректируемыйДокумент" 
				//или 
				//Колонка.Имя = "ПакетнаяОбработка" 
			Тогда
				Колонка.ТолькоПросмотр = Ложь;
			Иначе
				Колонка.ТолькоПросмотр = Истина;
			КонецЕсли;
		КонецЦикла; 
	ИначеЕсли ТекущиеДанные.Уровень = "СтрокаФайла" Тогда
		Для каждого Колонка Из ЭлементыФормы.СгруппированныеДанные.Колонки Цикл
			Если Колонка.Имя = "СтрокаКорректируемогоДокумента" Тогда
				Колонка.ТолькоПросмотр = Ложь;
			Иначе
				Колонка.ТолькоПросмотр = Истина;
			КонецЕсли;
		КонецЦикла; 
	Иначе
		Для каждого Колонка Из ЭлементыФормы.СгруппированныеДанные.Колонки Цикл
			Колонка.ТолькоПросмотр = Ложь;
		КонецЦикла;
	КонецЕсли;                

	ЭлементыФормы.СгруппированныеДанные.Колонки.ПакетнаяОбработка.ТолькоПросмотр=Ложь;	
	
КонецПроцедуры

Процедура КоманднаяПанель2НайтиПоследнююКорректировкуДляВыбранныхСтрок(Кнопка)

	СписокСтрокДляОбработки = ПолучитьСписокСтрокСОтметкойПакетнойОбработки();

	Если СписокСтрокДляОбработки.Количество() = 0  Тогда
		Предупреждение("Не выбрано ни одного документа");
		Возврат;
	КонецЕсли;
	
	Для каждого Строка Из СписокСтрокДляОбработки  Цикл
		//Строка.ПакетнаяОбработка = Ложь;
		НайтиПоследнююКорректировкуДляСтроки(Строка, Ложь);
	КонецЦикла;
	
	Сообщить("Поиск завершен");
	
КонецПроцедуры

Функция ПолучитьСписокСтрокСОтметкойПакетнойОбработки()
	
	Результат = Новый Массив;
	
    Для Каждого Договор Из СгруппированныеДанные.Строки Цикл
		Для Каждого ВидПерерасчета Из Договор.Строки Цикл
			Для Каждого Период Из ВидПерерасчета.Строки Цикл
				Для Каждого Документ Из Период.Строки Цикл
					Если Документ.ПакетнаяОбработка Тогда
						Результат.Добавить(Документ);
					КонецЕсли;
					//Для Каждого СтрокаФайла Из Документ.Строки Цикл
					//	Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					//	КонецЦикла;
					//КонецЦикла; // Строки файла
                КонецЦикла; // Документы
            КонецЦикла; // Периоды
        КонецЦикла; // Виды перерасчета
    КонецЦикла; // Договоры

	Возврат Результат;	
	
КонецФункции // ()

Процедура КоманднаяПанель2ВыбратьВсеГлобально(Кнопка)
    // Отметить все документы во всём дереве
    ОтметитьДокументыРекурсивно(СгруппированныеДанные, Истина);
КонецПроцедуры

Процедура КоманднаяПанель2СнятьВсеГлобально(Кнопка)
    // Снять все документы во всём дереве
    ОтметитьДокументыРекурсивно(СгруппированныеДанные, Ложь);
КонецПроцедуры

Процедура КоманднаяПанель2ВыбратьВВетке(Кнопка)
    // Отметить документы в текущей ветке
    ТекущаяСтрока = ЭлементыФормы.СгруппированныеДанные.ТекущиеДанные;
    
    Если ТекущаяСтрока = Неопределено Тогда
        Предупреждение("Нет текущей строки. Выберите регион, вид, период или документ.");
        Возврат;
    КонецЕсли;
    
    ОтметитьДокументыРекурсивно(ТекущаяСтрока, Истина);
КонецПроцедуры

Процедура КоманднаяПанель2СнятьВВетке(Кнопка)
    // Снять документы в текущей ветке
    ТекущаяСтрока = ЭлементыФормы.СгруппированныеДанные.ТекущиеДанные;
    
    Если ТекущаяСтрока = Неопределено Тогда
        Предупреждение("Нет текущей строки. Выберите регион, вид, период или документ.");
        Возврат;
    КонецЕсли;
    
    ОтметитьДокументыРекурсивно(ТекущаяСтрока, Ложь);
КонецПроцедуры

Процедура ОтметитьДокументыРекурсивно(Ветка, УстановитьФлажок)
    
    Если ТипЗнч(Ветка) = Тип("ДеревоЗначений") Тогда
        // Корень дерева
        Для Каждого Строка Из Ветка.Строки Цикл
            ОтметитьДокументыРекурсивно(Строка, УстановитьФлажок);
        КонецЦикла;
        Возврат;
    КонецЕсли;
    
    // Это строка дерева
    Если Ветка.Уровень = "ДокументРеализации" Тогда
        Ветка.ПакетнаяОбработка = УстановитьФлажок;
        Возврат;
    КонецЕсли;
    
    Для Каждого ПодчиненнаяСтрока Из Ветка.Строки Цикл
        ОтметитьДокументыРекурсивно(ПодчиненнаяСтрока, УстановитьФлажок);
    КонецЦикла;
    
КонецПроцедуры

#КонецОбласти

#Область СозданиеКорректирующегоДокумента

Процедура КомандаКнопкиСоздатьКорректировочныйДокумент(Кнопка)

	КомандаСоздатьКорректировочныйДокумент();

КонецПроцедуры

Функция ПолучитьСтрокиИерархии(СтрокаДокументРеализации)
	
	Результат = Новый Структура;
	
	// Поднимаемся по иерархии
	Результат.Вставить("СтрокаДокумент", СтрокаДокументРеализации);
	Результат.Вставить("СтрокаПериод",   Результат.СтрокаДокумент.Родитель);
	Результат.Вставить("СтрокаВид",      Результат.СтрокаПериод.Родитель);
	Результат.Вставить("СтрокаДоговор",  Результат.СтрокаВид.Родитель);
	
	// Для удобства — сразу заполняем основные реквизиты
	Результат.Вставить("СубъектРФ",               Результат.СтрокаДоговор.СубъектРФ);
	Результат.Вставить("ВидПерерасчета",           Результат.СтрокаВид.ВидПерерасчёта);
	Результат.Вставить("ДопЛист",                  Результат.СтрокаВид.ДополнительныйЛист);
	Результат.Вставить("Знак",                      Результат.СтрокаВид.Знак);
	Результат.Вставить("Книга",                     Результат.СтрокаВид.Книга);
	Результат.Вставить("Счет",                      Результат.СтрокаВид.Счет);
	Результат.Вставить("ПериодРасчета",             Результат.СтрокаПериод.ПериодРасчета);
	Результат.Вставить("ОтражаетсяВКвартале",       Результат.СтрокаДокумент.ОтражаетсяВКвартале);
	Результат.Вставить("ДокументРеализацииСсылка",  Результат.СтрокаДокумент.ДокументРеализации);
	Результат.Вставить("СтрокиФайла",               Результат.СтрокаДокумент.Строки);
	
	// Форматированные значения для сообщений
	Результат.Вставить("ПериодРасчетаФорм", Формат(Результат.ПериодРасчета, "ДФ='ММММ гггг'"));
	Результат.Вставить("КварталФорм",        Формат(Результат.ОтражаетсяВКвартале, "ДФ='к""кв.""гггг'"));
	Результат.Вставить("СтрокиФайлаТекст",   ПолучитьСписокСтрокФайлаСтрокой(Результат.СтрокаДокумент));
	
	Возврат Результат;
	
КонецФункции

Процедура КомандаСоздатьКорректировочныйДокумент(СтрокаУровняДокументаДляОбработки = Неопределено, ДокументУспешноСоздан = Ложь, БезСчетаФактуры = Ложь, РежимВызоваОдиночный = Истина) 
	
	ПС = Символы.ПС;
	
	Если СтрокаУровняДокументаДляОбработки = Неопределено Тогда
		СтрокаУровняДокументаДляОбработки = ЭлементыФормы.СгруппированныеДанные.ТекущиеДанные;
	КонецЕсли;
	
	СтрокаДокументРеализации = ВернутьСтрокуДокументРеализации(СтрокаУровняДокументаДляОбработки);
	
	Строки = ПолучитьСтрокиИерархии(СтрокаДокументРеализации);
	
	Если ЗначениеЗаполнено(Строки.СтрокаДокумент.КорректирующийДокумент) Тогда
		Сообщить(СтрШаблон("Строки файла:%2 - Уже имеется корректирующий документ: %1, пропускаю...", 
			Строки.СтрокаДокумент.КорректирующийДокумент, 
			Строки.СтрокиФайлаТекст));
		Возврат;
	КонецЕсли; 
	
	Если Строки.СтрокиФайла.Количество() = 0 Тогда
		Сообщить(СтрШаблон("Для документа реализации %1 отсутствуют строки файла, пропускаю...", 
			Строки.ДокументРеализацииСсылка));
		Возврат;
	КонецЕсли; 
	
	РезультатПроверки = КорректируемыйДокументПоследнийВЦепочке(Строки.СтрокаДокумент);
	
	Если РезультатПроверки.ЯвляетсяПоследним и СтрокиКорректируемогоДокументаПодобраны(Строки.СтрокаДокумент) Тогда
		
		СозданиеКорректирующегоДокумента = СоздатьКорректировочныйДокумент(Строки.СтрокаДокумент, БезСчетаФактуры);
		
		Если СозданиеКорректирующегоДокумента.ВсеХорошо Тогда
			Строки.СтрокаДокумент.КорректирующийДокумент = СозданиеКорректирующегоДокумента.КорректирующийДокумент;
			ДокументУспешноСоздан = Истина; 
			Строки.СтрокаДокумент.ПакетнаяОбработка = Ложь;
		КонецЕсли;
		
	Иначе 
		
		Если Не ЗначениеЗаполнено(Строки.СтрокаДокумент.КорректируемыйДокумент) Тогда
			Сообщить(СтрШаблон("Остановка: для строк %1 не выбран корректируемый документ", 
				Строки.СтрокиФайлаТекст));
		ИначеЕсли Не РезультатПроверки.ЯвляетсяПоследним Тогда
			Сообщить(СтрШаблон("Остановка: документ %1 не последний в цепочке (строки %2)", 
				Строка(Строки.СтрокаДокумент.КорректируемыйДокумент),
				Строки.СтрокиФайлаТекст));
		ИначеЕсли НЕ СтрокиКорректируемогоДокументаПодобраны(Строки.СтрокаДокумент) Тогда
			Сообщить(СтрШаблон("Остановка: не подобраны строки корректируемого документа (строки файла %1)", 
				Строки.СтрокиФайлаТекст));
		Иначе
			Сообщить(СтрШаблон("Остановка: проблема при создании корректировки (строки %1)", 
				Строки.СтрокиФайлаТекст));
		КонецЕсли;
		
		КонтекстДокумента = 
			"Регион: " + Строки.СубъектРФ + ПС +
			"Период: " + Строки.ПериодРасчетаФорм + ПС +
			"Доп.лист: " + Строки.ДопЛист + ПС +
			"Отражается в квартале: " + Строки.КварталФорм + ПС +
			"Знак: " + Строки.Знак + ПС +
			"Книга: " + Строки.Книга + ПС +
			"Строки файла: " + Строки.СтрокиФайлаТекст;
		
		Если Не ЗначениеЗаполнено(Строки.СтрокаДокумент.КорректируемыйДокумент) Тогда
			ТекстПричины = "Корректируемый документ не выбран.";
		ИначеЕсли Не РезультатПроверки.ЯвляетсяПоследним Тогда
			ТекстПричины = "Корректируемый документ " + Строка(Строки.СтрокаДокумент.КорректируемыйДокумент) + " не является последним в цепочке.";
		ИначеЕсли НЕ СтрокиКорректируемогоДокументаПодобраны(Строки.СтрокаДокумент) Тогда
			ТекстПричины = "Строки корректируемого документа не подобраны.";
		Иначе
			ТекстПричины = "Обнаружена проблема при создании корректировки.";
		КонецЕсли;
		
		//ТекстВопроса = 
		//    "" + ТекстПричины + ПС + ПС +
		//	//"Создание корректировочного документа:" + ПС + ПС +
		//	КонтекстДокумента + ПС + ПС +
		//	"Выполнить автоматическое обновление (подобрать актуальный документ и строки) и продолжить создание?" + ПС +
		//	"(Да — обновить и продолжить, Нет — отменить создание)";
		//	
		//	
		//	
		//Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		//	Возврат;
		//КонецЕсли;
		
		ТекстВопроса = 
		    "" + ТекстПричины + ПС + ПС +
		    КонтекстДокумента + ПС + ПС +
		    "Выполнить автоматическое обновление (подобрать актуальный документ и строки) и продолжить создание?" + ПС +
		    "(Да — обновить и продолжить, Нет — пропустить текущий документ";

		// Определяем режим диалога
		РежимДиалога = РежимДиалогаВопрос.ДаНет;
		Если НЕ РежимВызоваОдиночный Тогда
		    РежимДиалога = РежимДиалогаВопрос.ДаНетОтмена;
		    ТекстВопроса = ТекстВопроса + ", Отмена — прервать пакетную обработку";
		КонецЕсли; 
		
		ТекстВопроса = ТекстВопроса + ")";

		РезультатДиалога = Вопрос(ТекстВопроса, РежимДиалога);

		Если РезультатДиалога = КодВозвратаДиалога.Нет Тогда
		    // Просто пропускаем текущий документ
		    Возврат;
		ИначеЕсли РезультатДиалога = КодВозвратаДиалога.Отмена Тогда
		    // Прерываем пакетную обработку
		    Сообщить("Пакетная обработка остановлена пользователем.");
		    ВызватьИсключение "ПакетнаяОбработкаПрервана";
		КонецЕсли;
		// Если Да — просто идем дальше		
		
		СтруктураКорректируемыйДокумент = РезультатПроверки.РезультатПоиска;
		Строки.СтрокаДокумент.КорректируемыйДокумент = СтруктураКорректируемыйДокумент.КорректируемыйДокумент;
		Строки.СтрокаДокумент.КоличествоНайденныхКорректировок = СтруктураКорректируемыйДокумент.КоличествоНайденныхКорректировок; 
		Строки.СтрокаДокумент.НайденныеКорректировки = СтруктураКорректируемыйДокумент.СписокНайденныхКорректировок;  

		ПодобратьСтрокиКорректируемогоДокумента(Строки.СтрокаДокумент, Истина);
		
		// === ПОВТОРНАЯ ПОПЫТКА СОЗДАНИЯ ===
		Если ЗначениеЗаполнено(Строки.СтрокаДокумент.КорректируемыйДокумент) и СтрокиКорректируемогоДокументаПодобраны(Строки.СтрокаДокумент) Тогда
			СозданиеКорректирующегоДокумента = СоздатьКорректировочныйДокумент(Строки.СтрокаДокумент, БезСчетаФактуры);
			Если СозданиеКорректирующегоДокумента.ВсеХорошо Тогда
				Строки.СтрокаДокумент.КорректирующийДокумент = СозданиеКорректирующегоДокумента.КорректирующийДокумент; 
				ДокументУспешноСоздан = Истина; 
				Строки.СтрокаДокумент.ПакетнаяОбработка = Ложь;
			КонецЕсли;
		Иначе
			Если Не ЗначениеЗаполнено(Строки.СтрокаДокумент.КорректируемыйДокумент) Тогда
				Сообщить(СтрШаблон("Строки файла:%1 - Корректируемый документ не подобран. ПРОПУСКАЮ", Строки.СтрокиФайлаТекст));
			ИначеЕсли ЗначениеЗаполнено(Строки.СтрокаДокумент.КорректируемыйДокумент) и НЕ СтрокиКорректируемогоДокументаПодобраны(Строки.СтрокаДокумент) Тогда
				Сообщить(СтрШаблон("Строки файла:%1 - Строки корректируемого документа не подобраны. ПРОПУСКАЮ", Строки.СтрокиФайлаТекст));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СтрокиКорректируемогоДокументаПодобраны(СтрокаДокументРеализации)

    Результат = Истина;
	Для каждого Строка Из СтрокаДокументРеализации.Строки Цикл
		Если Не ЗначениеЗаполнено(Строка.СтрокаКорректируемогоДокумента) Тогда
			Результат = Ложь;
		КонецЕсли;
	КонецЦикла;	      
	Возврат Результат;

КонецФункции // ()

Функция СоздатьКорректировочныйДокумент(СтрокаДокументРеализации, БезСчетаФактуры = Ложь)

	СтрокиИерархии = ПолучитьСтрокиИерархии(СтрокаДокументРеализации);
	
	СтруктруаРезультат = Новый Структура;
	СтруктруаРезультат.Вставить("ВсеХорошо", 				Ложь);
	СтруктруаРезультат.Вставить("КорректирующийДокумент", 	Документы.КорректировкаРеализации.ПустаяСсылка());
	
	ВсеХорошо = Истина;
	
	НачатьТранзакцию();
	
	НовыйДокументКорректировка = Документы.КорректировкаРеализации.СоздатьДокумент(); 
	
	НовыйДокументКорректировка.ДокументРеализации = СтрокиИерархии.СтрокаДокумент.КорректируемыйДокумент;
	НовыйДокументКорректировка.ЗаполнитьСвойстваШапки();
	НовыйДокументКорректировка.ЗаполнитьПоДокументу();
	
	НовыйДокументКорректировка._ВидПерерасчета      = СтрокиИерархии.ВидПерерасчета;
	НовыйДокументКорректировка._ДопЛист             = СтрокиИерархии.ДопЛист;
	НовыйДокументКорректировка._Знак                = СтрокиИерархии.Знак;
	НовыйДокументКорректировка._ОтражаетсяВКвартале = СтрокиИерархии.ОтражаетсяВКвартале;
	НовыйДокументКорректировка._ПериодКорректировки = СтрокиИерархии.ПериодРасчета;
	
	МассивКниг = СвернутьМассив(СтрокиИерархии.СтрокиФайла.ВыгрузитьКолонку("Книга"));
	Если МассивКниг.Количество() = 1  Тогда
		НовыйДокументКорректировка._Книга = МассивКниг[0]; 
	КонецЕсли;
		
	НовыйДокументКорректировка.Дата = НачалоДня(ДатаРегистрации);
	НовыйДокументКорректировка.ПризнаватьЗачитыватьАванс = Истина;
	
	ТЧ = НовыйДокументКорректировка.Услуги;
	
	Для каждого СтрокаФайла Из СтрокиИерархии.СтрокиФайла Цикл
		
		СтрокаДляКорректировки = ТЧ.Найти(СтрокаФайла.СтрокаКорректируемогоДокумента, "НомерСтроки");
		Если СтрокаДляКорректировки = Неопределено Тогда
		    Сообщить(СтрШаблон("Не найдена строка табличной части с номером %1", СтрокаФайла.СтрокаКорректируемогоДокумента));
		    Продолжить;
		КонецЕсли;		
		
		СтрокаДляКорректировки.Сумма 		= СтрокаДляКорректировки.Сумма 		+ СтрокаФайла.Сумма;
		СтрокаДляКорректировки.СуммаНДС 	= СтрокаДляКорректировки.СуммаНДС 	+ СтрокаФайла.СуммаНДС;
		СтрокаДляКорректировки.Количество 	= СтрокаДляКорректировки.Количество + СтрокаФайла.Количество;
		
		Если СтрокиИерархии.СтрокаДокумент.ДокументРеализации.Дата >= Дата(2026,1,1) Тогда
			Если СтрокаДляКорректировки.Количество <> 0 Тогда
				СтрокаДляКорректировки.Цена = СтрокаДляКорректировки.Сумма / СтрокаДляКорректировки.Количество;
			Иначе
				СтрокаДляКорректировки.Цена = 0;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла; 
	
	// СПОСОБ ОТРАЖЕНИЯ (Счет 90 - 1, Счет 91 - 2) 
	Если СтрокиИерархии.Счет = "90" Тогда
		НовыйДокументКорректировка._СпособОтражения = 1;	
	ИначеЕсли СтрокиИерархии.Счет = "91" Тогда
		НовыйДокументКорректировка._СпособОтражения = 2;	
	КонецЕсли;
	
	НовыйДокументКорректировка.ОтчетностьПодписана = Истина;
	
	// СТАТЬЯ ПРОЧИХ ДОХОДОВ И РАСХОДОВ 
	СтатьяПрочихДоходовИРасходов = Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка();
	Если 
		СтрокиИерархии.ДопЛист = "доп.лист" 
		и 
		СтрокиИерархии.Счет = "90" 
		Тогда
		  	СтатьяПрочихДоходовИРасходов = Справочники.ПрочиеДоходыИРасходы.НайтиПоКоду("000000069");   
	ИначеЕсли 
		СтрокиИерархии.ДопЛист = "доп.лист" 
		и 
		СтрокиИерархии.Счет = "91" 
		Тогда
			СтатьяПрочихДоходовИРасходов = Справочники.ПрочиеДоходыИРасходы.НайтиПоКоду("000000058");
	ИначеЕсли 
		СтрокиИерархии.ДопЛист = "текущий период" 
		Тогда
			СтатьяПрочихДоходовИРасходов = Справочники.ПрочиеДоходыИРасходы.НайтиПоКоду("000000067");
	КонецЕсли; 
	НовыйДокументКорректировка.СтатьяПрочихДоходовИРасходов = СтатьяПрочихДоходовИРасходов; 
	
	НовыйДокументКорректировка.ОтражатьВБухгалтерскомУчете 		= Истина;
	НовыйДокументКорректировка.ОтражатьВНалоговомУчете 			= Истина;
	НовыйДокументКорректировка.ОтражатьВУправленческомУчете 	= Истина;
	НовыйДокументКорректировка.ВидОперации 						= Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение;
	НовыйДокументКорректировка.КорректироватьБУиНУ 				= Истина; 
	НовыйДокументКорректировка.СчетУчетаРасчетовПоАвансам		= ПланыСчетов.Хозрасчетный.НайтиПоКоду("62.02");
	НовыйДокументКорректировка.СчетУчетаРасчетовСКонтрагентом 	= ПланыСчетов.Хозрасчетный.НайтиПоКоду("62.01");
	НовыйДокументКорректировка.БанковскийСчетОрганизации 		= НовыйДокументКорректировка.Организация.ОсновнойБанковскийСчет;
	НовыйДокументКорректировка.Ответственный 					= ПараметрыСеанса.ТекущийПользователь;
	
	// ПРОЕКТ И ПОДРАЗДЕЛЕНИЕ 
	Аналитика = АналитикаРегионов.Найти(СтрокиИерархии.СубъектРФ, "СубъектРФ");
	Если  Аналитика <> Неопределено Тогда
		НовыйДокументКорректировка.Подразделение 			= Аналитика.Подразделение;
		НовыйДокументКорректировка.ПодразделениеОрганизации = Аналитика.ПодразделениеОрганизации;
		НовыйДокументКорректировка.Проект 					= Аналитика.Проект;
	КонецЕсли;
	
	мПодразделение      		= НовыйДокументКорректировка.Подразделение;
	мПериодКорректировки		= Формат(НовыйДокументКорректировка._ПериодКорректировки, "ДФ='ММММ гггг'");
	мДопЛист            		= НовыйДокументКорректировка._ДопЛист;
	мОтражаетсяВКвартале	 	= Формат(НовыйДокументКорректировка._ОтражаетсяВКвартале, "ДФ='к""кв.""гггг'");
	мЗнак               		= НовыйДокументКорректировка._Знак;
	мКнига              		= НовыйДокументКорректировка._Книга;

	НовыйДокументКорректировка.Комментарий = 
											СтрШаблон(
												"Корректировка реализации ТЭ (%1) за %2, %3 к %4, знак %5, %6",
												мПодразделение,
												мПериодКорректировки,
												мДопЛист,
												мОтражаетсяВКвартале,
												мЗнак,
												мКнига
											);

	Если ПроводитьДокументы Тогда
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	Иначе
		РежимЗаписи = РежимЗаписиДокумента.Запись;
	КонецЕсли;
	
    Попытка
		
		НовыйДокументКорректировка.Записать(РежимЗаписи);  
		
		Если СоздаватьСчетаФактуры и БезСчетаФактуры = Ложь Тогда
			ОбъектСчетаФактуры = Документы.СчетФактураВыданный.СоздатьДокумент();
			ОбъектСчетаФактуры.Заполнить(НовыйДокументКорректировка.Ссылка); 
			ОбъектСчетаФактуры.Записать(РежимЗаписи);
			УчетНДС.ПроверитьСоответствиеРеквизитовСчетаФактуры(НовыйДокументКорректировка, "СчетФактураВыданный", Ложь, ОбъектСчетаФактуры.Ссылка);
		КонецЕсли;
		
        ЗафиксироватьТранзакцию();
		
    Исключение
		
		ВсеХорошо = Ложь; 
		
		Сообщить(
		    СтрШаблон(
				        "НЕ УДАЛОСЬ создать корректирующий документ: строки файла %7, %1 за %2, %3 к %4, знак %5, %6. ",
				        мПодразделение,
				       	мПериодКорректировки,
				        мДопЛист,
				        мОтражаетсяВКвартале,
				        мЗнак,
				        мКнига,
				        СтрокиИерархии.СтрокиФайлаТекст
					)  
			+
			СтрШаблон(	
						"Сумма: %1 (НДС: %2, всего: %3)",
				        Формат(СтрокиИерархии.СтрокаДокумент.Сумма, "ЧДЦ=2"),
				        Формат(СтрокиИерархии.СтрокаДокумент.СуммаНДС, "ЧДЦ=2"),
				        Формат(СтрокиИерархии.СтрокаДокумент.СуммаСНДС, "ЧДЦ=2")
	    			)
			+ ОписаниеОшибки()        
				);	 
		
		ОтменитьТранзакцию();
		
	КонецПопытки;	
	
	Если ВсеХорошо Тогда
		
		СтруктруаРезультат.КорректирующийДокумент = НовыйДокументКорректировка.Ссылка;
		
		Сообщить(
		    СтрШаблон(
				        "Создан корректирующий документ: строки файла %7, %1 за %2, %3 к %4, знак %5, %6. ",
				        мПодразделение,
				       	мПериодКорректировки,
				        мДопЛист,
				        мОтражаетсяВКвартале,
				        мЗнак,
				        мКнига,
				        СтрокиИерархии.СтрокиФайлаТекст
					)  
			+
			СтрШаблон(	
						"Сумма: %1 (НДС: %2, всего: %3)",
				        Формат(СтрокиИерархии.СтрокаДокумент.Сумма, "ЧДЦ=2"),
				        Формат(СтрокиИерархии.СтрокаДокумент.СуммаНДС, "ЧДЦ=2"),
				        Формат(СтрокиИерархии.СтрокаДокумент.СуммаСНДС, "ЧДЦ=2")
	    			)
				);				
		
	КонецЕсли;  
	
	СтруктруаРезультат.ВсеХорошо = ВсеХорошо;
	Возврат СтруктруаРезультат;

КонецФункции

Функция СвернутьМассив(Знач Массив)
	
	СвернутыйМассив = Новый Массив;
	Для каждого Элемент Из Массив Цикл
		Если СвернутыйМассив.Найти(Элемент) = Неопределено Тогда
			СвернутыйМассив.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;
	Возврат СвернутыйМассив;
	
КонецФункции

Функция ПолучитьСписокСтрокФайлаСтрокой(СтрокаДокументРеализации)

	Результат = "";
	Для каждого Строка Из СтрокаДокументРеализации.Строки Цикл
		Результат = Результат + ?(Результат ="", "", "; ") + Строка.НомерСтрокиXl;
	КонецЦикла;	
	
	Результат = "[" + Результат + "]";
	
	Возврат Результат;

КонецФункции

Функция КорректируемыйДокументПоследнийВЦепочке(СтрокаДокументРеализации)
	
	СтруктруаРезультат = Новый Структура;
	СтруктруаРезультат.Вставить("ЯвляетсяПоследним", 	Ложь);
	СтруктруаРезультат.Вставить("РезультатПоиска", 		Неопределено);
	
	Если НЕ СтрокаДокументРеализации = Неопределено Тогда
		СтруктураКорректируемыйДокумент = НайтиПоследнююКорректировку(СтрокаДокументРеализации.ДокументРеализации);
		Если СтруктураКорректируемыйДокумент.КорректируемыйДокумент = СтрокаДокументРеализации.КорректируемыйДокумент Тогда
			СтруктруаРезультат.ЯвляетсяПоследним = Истина;
		КонецЕсли;
		СтруктруаРезультат.РезультатПоиска = СтруктураКорректируемыйДокумент;
	КонецЕсли;

	Возврат СтруктруаРезультат;	
	
КонецФункции // ()

Процедура КоманднаяПанель2СоздатьКорректировочныйДокументДляВыбранныхСтрок(Кнопка)

	СписокСтрокДляОбработки = ПолучитьСписокСтрокСОтметкойПакетнойОбработки();	
	
	Если СписокСтрокДляОбработки.Количество() = 0  Тогда
		Предупреждение("Не выбрано ни одного документа");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = 
	"1. При массовом создании документов,
	|	для последовательного их включения в структуру подчиненности, 
	|	будут проверены и при необходимости заново автоматически подобраны 
	|	КОРРЕКТИРУЕМЫЕ документы.
	|2. При замене корректируемого документа 
	|	будут автоматически заново подобраны и его СТРОКИ.
	|3. В случае неоднозначного автоматического подбора документ будет пропущен 
	|	с сохранением установленного флажка ""пакетная обработка"".
	|4. После ручного выбора можно будет продолжить с места остановки 
	|	(обработать пропущенные документы), если не закрывать окно обработки.
	|
	|Выполнять?
	|";
	
	Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	//Для каждого Строка Из СписокСтрокДляОбработки Цикл
	//	ЭлементыФормы.СгруппированныеДанные.ТекущаяСтрока = Строка;
	//	СтрокаОбработана = Ложь;
	//	КомандаСоздатьКорректировочныйДокумент(Строка, СтрокаОбработана);
	//	Строка.ПакетнаяОбработка = НЕ СтрокаОбработана;
	//КонецЦикла;
	
	Для каждого Строка Из СписокСтрокДляОбработки Цикл 
			
	    ЭлементыФормы.СгруппированныеДанные.ТекущаяСтрока = Строка;
	    СтрокаОбработана = Ложь;
	    
	    Попытка
	        // Явно указываем, что это пакетный режим (РежимВызоваОдиночный = Ложь)
	        КомандаСоздатьКорректировочныйДокумент(Строка, СтрокаОбработана, , Ложь);
	    Исключение
	        Если СтрНайти(ОписаниеОшибки(), "ПакетнаяОбработкаПрервана") > 0 Тогда
	            Сообщить("Пакетная обработка прервана по желанию пользователя.");
	            Прервать;
	        Иначе
	            // Если это другая ошибка, показываем ее и продолжаем?
	            // Или тоже прерываем? Давайте пока прервем, но сообщим
	            Сообщить("Необработанная ошибка: " + ОписаниеОшибки());
	            Прервать; // Можно также просто ВызватьИсключение, если хотим жесткую остановку
	        КонецЕсли;
	    КонецПопытки;
	    
	    // Снимаем флажок только если документ успешно создан
	    Строка.ПакетнаяОбработка = НЕ СтрокаОбработана;
		
	КонецЦикла;	
	
КонецПроцедуры  

Процедура КомандаКнопкиСоздатьКорректировочныйДокументБезСФ(Кнопка)

	КомандаСоздатьКорректировочныйДокумент(,,Истина);

КонецПроцедуры

#КонецОбласти


#Область ФормаПереноса
	
Перем ПереносимаяСтрока Экспорт;
Перем СгруппированныеДанныеОсн Экспорт;

Процедура ПриОткрытии()
	
	Если ВладелецФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Получаем текущую выбранную строку из основной формы
	ПереносимаяСтрока = ВладелецФормы.ЭлементыФормы.СгруппированныеДанные.ТекущиеДанные;
	
	// Получаем дерево из основной формы
	СгруппированныеДанныеОсн = ВладелецФормы.СгруппированныеДанные;
	
	// Заполняем дерево для выбора нового родителя
	ЗаполнитьДеревоДляПереноса();
	
	// Настраиваем кнопки
	ЭлементыФормы.Перенести.Доступность = Ложь;
	
КонецПроцедуры

Процедура ЗаполнитьДеревоДляПереноса()
	
	// Создаем новое дерево
	НовоеДерево = Новый ДеревоЗначений;
	
	// Копируем колонки из основного дерева
	Для Каждого Колонка Из СгруппированныеДанныеОсн.Колонки Цикл
		НовоеДерево.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
	КонецЦикла;
	
	// Рекурсивно копируем только до уровня "ДокументРеализации"
	КопироватьДоУровня(СгруппированныеДанныеОсн, НовоеДерево, "ДокументРеализации");
	
	// Присваиваем дерево элементу формы
	ЭлементыФормы.СгруппированныеДанныеПеренос.Значение = НовоеДерево;
	
	// Принудительно обновляем отображение
	ЭлементыФормы.СгруппированныеДанныеПеренос.СоздатьКолонки();
	
	НастроитьЭлементФормы();
	
КонецПроцедуры

Процедура НастроитьЭлементФормы()
	
	// Получаем элемент из основной формы
	ОснЭлемент = ВладелецФормы.ЭлементыФормы.СгруппированныеДанные;
	НашЭлемент = ЭлементыФормы.СгруппированныеДанныеПеренос;
	
	Для Каждого Колонка Из НашЭлемент.Колонки Цикл
		КолонкаОсн = ОснЭлемент.Колонки.Найти(Колонка.Имя);
		Если КолонкаОсн <> Неопределено Тогда
			Колонка.Видимость = КолонкаОсн.Видимость;
			Колонка.Ширина = КолонкаОсн.Ширина;
			Колонка.Положение = КолонкаОсн.Положение;
			Колонка.ТекстШапки = КолонкаОсн.ТекстШапки;
			Колонка.Формат = КолонкаОсн.Формат;
			Колонка.ГоризонтальноеПоложениеВКолонке  = КолонкаОсн.ГоризонтальноеПоложениеВКолонке;
			Колонка.ШрифтТекста = КолонкаОсн.ШрифтТекста;
			Колонка.ЦветФонаПоля = КолонкаОсн.ЦветФонаПоля;
		КонецЕсли;
	КонецЦикла;
	
	
КонецПроцедуры

Процедура КопироватьДоУровня(ИсходнаяВетка, ЦелеваяВетка, ИмяУровня)
	
	Для Каждого СтрокаИсх Из ИсходнаяВетка.Строки Цикл
		
		// Копируем строку
		НоваяСтрока = ЦелеваяВетка.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсх);
		
		// Если это нужный уровень - не копируем подчиненные
		Если СтрокаИсх.Уровень = ИмяУровня Тогда
			Продолжить;
		КонецЕсли;
		
		// Иначе рекурсивно копируем детей
		Если СтрокаИсх.Строки.Количество() > 0 Тогда
			КопироватьДоУровня(СтрокаИсх, НоваяСтрока, ИмяУровня);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СтрокаЯвляетсяРодителем(ПотенциальныйРодитель, Ребенок)
	
	ТекРодитель = Ребенок.Родитель;
	
	Пока ТекРодитель <> Неопределено Цикл
		Если ТекРодитель = ПотенциальныйРодитель Тогда
			Возврат Истина;
		КонецЕсли;
		ТекРодитель = ТекРодитель.Родитель;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура Перенести(Кнопка)
	
	// Получаем целевой документ (новый родитель) из дерева формы переноса
	ЦелеваяСтрокаИзПереноса = ЭлементыФормы.СгруппированныеДанныеПеренос.ТекущиеДанные;
	
	// Проверяем, что не переносим в того же родителя
	Если ПереносимаяСтрока.Родитель.УИДСтроки = ЦелеваяСтрокаИзПереноса.УИДСтроки Тогда
		Предупреждение("Строка уже находится в этом документе");
		Возврат;
	КонецЕсли;
	
	// !!! ИЩЕМ ТАКУЮ ЖЕ СТРОКУ В ОСНОВНОМ ДЕРЕВЕ !!!
	// Находим родителя в основном дереве по тем же реквизитам
	ЦелеваяСтрокаОсн = Неопределено;
	
	//СгруппированныеДанныеОсн = ВладелецФормы.ЭлементыФормы.СгруппированныеДанные.Значение;
	
	// Перебираем корневые строки основного дерева
	Для Каждого Строка1 Из ВладелецФормы.ЭлементыФормы.СгруппированныеДанные.Значение.Строки Цикл
		Для Каждого Строка2 Из Строка1.Строки Цикл
			Для Каждого Строка3 Из Строка2.Строки Цикл
				Для Каждого Строка4 Из Строка3.Строки Цикл
					Если Строка4.УИДСтроки = ЦелеваяСтрокаИзПереноса.УИДСтроки
					Тогда
						ЦелеваяСтрокаОсн = Строка4;
						Прервать; 
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если ЦелеваяСтрокаОсн = Неопределено Тогда
		Предупреждение("Не удалось найти целевой документ");
		Возврат;
	КонецЕсли;
	
	// Запоминаем старого родителя
	СтарыйРодитель = ПереносимаяСтрока.Родитель;
	
	// СОЗДАЕМ НОВУЮ СТРОКУ в целевом родителе ОСНОВНОГО дерева
	НоваяСтрока = ЦелеваяСтрокаОсн.Строки.Добавить();
	
	// КОПИРУЕМ реквизиты
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ПереносимаяСтрока);
	
	//// КОПИРУЕМ дочерние строки
	//Для Каждого ДочерняяСтрока Из ПереносимаяСтрока.Строки Цикл
	//	НоваяДочерняя = НоваяСтрока.Строки.Добавить();
	//	ЗаполнитьЗначенияСвойств(НоваяДочерняя, ДочерняяСтрока);
	//КонецЦикла;
	
	// УДАЛЯЕМ старую строку
	СтарыйРодитель.Строки.Удалить(ПереносимаяСтрока);
	
	//// Обновляем отображение в основной форме
	ВладелецФормы.Обновить();
	
	// Закрываем форму
	ЭтаФорма.Закрыть();
	
	// Пересчитываем ветку СТАРОГО родителя (откуда убрали)
	ВладелецФормы.ПересчитатьИтогиВверхПоИерархии(СтарыйРодитель);

	// Пересчитываем ветку НОВОГО родителя (куда добавили)
	ВладелецФормы.ПересчитатьИтогиВверхПоИерархии(ЦелеваяСтрокаОсн);

КонецПроцедуры

Процедура СгруппированныеДанныеПереносПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.Уровень = "Договор" Тогда
		
		ЦветФона = Новый Цвет(248, 242, 216);
		ОформлениеСтроки.ЦветФона = ЦветФона;
		ЖирныйШрифт = новый Шрифт("Arial", 8, Истина, Ложь, Ложь, Ложь); //имя,размер,полужирный,наклонный,подчеркивание,зачеркивание
		ОформлениеСтроки.Шрифт = ЖирныйШрифт ;
		
	КонецЕсли;                
	
	Если ДанныеСтроки.Уровень = "ВидПерерасчёта" Тогда
		
		ЦветФона = Новый Цвет(251, 249, 236);
		ОформлениеСтроки.ЦветФона = ЦветФона;
		
		ЖирныйШрифт = новый Шрифт("Arial", 8, Истина, Ложь, Ложь, Ложь); //имя,размер,полужирный,наклонный,подчеркивание,зачеркивание
		ОформлениеСтроки.Шрифт = ЖирныйШрифт ;
		
	КонецЕсли;    
	
	Если ДанныеСтроки.Уровень = "ПериодРасчета" Тогда
		
		ЦветФона = Новый Цвет(246, 246, 255);
		ОформлениеСтроки.ЦветФона = ЦветФона;
		
		ЖирныйШрифт = новый Шрифт("Arial", 8, Истина, Ложь, Ложь, Ложь); //имя,размер,полужирный,наклонный,подчеркивание,зачеркивание
		ОформлениеСтроки.Шрифт = ЖирныйШрифт ;
		
	КонецЕсли; 	
	
	Если ДанныеСтроки.Уровень = "ДокументРеализации" Тогда
		
		ЦветФона = Новый Цвет(251, 251, 255);
		ОформлениеСтроки.ЦветФона = ЦветФона;
		
		ЖирныйШрифт = новый Шрифт("Arial", 8, Истина, Ложь, Ложь, Ложь); //имя,размер,полужирный,наклонный,подчеркивание,зачеркивание
		ОформлениеСтроки.Шрифт = ЖирныйШрифт ;
		
		Если Не ЭлементыФормы.СгруппированныеДанныеПеренос.Колонки.Найти("КоличествоНайденныхКорректировок") = Неопределено  Тогда
			Если ДанныеСтроки.КоличествоНайденныхКорректировок > 1 Тогда
				ЦветФона = Новый Цвет(255, 255, 0);
				ОформлениеСтроки.Ячейки.КоличествоНайденныхКорректировок.ЦветФона = ЦветФона;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДанныеСтроки.Уровень = "СтрокаКорректируемогоДокумента" Тогда
		
		ЦветТекста = Новый Цвет(100, 100, 255);
		ОформлениеСтроки.ЦветТекста = ЦветТекста;
		
	КонецЕсли; 	
	
	
КонецПроцедуры

Процедура СгруппированныеДанныеПереносПриАктивизацииСтроки(Элемент)
	
	ТекСтрока = ЭлементыФормы.СгруппированныеДанныеПеренос.ТекущиеДанные;
	
	// Активируем кнопку только для уровня "ДокументРеализации"
	Если ТекСтрока <> Неопределено 
	   И ТекСтрока.Уровень = "ДокументРеализации"
	Тогда
		ЭлементыФормы.Перенести.Доступность = Истина;
	Иначе
		ЭлементыФормы.Перенести.Доступность = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
